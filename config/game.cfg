// Game specific config stuff goes here

zoomtoggle = 0
dozoom = [if $zoomtoggle [zoom (? $zoom -1 1)] [zoom 1 ; onrelease [zoom -1]]]

sayteamcommand = [inputcommand $arg1 [sayteam $commandbuf] "[team]"]

mapcomplete = [complete $arg1 media/map ogz]
mapcomplete map

modenames = "edit rdm pdm rtdm ptdm rctf pctf"

loop i (listlen $modenames) [
    mname = (at $modenames $i)
    alias $mname [if (mode @i) [if (> $numargs 0) [map $arg1] [map]]]
    mapcomplete $mname
]

demo = [stopdemo; if (mode -1) [map $arg1]]
complete demo . dmo

allowedittoggle = [result 1]

playermodelnum = 1

playermodeldir0   = "bones"
playermodelicon0  = "bones"
playermodelname0  = "Bones"
playermodelstory0 = [Half cyborg, half american rugby player, full fragging machine.]

playermodeldir   = [getalias (concatword "playermodeldir" (if (> $numargs 0) [result $arg1] [result $playermodel]))]
playermodelicon  = [getalias (concatword "playermodelicon" (if (> $numargs 0) [result $arg1] [result $playermodel]))]
playermodelname  = [getalias (concatword "playermodelname" (if (> $numargs 0) [result $arg1] [result $playermodel]))]
playermodelstory = [getalias (concatword "playermodelstory" (if (> $numargs 0) [result $arg1] [result $playermodel]))]

texsky      = [setshader "stdworld"; texture 0 "game/sky.png"]
texerror    = [setshader "stdworld"; texture 0 "game/notexture.png"]
texnorender = [setshader "stdworld"; texture 0 "game/norender.png"]
texload = [
    if (findfile [media/texture/@arg1.tex]) [
        exec [media/texture/@arg1.tex]
    ] [
        error [texload error: @arg1.tex not found]
        texerror
    ]
]
