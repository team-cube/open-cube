// Game specific config stuff goes here

defvarp showip 0 0 1

defvarp zoomtoggle 0 0 1
dozoom = [if $zoomtoggle [zoom (? $zoom -1 1)] [zoom 1 ; onrelease [zoom -1]]]

sayteamcommand = [inputcommand $arg1 [sayteam $commandbuf] "[team]"]

mapcomplete = [complete $arg1 media/map ogz]
mapcomplete map

modenames = "edit rdm pdm rtdm ptdm rctf pctf"

loop i (listlen $modenames) [
   mname = (at $modenames $i)
   alias $mname [if (mode @i) [if (> $numargs 0) [map $arg1] [map]]]
   mapcomplete $mname
]

demo = [stopdemo; if (mode -1) [map $arg1]]
complete demo . dmo

allowedittoggle = [result 1]

playermodelnum = 1

playermodeldir0   = "bones"
playermodelicon0  = "bones"
playermodelname0  = "Bones"
playermodelstory0 = [Half cyborg, half american rugby player, full fragging machine.]

playermodeldir   = [getalias [playermodeldir@(? $numargs $arg1 $playermodel)]]
playermodelicon  = [getalias [playermodelicon@(? $numargs $arg1 $playermodel)]]
playermodelname  = [getalias [playermodelname@(? $numargs $arg1 $playermodel)]]
playermodelstory = [getalias [playermodelstory@(? $numargs $arg1 $playermodel)]]
