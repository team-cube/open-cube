//Creates a basic menu with no title nor close button
//args: name, contents, onshow, onhide, children, maybetitle
UIMenuNoTitle = [
    newui $arg1 [
        uivlist 0 [
            uiclamp 1 1 1 1
            @@arg6
            uicolor @@(? $arg7 $arg7 0xEE080808) 0.04 0.04 [
                uiclamp 1 1 1 1
                uioutline 0x343332
                    uiclamp- 1 1 1 1
                uispace 0.01 0.01 [
                    uiclamp 1 1 1 1
                    @@@@arg2
                ]
            ]
        ]
        @arg5
    ] $arg3 $arg4
]

//Creates a basic menu with a title, but without a close button
//args: name, contents, title, onshow, onhide, children, extratitle
UIMenuNoClose = [
    UIMenuNoTitle $arg1 $arg2 $arg4 $arg5 $arg6 [
        uivgradient 0xEE1A1A1A 0xEE0C0C0C 0.04 0.04 [
            uiclamp 1 1 0 0
            uioutline 0x343332
                uiclamp- 1 1 1 1
            uitext [@@@arg3] 0.8
            @@arg7
            uispace 0.01 0 [
                uialign 1 0
                UICloseButton [@@@@arg1]
            ]
        ]
    ]
]

//Creates a basic menu with a title and a close button
//args: name, contents, title, onshow, onhide, children, extratitle
UIMenu = [
    UIMenuNoClose $arg1 $arg2 $arg3 $arg4 $arg5 $arg6 [
        uispace 0.01 0 [
            uialign 1 0
            UICloseButton [@@@arg1]
        ]
        @arg7
    ]
]

//Creates a basic menu with a title and a close button, auto-hides outside of edit mode
//args: name, contents, title, onshow, onhide, children, extratitle
UIEditMenu = [
    UIMenu $arg1 [if $editing [@@arg2] [hideui [@@@arg1]]] $arg3 $arg4 $arg5 $arg6 $arg7
]
 
UICloseButton = [
    uifill 0.025 0.025 [
        uialign 1 0
        uihover+ [uicolor 0x585858] [uicolor 0x1A1A1A]
            uiclamp- 1 1 1 1
        uitext "X" 0.7
        uihold [uimodcolor 0xB3B3B3; uiclamp- 1 1 1 1]
        uioutline 0x4f4d4c
            uiclamp- 1 1 1 1
        uirelease [hideui $arg1]
    ]
]

//creates a button with a centred text element
//args: text, action, children
UIButton = [
    uifill 0.16 0.04 [
        uiclamp 1 1 0 0
        uihover+ [uicolor 0x585858] [uicolor 0x1A1A1A]               
            uiclamp- 1 1 1 1
        uispace 0.01 0 [
            uitext $arg1 0.8
        ]
        uihold [uimodcolor 0xB3B3B3; uiclamp- 1 1 1 1]
        uioutline 0x4f4d4c
            uiclamp- 1 1 1 1
        uirelease [arg2]
        arg3
    ]
]

//args: var, onchange, children
UICheckBox = [
    uifill 0.03 0.03 [
        uihover+ [uicolor 0x585858] [uicolor 0x1A1A1A]
            uiclamp- 1 1 1 1
        if $$arg1 [
            uispace 0.005 0.005 [
                uiclamp 1 1 1 1
                uicolor 0xA0A0A0
                    uiclamp- 1 1 1 1 
            ]
        ]
        uihold [uimodcolor 0xB3B3B3; uiclamp- 1 1 1 1]
        uioutline 0x4f4d4c
            uiclamp- 1 1 1 1
        uirelease [
            $arg1 = (! $$arg1)
            arg2
        ]
        arg3
    ]
]

//args: var, type, val, onchange, children
UIRadio = [
    uifill 0.03 0.03 [
        uihover+ [uicircle 0x585858] [uicircle 0x1A1A1A]
            uiclamp- 1 1 1 1
        if ($arg2 $$arg1 $arg3) [
            uispace 0.005 0.005 [
                uiclamp 1 1 1 1
                uicircle 0xA0A0A0
                uicircleoutline 0xA0A0A0
                uiclamp* 1 1 1 1
            ]
        ]
        uihold [uimodcircle 0xB3B3B3; uiclamp- 1 1 1 1]
        uicircleoutline 0x4f4d4c
            uiclamp- 1 1 1 1
        uirelease [
            $arg1 = $arg3
            arg4
        ]
        arg5
    ]
]

//args: size, orient
UIRadioSliderArrow = [
    uigroup [
        uihover [uicolor 0x585858] [uicolor 0x1A1A1A]
            uiclamp- 1 1 1 1
        uitriangle 0x999999 (*f $arg1 0.6) (*f $arg1 0.7) (* $arg2 90)
        uitriangleoutline 0x999999 (*f $arg1 0.6) (*f $arg1 0.7) (* $arg2 90)
        uihold [uimodcolor 0xB3B3B3; uiclamp- 1 1 1 1]
        uioutline 0x343332 $arg1 $arg1
            uiclamp- 1 1 1 1
        uirelease 1
    ]
]

//Creates a horizontal slider for adjusting a variable
//args: var, comparison, values, textsize, onchange, children
UIRadioSlider = [
    uihlist 0 [
        if (UIRadioSliderArrow $uitextscale 1) [
            n = ([listfind@arg2] $arg3 $$arg1 1)
            if (>= $n 0) [
                n = (- $n 2)
                if (< $n 0) [n = (+ $n (listlen $arg3))]
            ] [n = 0]
            $arg1 = (at $arg3 $n)
            arg5
        ]
        uitextfill $arg4 1 [
            uicolor 0x080808
                uiclamp- 1 1 1 1
            uioutline 0x343332
                uiclamp- 1 1 1 1
            uitext (|| [[listassoc@arg2] $arg3 $$arg1] [result custom])
            arg6
        ]
        if (UIRadioSliderArrow $uitextscale 3) [
            n = ([listfind@arg2] $arg3 $$arg1 1)
            if (>= $n 0) [
                n = (mod (+ $n 2) (listlen $arg3))
            ] [n = (- (listlen $arg3) 2)]
            $arg1 = (at $arg3 $n)
            arg5
        ]
    ]
]

//creates a conditional button with a centred text element
//args: text, cond, action, children
UICondButton = [
    uifill 0.16 0.04 [
        uiclamp 1 1 0 0
        if (arg2) [
            uihover+ [uicolor 0x585858] [uicolor 0x1A1A1A]
                uiclamp- 1 1 1 1
            uispace 0.01 0 [
                uitext $arg1 0.8
            ]
            uihold [uimodcolor 0xB3B3B3; uiclamp- 1 1 1 1]
            uirelease [arg3]
        ] [
            uicolor 0
                uiclamp- 1 1 1 1
            uispace 0.01 0 [
                uitext $arg1 0.8
            ]
            uimodcolor 0x4D4D4D
                uiclamp- 1 1 1 1
        ]
        uioutline 0x4f4d4c
            uiclamp- 1 1 1 1
        arg4
    ]
]

//args: step, size, orient
UIScrollArrow = [
    uiscrollarrow $arg1 [
        uihover [uicolor 0x585858] [uicolor 0x1A1A1A]               
            uiclamp- 1 1 1 1
        uitriangle 0x999999 (*f $arg2 0.7) (*f $arg2 0.6) (* $arg3 90)
        uitriangleoutline 0x999999 (*f $arg2 0.7) (*f $arg2 0.6) (* $arg3 90)
        uihold [uimodcolor 0xB3B3B3; uiclamp- 1 1 1 1]
        uioutline 0x343332 $arg2 $arg2
            uiclamp- 1 1 1 1
    ]
]

//Creates a uiscroll area with a uivscrollbar at the right
//args: X-Size, Y-Size, ArrowSize, ScrollSpeed, Contents
UIVScroller = [
    uihlist 0.01 [
        uiscroll (-f $arg1 $arg3) $arg2 [
            uifill (-f $arg1 $arg3) $arg2
            arg5
        ]
        uivlist 0 [
            UIScrollArrow (-f 0 $arg4) $arg3 0
            uivscrollbar [
                uicolor 0x080808 $arg3 (-f $arg2 (*f $arg3 2)) [
                    uiclamp 1 1 1 1
                    uioutline 0x343332
                        uiclamp- 1 1 1 1
                ]
                uiscrollbutton [
                    uihover [uicolor 0x585858] [uicolor 0x1A1A1A]     
                        uiclamp- 1 1 1 1
                    uihold [uimodcolor 0xB3B3B3; uiclamp- 1 1 1 1]
                    uioutline 0x343332 $arg3 $arg3
                        uiclamp- 1 1 1 1
                ]
            ]
            UIScrollArrow $arg4 $arg3 2
        ]
    ]
]

//args: StepSize, ArrowSize, Orient
UISliderArrow = [
    uisliderarrow $arg1 [
        uihover [uicolor 0x585858] [uicolor 0x1A1A1A]     
            uiclamp- 1 1 1 1
        uitriangle 0x999999 (*f $arg2 0.6) (*f $arg2 0.7) (* $arg3 90)
        uitriangleoutline 0x999999 (*f $arg2 0.6) (*f $arg2 0.7) (* $arg3 90)
        uihold [uimodcolor 0xB3B3B3; uiclamp- 1 1 1 1]
        uioutline 0x343332 $arg2 $arg2
            uiclamp- 1 1 1 1
    ]
]

//Creates a horizontal slider for adjusting a variable
//args: Var, Min, Max, OnChange, Length, ArrowSize, StepSize, Children
UIHSlider = [
    uihlist 0 [
        UISliderArrow (-f 0 $arg7) $arg6 1
        uihslider $arg1 $arg2 $arg3 $arg7 $arg4 [
            uicolor 0x080808 (-f $arg5 (*f $arg6 2)) $arg6 [
                uiclamp 1 1 1 1
                uioutline 0x343332
                    uiclamp- 1 1 1 1
            ]
            uisliderbutton [
                uihover [uicolor 0x585858] [uicolor 0x1A1A1A]     
                    uiclamp- 1 1 1 1
                uispace 0.001 0 [
                    uitext $$arg1 (minf 1 (*f $arg6 40))
                ]
                uihold [uimodcolor 0xB3B3B3; uiclamp- 1 1 1 1]
                uioutline 0x343332 $arg6 $arg6
                    uiclamp- 1 1 1 1
            ]
            arg8
        ]
        UISliderArrow $arg7 $arg6 3
    ]
]
    
