// ui_menu "NAME" [children] [on-show] [on-hide] [ui-children] [title-children] [button-area-children]
ui_menu = [
   newui $arg1 [
      @arg5
      uivlist 0 [
         uiclamp 1 1 1 1
         @@(? (>= $numargs 6) [
            ui_menutitle [uiclamp 1 1 ; @@arg6] 0 0 [
               @@arg7
               ui_button "hold2" [uitext "X" 0.72] 0 0 [hideui [@@@@arg1]] 0 [
                  uihover [uicolor $_c_menu]
               ] 0.008
            ]
         ])
         uicolor $_c_menu 0.01 0.01 [
            uiclamp 1 1 1 1
            uispace $_ui_window_padding $_ui_window_padding [
               uiclamp 1 1 1 1
               @@@@arg2
            ]
            uioutline $_c_line
            uiclamp- 1 1 1 1
         ]
      ]
   ] $arg3 $arg4
]

// ui_panel "NAME" [children] [on-show] [on-hide] [ui-children]
ui_panel = [
   newui $arg1 [
      @arg5
      uicolor $_c_menu_a 0.01 0.01 [
         uispace $_ui_window_padding $_ui_window_padding [
            uiclamp 1 1 1 1
            @@@arg2
         ]
         uioutline $_c_line
         uiclamp- 1 1 1 1
      ]
      uiclamp* 1 1 1 1
   ] $arg3 $arg4
]

// ui_window "NAME" [children] [on-show] [on-hide] [ui-children]
ui_window = [
   newui $arg1 [
      @arg5
      uiallowinput 0
      uifill 0.01 0.01 [
         uiclamp 1 1 1 1
         @@arg2
      ]
   ] $arg3 $arg4
]

// ui_bar CLAMP-X CLAMP-Y
ui_bar = [
   uiline $_c_line
   uiclamp- $arg1 $arg1 $arg2 $arg2
]

// ui_iso_triangle 0xCOLOR X Y (ORIENTATION) [children]
ui_iso_triangle = [
   uigroup [
      uitriangle (? $arg1 $arg1 $_c_fill) $arg2 $arg3 $arg4 [arg5]
      uitriangleoutline $_c_line $arg2 $arg3 $arg4
   ]
]

// ui_title [title children] X Y
ui_title = [
   uivgradient $_c_line $_c_menu $arg2 $arg3 [
      arg1
      uioutline $_c_line
      uiclamp- 1 1 1 1
   ]
]

// ui_menutitle [title children] X Y [button-area-children]
ui_menutitle = [
   arg3 = (? $arg3 $arg3 0.04)
   uivgradient $_c_main1 $_c_main2 $arg2 $arg3 [
      arg1
      if (>= $numargs 4) [
         uispace $_ui_button_padding $_ui_button_padding [
            uialign 1
            uihlist $_ui_button_padding [arg4]
         ]
      ]
      uioutline $_c_line
      uiclamp- 1 1 1 1
   ]
]

// ui_area (STYLE(S) X Y [children]
ui_area = [
   uitarget $arg2 $arg3 [
      if (=s $arg1 "") [
         _style_generic_default
      ] [looplist i $arg1 [[_style_generic_@i]]]
      uiclamp* 1 1 1 1
      arg4
   ]
]

// ui_button (STYLE(S)) (BUTTON-WIDTH) (BUTTON-HEIGHT) [children]
ui_buttonarea = [
   uitarget $arg2 $arg3 [
      if (=s $arg1 "") [
         _style_button_default
      ] [[_style_button_@arg1]]
      uiclamp* 1 1 1 1
      arg4
   ]
]

// ui_button (STYLE(S)) [button-children] (BUTTON-WIDTH) (BUTTON-HEIGHT) [on-release] (ALIGNMENT) [children] (USE-PADDING)
ui_button = [
   uitarget $arg3 $arg4 [
      arg7
      if (=s $arg1 "") [
         _style_button_default
      ] [[_style_button_@arg1]]
      uirelease [arg5]
      uiclamp* 1 1 1 1
      uispace (? (>= $numargs 8) $arg8 $_ui_button_padding) 0 [
         uiclamp 1 1
         uihlist 0.01 [
            uialign $arg6 0
            arg2
         ]
      ]
   ]
]

// ui_checkbox [condition] (SIZE) 0xCB-ON 0xCB-OFF [children]
ui_checkbox = [
   arg4 = (? $arg4 $arg4 $_c_off)
   arg3 = (? $arg3 $arg3 $_c_on1)
   uicolor (? (arg1) $arg3 $arg4) $arg2 $arg2 [
      uimodvgradient 0xFFFFFF 0xB0B0B0
      arg5
      uioutline $_c_line
      uiclamp* 1 1 1 1
   ]
]

// ui_radio [condition] (SIZE) 0xCB-ON 0xCB-OFF [children]
ui_radio = [
   arg4 = (? $arg4 $arg4 $_c_off)
   arg3 = (? $arg3 $arg3 $_c_on2)
   uicircleoutline (? $arg5 $arg5 0x484848) $arg2 [
      uicircle (? (arg1) $arg3 $arg4) $arg2 [
         //uimodvgradient 0xFFFFFF 0xB0B0B0// no way to limit this to a circle :c
         arg6
         uiclamp* 1 1 1 1
      ]
   ]
]

//ui_radio = [// square-ish version
//   arg4 = (? $arg4 $arg4 $_c_off)
//   arg3 = (? $arg3 $arg3 $_c_on2)
//   uicolor (? (arg1) $arg3 $arg4) $arg2 $arg2 [
//      uimodvgradient 0xFFFFFF 0xB0B0B0
//      arg5
//      uioutline $_c_line
//      uiclamp* 1 1 1 1
//   ]
//]

// ui_arrowbutton X Y (ORIENTATION) [on-release] (ARROW-%-SIZE) [children]
ui_arrowbutton = [
   arg24 = (*f (minf $arg1 $arg2) (minf 1 (maxf 0 $arg5)))
   ui_button "" [
      ui_iso_triangle 0 @arg24 @arg24 @arg3
   ] $arg1 $arg2 $arg4 0 [uicolor $_c_menu ; @arg6] 0
]

// ui_skybox "SKYBOX/PATH/HERE" Y
ui_skybox = [
   uigroup [
      arg3 = (=s $skybox $arg1)
      arg4 = (uihover?)
      uihlist 0 [
         looplist i "ft lf bk rt" [
            uiimage (format "media/sky/%1_%2.png" $arg1 $i) $arg2 $arg2
         ]
      ]
      uispace 0 (*f $arg2 0.05) [
         uialign 0 1
         uifont "default_outline" [
            uicolortext (? (=s $arg1 "") "[no skybox]" $arg1) (
               ? (|| $arg3 $arg4) 0xFFFFFF 0xA0FFFFFF
            ) 0.6
         ]
      ]
      if $arg3 [uioutline $_c_fill] [
         uirelease [skybox $arg1]
         uioutline $_c_line
      ]
      uiclamp- 1 1 1 1
   ]
]

// ui_htab X Y VAR ["name" [action] ..] <BUTTON BEHAVIOR: 0=HOVER, 1=PRESS, 2=RELEASE>
ui_htab = [
   uifill $arg1 $arg2 [
      uihlist 0 [
         uiclamp 1 1
         looplist3 v n c $arg4 [
            if (=s $$arg3 $v) [
               uimodvgradient 0xFFFFFF $_c_grdmod 0 $arg2 [
                  ui_bar 0 1 ; uialign- -1 0
                  ui_bar 0 1 ; uialign-  1 0
                  uispace 0.01 0 [uitext (concatword "^f8" $n) 0.7]
               ]
            ] [
               uitarget 0 $arg2 [
                  uispace 0.01 0 [uitext $n 0.7]
                  case $arg5 0 [
                     uihover [$arg3 = $v]
                  ] 1 [
                     uipress [$arg3 = $v]
                  ] 2 [uirelease [$arg3 = $v]]
                  do $c
               ]
            ]
         ]
         uiclamp* 1 1
      ]
      ui_bar 1 0 ; uialign- 0 1
   ]
]

// ui_vtab X Y VAR ["name" [action] ..] <BUTTON BEHAVIOR: 0=HOVER, 1=PRESS, 2=RELEASE>
ui_vtab = [
   uifill $arg1 $arg2 [
      uivlist 0 [
         uiclamp 0 0 1 1
         looplist3 v n c $arg4 [
            if (=s $$arg3 $v) [
               uimodhgradient 0xFFFFFF $_c_grdmod $arg1 0 [
                  ui_bar 1 ; uialign- 0 -1
                  ui_bar 1 ; uialign- 0  1
                  uispace 0.01 0 [uitext (concatword "^f8" $n) 0.7]
               ]
            ] [
               uitarget $arg1 0 [
                  uispace 0.01 0 [uitext $n 0.7]
                  case $arg5 0 [
                     uihover [$arg3 = $v]
                  ] 1 [
                     uipress [$arg3 = $v]
                  ] 2 [uirelease [$arg3 = $v]]
                  do $c
               ]
            ]
         ]
         uiclamp* 1 1 1 1
      ]
      ui_bar 0 1 ; uialign- 1
   ]
]

// ui_vscroll X Y (SCROLLSPEED) (ARROW-%-SIZE)
ui_vscroll = [
   arg4 = (? $arg4 $arg4 0.5)
   uivlist 0 [
      uiscrollarrow (-f 0 $arg3) [ui_arrowbutton $arg1 $arg1 0 [] $arg4]
      uivscrollbar [
         uifill (*f 0.85 $arg1) (-f $arg2 (*f $arg1 2)) [uioutline $_c_line ; uiclamp- 1 1 1 1]
         uiscrollbutton [
            uicolor $_c_menu $arg1
            _style_button_hold5
            uiclamp* 1 1 1 1
            _style_generic_linesh 3 0.004 (*f $arg1 0.5)
         ]
      ]
      uiscrollarrow $arg3 [ui_arrowbutton $arg1 $arg1 180 [] $arg4]
   ]
]

// ui_vscroll X Y (SCROLLSPEED) (ARROW-%-SIZE)
ui_hscroll = [
   arg4 = (? $arg4 $arg4 0.5)
   uihlist 0 [
      uiscrollarrow (-f 0 $arg3) [ui_arrowbutton $arg2 $arg2 90 [] $arg4]
      uihscrollbar [
         uifill (-f $arg1 (*f $arg2 2)) (*f $arg2 0.85) [uioutline $_c_line ; uiclamp- 1 1 1 1]
         uiscrollbutton [
            uicolor $_c_menu 0 $arg2
            _style_button_hold1
            uiclamp* 1 1 1 1
            _style_generic_linesv 3 0.004 (*f $arg2 0.5)
         ]
      ]
      uiscrollarrow $arg3 [ui_arrowbutton $arg2 $arg2 270 [] $arg4]
   ]
]

// ui_vslider VAR MIN MAX STEP X Y [children] [on-change] (ARROW-%-SIZE)
ui_vslider = [
   arg9 = (? $arg9 $arg9 0.5)
   uivlist 0 [
      uisliderarrow (-f 0 $arg4) [ui_arrowbutton $arg5 $arg5 0 [] $arg9]
      uihslider $arg1 (minf $arg3 $arg2) (maxf $arg2 $arg3) $arg4 $arg8 [
         uifill (-f $arg6 (*f $arg5 2)) (*f 0.85 $arg5) [uioutline $_c_line ; uiclamp- 1 1 1 1]
         uisliderbutton [
            uicolor $_c_menu $arg5
            _style_button_hold5
            uiclamp* 1 1 1 1
            uispace (? (>= $numargs 10) $arg11 $_ui_button_padding) 0 [arg7]
         ]
      ]
      uisliderarrow $arg4 [ui_arrowbutton $arg5 $arg5 180 [] $arg9]
   ]
]

// ui_hslider VAR MIN MAX STEP X Y [children] [on-change] (ARROW-%-SIZE)
ui_hslider = [
   arg9 = (? $arg9 $arg9 0.5)
   uihlist 0 [
      uisliderarrow (-f 0 $arg4) [ui_arrowbutton $arg6 $arg6 90 [] $arg9]
      uihslider $arg1 $arg2 $arg3 $arg4 $arg8 [
         uifill (-f $arg5 (*f $arg6 2)) (*f 0.85 $arg6) [uioutline $_c_line ; uiclamp- 1 1 1 1]
         uisliderbutton [
            uicolor $_c_menu 0 $arg6
            _style_button_hold1
            uiclamp* 1 1 1 1
            uispace (? (>= $numargs 10) $arg11 $_ui_button_padding) 0 [arg7]
         ]
      ]
      uisliderarrow $arg4 [ui_arrowbutton $arg6 $arg6 270 [] $arg9]
   ]
]

// ui_vcolorslider VAR MIN MAX 0xCOLOR1 0xCOLOR2 X Y [on-change]
ui_vcolorslider = [
   uigroup [
      arg24 = (|| uihover? uihold?)
      uivgradient $arg5 $arg4 (*f 0.85 $arg6) $arg7 [uioutline $_c_line ; uiclamp- 1 1 1 1]
      uifont "default_outline" [uivlist -0.01 [
         loop i (strlen (abs $$arg1)) [
            uicolortext (substr $$arg1 $i 1) (? $arg24 0x80FFFFFF 0x40FFFFFF) 0.8
         ]
      ]]
      uivslider $arg1 $arg2 $arg3 1 $arg8 [
         uifill $arg6 (*f 0.98 $arg7)
         uisliderbutton [uiline 0xFFFFFF $arg6 0]
      ]
   ]
]

// ui_hcolorslider VAR MIN MAX 0xCOLOR1 0xCOLOR2 X Y [on-change]
ui_hcolorslider = [
   uigroup [
      uihgradient $arg4 $arg5 $arg6 (*f 0.85 $arg7) [uioutline $_c_line ; uiclamp- 1 1 1 1]
      uihslider $arg1 $arg2 $arg3 1 $arg8 [
         uifill (*f 0.98 $arg6) $arg7
         uisliderbutton [uiline 0xFFFFFF 0 $arg7]
      ]
   ]
]

//ui_listslider VAR COMPARISON [values] X Y [on-change] (ARROW-%-SIZE) [children]
ui_listslider = [
   arg24 = (listlen $arg3)
   arg7  = (? $arg7 $arg7 0.5)
   uihlist 0 [
      ui_arrowbutton $arg5 $arg5 90 [
         n = ([listfind@@arg2] [@@arg3] $@arg1 1)
         if (< $n 0) [n = 0] [
            n = (- $n 2)
            if (< $n 0) [n = (+ $n @@@arg24)]
         ]
         @arg1 = (at [@@arg3] $n)
         @arg6
      ] $arg7
      ui_area "" (-f $arg4 (*f $arg5 2)) (*f $arg5 0.85) [
         if (=s @arg8 "") [
            uitext (|| [[listassoc@@@@arg2] [@@@@arg3] $@@@arg1] [result "custom"]) 0.6
         ] [@@arg8]
      ]
      //ui_button "" [// might make more sense to keep this if it actually ends up being a button for something
      //   if (=s @arg8 "") [
      //      uitext (|| [[listassoc@@@@arg2] [@@@@arg3] $@@@arg1] [result "custom"]) 0.6
      //   ] [@@arg8]
      //] (-f $arg4 (*f $arg5 2)) $arg5 []
      ui_arrowbutton $arg5 $arg5 270 [
         n = ([listfind@@arg2] [@@arg3] $@arg1 1)
         n = (if (>= $n 0) [mod (+ $n 2) @@arg24] [- @@arg24 2])
         @arg1 = (at [@@arg3] $n)
         @arg6
      ] $arg7
   ]
]

