// standard menu definitions
// don't modify, add personal menus to autoexec.cfg instead

exec "config/ui_edithud.cfg"       // Editmode HUD
exec "config/ui_fkey.cfg"          // F# Key Menus
exec "config/ui_scoreboard.cfg"    // Scoreboard
exec "config/ui_serverbrowser.cfg" // Server Browser

togglemainmenu = [|| (hidetopui) [toggleui main]]

bind ESCAPE [togglemainmenu]

///////////////////////////////////////////////////////////////////////////////
//  Menu - Main                                                              //
///////////////////////////////////////////////////////////////////////////////
UIMenuNoTitle "main" [
   uivlist 0.0075 [
      UIButton "Server Browser" [hideui main; showui server_browser]
      UIButton "Map Browser"    [hideui main; showui map_browser]
      uispace 0 0.005
      UIButton "Player Setup"   [hideui main; showui player_setup]
      UIButton "Options"        [hideui main; showui options]
      if (isconnected) [
         uispace 0 0.005
         case $getteam [
            1] [UIButton "Join ^f3Rojo" [team rojo; hideui main]] [
            2] [UIButton "Join ^f1Azul" [team azul; hideui main]]
         if (isspectator $getclientnum) [UIButton "Play" [spectator 0; hideui main]] [UIButton "Spectate" [spectator 1; hideui main]]
         UIButton "Disconnect"  [disconnect]
      ] [
         UIButton "Credits"     [hideui main; showui credits]
         uispace 0 0.005
         UIButton "Quit"        [quit]
      ]
      uiclamp* 1 1 0 0
   ]
]

///////////////////////////////////////////////////////////////////////////////
//  Menu - Player Setup                                                      //
///////////////////////////////////////////////////////////////////////////////
ps_model  = 0
ps_color  = $playercolor
ps_team   = 0
ps_weapon = 1
ps_colorbutton = [
   uifill 0.06 0.06 [
      uicolor (getplayercolor $arg2 $arg3)
         uiclamp- 1 1 1 1
      if (= $$arg1 $arg2) [
         uioutline 0xFFFFFF
            uiclamp- 1 1 1 1
      ]
      uirelease [
         $arg1 = $arg2
         ps_team = $arg3
      ]
   ]
]

UIMenu "player_setup" [
   uihlist 0 [
      uivlist 0.01 [ 
         ps_color = (case $ps_team 1 $playercolorazul 2 $playercolorrojo () $playercolor)
         uiplayerpreview $ps_model $ps_color $ps_team $ps_weapon 0.5 0.5
         uihlist 0.01 [
            UIRadio ps_team = 0; uitext "Solo" 0.6
            UIRadio ps_team = 1; uitext "Azul" 0.6
            UIRadio ps_team = 2; uitext "Rojo" 0.6
         ]
         uihlist 0.01 [
            UIRadio ps_weapon = 1; uitext "Pulse Rifle" 0.6
            UIRadio ps_weapon = 0; uitext "Railgun" 0.6
         ]
      ]
      uivlist 0.01 [
         uitext "Name" 0.7
         uifield name 15 [] 0.9 [uioutline (uifocus? 0xFF0000 0x4f4d4c); uiclamp- 1 1 1 1]
         uitext "Color: Solo" 0.7
         uigrid 4 0.006 0.006 [
            loop i 16 [ps_colorbutton playercolor $i 0]
         ]
         uitext "Color: Azul" 0.7
         uigrid 4 0.006 0.006 [
            loop i 4 [ps_colorbutton playercolorazul $i 1]
         ]
         uitext "Color: Rojo" 0.7
         uigrid 4 0.006 0.006 [
            loop i 4 [ps_colorbutton playercolorrojo $i 2]
         ]
         uialign* -1 -1
      ]
      uialign* -1 -1
   ]
] "Player Setup"

///////////////////////////////////////////////////////////////////////////////
//  Menu - Options                                                           //
///////////////////////////////////////////////////////////////////////////////
UIMenu "options" [
   hidechanges 1
   uifill 0.3 0.3
   uitable 0.01 0.01 [
      uitablerow [
         uifill 0.03 0.03; uitext "Anisotropic filtering" 0.8
         UIRadioSlider aniso = [0 "^f4off" 2 "^f12x" 4 "^f04x" 8 "^f28x" 16 "^f316x"] 4
         uialign* -1 0
      ]
      uitablerow [
         uifill 0.03 0.03; uitext "Multisample AA" 0.8
         UIRadioSlider msaa = [0 "^f4off" 2 "^f12x" 4 "^f04x" 8 "^f28x" 16 "^f316x"] 4
         uialign* -1 0
      ]
      uitablerow [
        UICheckBox tqaa; uitext "Temporal AA" 0.8
        uialign* -1 0
      ]
      uitablerow [
         UICheckBox smaa; uitext "Morphological AA" 0.8
         if $smaa [
            UIRadioSlider smaaquality = [0 "^f1low" 1 "^f0medium" 2 "^f2high" 3 "^f3ultra"] 7 [
               smaacoloredge (>= $smaaquality 3)
            ]
         ]
         uialign* -1 0
      ]
      uitablerow [
         UICheckBox fxaa; uitext "FXAA" 0.8
         if $fxaa [
            UIRadioSlider fxaaquality = [0 "^f1low" 1 "^f0medium" 2 "^f2high" 3 "^f3ultra"] 7
         ]
         uialign* -1 0
      ]
      uitablerow [
         UICheckBox vsync; uitext "V-sync" 0.8
         if $vsync [
            uihlist 0.01 [
               UICheckBox vsynctear; uitext "Tear" 0.8
            ]
         ]
         uialign* -1 0
      ]
      uitablerow [
         uifill 0.03 0.03; uitext "Gamma" 0.8
         UIHSlider gamma 30 300 [] 0.5 (*f $uitextscale 0.8) 5; uifield gamma 3 [] 0.8 [uioutline (uifocus? 0xFF0000 0x4f4d4c); uiclamp- 1 1 1 2]
         uialign* -1 0
      ]
      uitablerow [uiline 0x2C2C2C; uiclamp- 1 1; uiline 0x2C2C2C; uiclamp- 1 1; uiline 0x2C2C2C; uiclamp- 1 1; uiline 0x2C2C2C; uiclamp- 1 1]
      uitablerow [uifill 0.03 0.03; uitext "Shadow Resolution" 0.8; UIRadioSlider smsize = [10 "^f4lowest" 11 "^f1low" 12 "^f0medium" 13 "^f2high" 14 "^f3ultra"] 7; uialign* -1 0]
      uitablerow [uifill 0.03 0.03; uitext "Shadow Filtering" 0.8; UIRadioSlider smfilter = [0 "^f1low" 1 "^f0medium" 2 "^f2high" 3 "^f3ultra"] 7; uialign* -1 0]
      uitablerow [
         UICheckBox gi; uitext "Global Illumination" 0.8
         if $gi [
            UIRadioSlider rhtaps = [12 "^f1low" 20 "^f0medium" 32 "^f2high"] 7
         ]
         uialign* -1 0
      ]
      uitablerow [
        UICheckBox ao; uitext "SSAO" 0.8
        if $ao [
            UIRadioSlider aotaps = [5 "^f1low" 8 "^f0medium" 12 "^f2high"] 7 [
               aobilateral (? (>= $aotaps 8) 4 3)
            ]
        ]
        uialign* -1 0
      ]
      uitablerow [
         uifill 0.03 0.03; uitext "Water" 0.8
         uihlist 0.01 [
            UICheckBox waterreflect; uitext "Reflection" 0.8
            UICheckBox caustics; uitext "Caustics" 0.8
            UICheckBox vertwater; uitext "Animation" 0.8
         ]
         uialign* -1 0
      ]
      uitablerow [UICheckBox softparticles; uitext "Soft Particles" 0.8; uialign* -1 0]
      uitablerow [
         UICheckBox decals; uitext "Decals" 0.8
         if $decals [
            UIRadioSlider decalfade = [2000 "^f4instant fade" 15000 "^f1quick fade" 30000 "^f0moderate fade" 60000 "^f2slow fade"] 13 [maxdecaltris (? (> $decalfade 15000) 8192 2048)]
         ]; uifield decalfade 5 [maxdecaltris (? (> $decalfade 15000) 8192 2048)] 0.8 [uioutline (uifocus? 0xFF0000 0x4f4d4c); uiclamp- 1 1 1 2]
         uialign* -1 0
      ]
      uitablerow [
         UICheckBox grass; uitext "3D Grass" 0.8
         if $grass [
            UIRadioSlider grassdist = [128 "^f1quick fade" 256 "^f0moderate fade" 512 "^f2slow fade" 1024 "^f3slower fade"] 13
         ]; uifield grassdist 5 [] 0.8 [uioutline (uifocus? 0xFF0000 0x4f4d4c); uiclamp- 1 1 1 2]
         uialign* -1 0
      ]
      uitablerow [uiline 0x2C2C2C; uiclamp- 1 1; uiline 0x2C2C2C; uiclamp- 1 1; uiline 0x2C2C2C; uiclamp- 1 1; uiline 0x2C2C2C; uiclamp- 1 1]
      uitablerow [
         uifill 0.03 0.03; uitext "Sound Volume" 0.8
         UIHSlider soundvol 0 255 [] 0.5 (*f $uitextscale 0.8) 5; uifield soundvol 3 [] 0.8 [uioutline (uifocus? 0xFF0000 0x4f4d4c); uiclamp- 1 1 1 2]
         uialign* -1 0
      ]
   ] 
] "Options" [] [if (pendingchanges) [showui changes]]

///////////////////////////////////////////////////////////////////////////////
//  Menu - Credits                                                           //
///////////////////////////////////////////////////////////////////////////////
creditlist = [
   uicolortext $arg1 0x808080 0.6 [uialign -1 0]
   uiline 0xFFFFFF; uiclamp- 1 1
   uispace 0.01 0 [
      uialign -1 0
      uivlist 0.001 [
         looplist credit $arg2 [
            uitext (strreplace (strreplace $credit "^"" "^fs^f8^"" "^"^fr") ": " "^f4: ") 0.6
         ]
         uialign* -1 0
      ]
   ]
   uispace 0 0.01
]

UIMenu "credits" [
   UIVScroller 0.8 0.8 0.02 3.0 [
      uivlist 0.001 [
         uialign 0 -1
         creditlist "Project" [
            [Lee "eihrul" Salzman]
            [Shane "Nieb" Nieb]
         ]
         creditlist "Code" [
            [Lee "eihrul" Salzman]
            [Benjamin Segovia]
            [Kevin "Hirato Kirata" Meyer]
         ]
         creditlist "Art 2D/3D" [
            [Shane "Nieb" Nieb : Playermodel, Mapmodels, Textures]
         ]
         creditlist "Sound" [
            [Shane "Nieb" Nieb]
            ["Q009"]
         ]
         creditlist "Cartography" [
            [Shane "Nieb" Nieb : complex]
         ]
         creditlist "Kudos" [
            [Kevin "Hirato Kirata" Meyer : CubeScript Documentation]
            ["RaZgRiZ" : UI Documentation]
            [Daniel "q66" Kolesa : Build-system Improvements]
         ]
         creditlist "Cube Engine 2" [
            [Wouter "Aardappel" van Oortmerssen]
            [Lee "eihrul" Salzman]
            [Mike "Gilt" Dysart]
            [Robert "baby-rabbit" Pointon]
            [Quinton "Quin" Reeves]
            [John "geartrooper" Siar]
            [and others...]
            "http://sauerbraten.org/"
         ]
      ]
   ]
] "Tesseract Contributors"

///////////////////////////////////////////////////////////////////////////////
//  Dialog - Display Changes                                                 //
///////////////////////////////////////////////////////////////////////////////
UIMenuNoTitle "changes" [
   uivlist 0.01 [
      uitext "The follow settings have changed:" 0.8
      uispace 0 0.01
      loop i (pendingchanges) [
         uitext (pendingchanges $i) 0.8
      ]
      uispace 0 0.01
      uitext "Apply changes now?" 0.8
      uihlist 0 [
         UIButton "Yes" [applychanges]
         uispace 0.02 0
         UIButton "No" [hideui changes]
      ]
   ]
]

///////////////////////////////////////////////////////////////////////////////
//  Notepad                                                                  //
///////////////////////////////////////////////////////////////////////////////
notepadfile = "untitled.txt"

UIMenu "notepad" [
   uivlist 0.01 [
      uihlist 0.01 [
         uialign -1 0
         uifield notepadfile -35 [] 1 [uioutline (uifocus? 0xFF0000 0x4f4d4c); uiclamp- 1 1 1 1]
         UIButton "load" [textfocus $notepadfile; textload $notepadfile]
         UIButton "save" [textfocus $notepadfile; textsave $notepadfile]
         UIButton "exec" [textfocus $notepadfile; textexec]
         UIButton "copy" [textfocus $notepadfile; textcopy]
         UIButton "paste" [textfocus $notepadfile; textpaste]
         UIButton "select" [textfocus $notepadfile; textselectall]
         UIButton "clear" [textfocus $notepadfile; textclear]
      ]
      uitexteditor $notepadfile -80 20 1 "" 0 [uioutline (uifocus? 0xFF0000 0x4f4d4c); uiclamp- 1 1 1 1]
      textinit $notepadfile $notepadfile
   ]
] "Notepad"

notepad = [
    if (> $numargs 0) [notepadfile = $arg1]
    showui notepad
]

