// standard menu definitions
// don't modify, add personal menus to autoexec.cfg instead

exec "config/ui_serverbrowser.cfg" // Server Browser
exec "config/ui_scoreboard.cfg"    // Scoreboard
exec "config/ui_fkey.cfg"          // F# Key Menus

togglemainmenu = [|| (hidetopui) [toggleui main]]

bind ESCAPE [togglemainmenu]

UIMenuNoTitle "fullconsole" [
   uiallowinput 0
   uiconsole 0 (divf $fullconsize 100) [uiclamp 1 1 0 0]
] [] [] [uiclamp 1 1 0 0; uialign 0 -1] [] 0xBB080808

UIMenuNoTitle "editstats" [
   uiallowinput 0
   uiabovehud
   uifill 0.4
   uivlist 0 [
      uicontext (edithud)
      uicontext (format "cube %1%2" $selchildcount (if $showmat [selchildmat ": "]))
      uihlist 0 [
         uicontextfill 12 0 [uicontext (format "wtr:%1k(%2%%)" $editstatwtr $editstatvtr); uialign- -1 0]
         uicontextfill 12 0 [uicontext (format "wvt:%1k(%2%%)" $editstatwvt $editstatvvt); uialign- -1 0]
         uicontextfill 11 0 [uicontext (format "evt:%1k" $editstatevt); uialign- -1 0]
         uicontextfill 7 0 [uicontext (format "eva:%1k" $editstateva); uialign- -1 0]
      ]
      uihlist 0 [
         uicontextfill 12 0 [uicontext (concatword "ond:" $editstatocta); uialign- -1 0]
         uicontextfill 12 0 [uicontext (concatword "va:" $editstatva); uialign- -1 0]
         uicontextfill 11 0 [uicontext (format "gl:%1(%2)" $editstatglde $editstatgeombatch); uialign- -1 0]
         uicontextfill 7 0 [uicontext (concatword "oq:" $editstatoq); uialign- -1 0]
         uicontextfill 7 0 [uicontext (concatword "pvs:" $editstatpvs); uialign -1 0]
      ]
      uialign* -1 0
   ]
] [] [] [uialign -1 1] [] 0xBB080808

edittoggled = [
   if $editing [showui editstats] [hideui editstats]
]

///////////////////////////////////////////////////////////////////////////////
//  Menu - Main                                                              //
///////////////////////////////////////////////////////////////////////////////
UIMenuNoTitle "main" [
   uivlist 0.0075 [
      UIButton "Multiplayer"  [hideui main; showui servers]
      UIButton "Player Setup" [hideui main; showui player_setup]
      uispace 0 0.01
      UIButton "Load Map"     [hideui main; showui loadmap]
      uispace 0 0.01
      UIButton "Options"      [hideui main; showui options]
      UIButton "Credits"      [hideui main; showui credits]
      uispace 0 0.01
      if (isconnected) [UIButton "Disconnect" [disconnect]] [UIButton "Quit" [quit]]
   ]
]

///////////////////////////////////////////////////////////////////////////////
//  Menu - Player Setup                                                      //
///////////////////////////////////////////////////////////////////////////////
ui_playercolor_button = [

]

UIMenu "player_setup" [
   uihlist 0 [
      uivlist 0.01 [ 
         uiplayerpreview 0 0 0 1 0.5 0.5
      ]
      uivlist 0.01 [
         uifield name 15 [] 1 [uioutline (uifocus? 0xFF0000 0x4f4d4c); uiclamp- 1 1 1 1]
         uitext "Player Color" 0.7
         uivlist 0 [
            uihlist 0 [
               uicolor 0xFF5E1F2A 0.06 0.06 [playercolor 0]
               uicolor 0xFFAB3E38 0.06 0.06 [playercolor 1]
               uicolor 0xFFB27436 0.06 0.06 [playercolor 2]
               uicolor 0xFF735E48 0.06 0.06 [playercolor 3]
            ]
            uihlist 0 [
               uicolor 0xFFABA561 0.06 0.06 [playercolor 4]
               uicolor 0xFF425E32 0.06 0.06 [playercolor 5]
               uicolor 0xFF468046 0.06 0.06 [playercolor 6]
               uicolor 0xFF3B8180 0.06 0.06 [playercolor 7]
            ]
            uihlist 0 [
               uicolor 0xFF657F8A 0.06 0.06 [playercolor 8]
               uicolor 0xFF295182 0.06 0.06 [playercolor 9]
               uicolor 0xFF915E75 0.06 0.06 [playercolor 10]
               uicolor 0xFF5B4573 0.06 0.06 [playercolor 11]
            ]
            uihlist 0 [
               uicolor 0xFF392A54 0.06 0.06 [playercolor 12]
               uicolor 0xFF555453 0.06 0.06 [playercolor 13]
               uicolor 0xFF737577 0.06 0.06 [playercolor 14]
               uicolor 0xFFBFB8AC 0.06 0.06 [playercolor 15]
            ]
         ]
         uitext "Player Color: Azul" 0.7
         uihlist 0 [
            uicolor 0xFF2A4778 0.06 0.06 [playercolorazul 0]
            uicolor 0xFF407187 0.06 0.06 [playercolorazul 1]
            uicolor 0xFF393973 0.06 0.06 [playercolorazul 2]
            uicolor 0xFF5D6EC6 0.06 0.06 [playercolorazul 3]
         ]
         uitext "Player Color: Rojo" 0.7
         uihlist 0 [
            uicolor 0xFFAC2C2A 0.06 0.06 [playercolorrojo 0]
            uicolor 0xFF7E211B 0.06 0.06 [playercolorrojo 1]
            uicolor 0xFF772235 0.06 0.06 [playercolorrojo 2]
            uicolor 0xFFA3435B 0.06 0.06 [playercolorrojo 3]
         ]
         uialign* -1 -1
      ]
      uialign* -1 -1
   ]
] "Player Setup"

///////////////////////////////////////////////////////////////////////////////
//  Menu - Options                                                           //
///////////////////////////////////////////////////////////////////////////////
UIMenu "options" [
   hidechanges 1
   uifill 0.3 0.3
   uitable 0.01 0.01 [
      uitablerow [
         uifill 0.03 0.03; uitext "water"
         uihlist 0.01 [
            UICheckBox waterreflect; uitext "reflection"
            UICheckBox caustics; uitext "caustics"
            UICheckBox vertwater; uitext "animation"
         ]
         uialign* -1 0
      ]
      uitablerow [uifill 0.03 0.03; uitext "shadows"; UIRadioSlider smfilter = [0 low 1 medium 2 high 3 ultra] 7; uialign* -1 0]
      uitablerow [
         UICheckBox gi; uitext "global illumination"
         if $gi [
            UIRadioSlider rhtaps = [12 low 20 medium 32 high] 7
         ]
         uialign* -1 0
      ]
      uitablerow [
        UICheckBox ao; uitext "SSAO"
        if $ao [
            UIRadioSlider aotaps = [5 low 8 medium 12 high] 7 [
               aobilateral (? (>= $aotaps 8) 4 3)
            ]
        ]
        uialign* -1 0
      ]
      uitablerow [
         uifill 0.03 0.03; uitext "anisotropic filtering"
         UIRadioSlider aniso = [0 off 2 2x 4 4x 8 8x 16 16x] 4
         uialign* -1 0
      ]
      uitablerow [
         uifill 0.03 0.03; uitext "multisample AA"
         UIRadioSlider msaa = [0 off 2 2x 4 4x 8 8x 16 16x] 4
         uialign* -1 0
      ]
      uitablerow [
        UICheckBox tqaa; uitext "temporal AA"
        uialign* -1 0
      ]
      uitablerow [
         UICheckBox smaa; uitext "morphological AA"
         if $smaa [
            UIRadioSlider smaaquality = [0 low 1 medium 2 high 3 ultra] 7 [
               smaacoloredge (>= $smaaquality 3)
            ]
         ]
         uialign* -1 0
      ]
      uitablerow [
         UICheckBox fxaa; uitext "FXAA"
         if $fxaa [
            UIRadioSlider fxaaquality = [0 low 1 medium 2 high 3 ultra] 7
         ]
         uialign* -1 0
      ]
      uitablerow [
         UICheckBox grass; uitext "grass"
         if $grass [
            UIRadioSlider grassdist = [128 "quick fade" 256 "moderate fade" 512 "slow fade"] 13
         ]
         uialign* -1 0
      ]
      uitablerow [UICheckBox softparticles; uitext "soft particles"; uialign* -1 0]
      uitablerow [
         UICheckBox decals; uitext "decals"
         if $decals [
            UIRadioSlider decalfade = [10000 "quick fade" 60000 "slow fade"] 13 [
               maxdecaltris (? (> $decalfade 10000) 4096 1024)
            ]
         ]
         uialign* -1 0
      ]
      uitablerow [
         UICheckBox vsync; uitext "v-sync"
         if $vsync [
            uihlist 0.01 [
               UICheckBox vsynctear; uitext "tear"
            ]
         ]
         uialign* -1 0
      ]
      uitablerow [
         uifill 0.03 0.03; uitext "gamma"
         UIHSlider gamma 30 300 [] 0.5 $uitextscale 1
         uialign* -1 0
      ]
   ] 
] "Options" [] [if (pendingchanges) [showui changes]]

///////////////////////////////////////////////////////////////////////////////
//  Menu - Credits                                                           //
///////////////////////////////////////////////////////////////////////////////
creditlist = [
   uicolortext $arg1 0x808080 0.6 [uialign -1 0]
   uiline 0xFFFFFF; uiclamp- 1 1
   uispace 0.01 0 [
      uialign -1 0
      uivlist 0.001 [
         looplist credit $arg2 [
            uitext (strreplace (strreplace $credit "^"" "^fs^f8^"" "^"^fr") ": " "^f4: ") 0.6
         ]
         uialign* -1 0
      ]
   ]
   uispace 0 0.01
]

UIMenu "credits" [
   UIVScroller 0.8 0.8 0.02 3.0 [
      uivlist 0.001 [
         uialign 0 -1
         creditlist "Project" [
            [Lee "eihrul" Salzman]
            [Shane "Nieb" Nieb]
         ]
         creditlist "Code" [
            [Lee "eihrul" Salzman]
            [Benjamin Segovia]
            [Kevin "Hirato Kirata" Meyer]
         ]
         creditlist "Art 2D/3D" [
            [Shane "Nieb" Nieb : Playermodel, Mapmodels, Textures]
         ]
         creditlist "Sound" [
            [Shane "Nieb" Nieb]
            ["Q009"]
         ]
         creditlist "Cartography" [
            [Shane "Nieb" Nieb : complex]
         ]
         creditlist "Kudos" [
            [Kevin "Hirato Kirata" Meyer : CubeScript Documentation]
            ["RaZgRiZ" : UI Documentation]
            [Daniel "q66" Kolesa : Build-system Improvements]
         ]
         creditlist "Cube Engine 2" [
            [Wouter "Aardappel" van Oortmerssen]
            [Lee "eihrul" Salzman]
            [Mike "Gilt" Dysart]
            [Robert "baby-rabbit" Pointon]
            [Quinton "Quin" Reeves]
            [John "geartrooper" Siar]
            [and others...]
            "http://sauerbraten.org/"
         ]
      ]
   ]
] "Tesseract Contributors"

///////////////////////////////////////////////////////////////////////////////
//  Dialog - Display Changes                                                 //
///////////////////////////////////////////////////////////////////////////////
UIMenuNoTitle "changes" [
   uivlist 0.01 [
      uitext "The follow settings have changed:"
      uispace 0 0.01
      loop i (pendingchanges) [
         uitext (pendingchanges $i)
      ]
      uispace 0 0.01
      uitext "Apply changes now?"
      uihlist 0 [
         UIButton "Yes" [applychanges]
         uispace 0.02 0
         UIButton "No" [hideui changes]
      ]
   ]
]
