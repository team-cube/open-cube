// standard menu definitions
// don't modify, add personal menus to autoexec.cfg instead

exec "config/ui_fkey.cfg"          // F# Key Menus
exec "config/ui_options.cfg"       // Options Menu
exec "config/ui_serverbrowser.cfg" // Server Browser
exec "config/ui_scoreboard.cfg"    // Scoreboard
exec "config/ui_edithud.cfg"       // Edit HUD

reui = [
   exec "config/stdlib.cfg"
   exec "config/ui_lib.cfg"
   exec "config/ui_style.cfg"
   exec "config/ui.cfg" 
]

togglemainmenu = [|| hidetopui [toggleui main]]

bind ESCAPE [togglemainmenu]

///////////////////////////////////////////////////////////////////////////////
//  Menu - Main                                                              //
///////////////////////////////////////////////////////////////////////////////
ui_menu "main" [
   uieschide (! $mainmenu)
   uihlist 0 [
      uivlist 0 [
         uifill 0.2
         ui_button "hold2" [uitext "Server Browser" 0.6] 0 0.04 [hideui main; showui "server_browser"]
         ui_button "hold2" [uitext "Map Browser"    0.6] 0 0.04 [hideui main; showui "map_browser"]
         uispace 0 0.01 [ui_bar 1]
         ui_button "hold2" [uitext "Player Setup"   0.6] 0 0.04 [hideui main; showui "player_setup"]
         ui_button "hold2" [uitext "Options"        0.6] 0 0.04 [hideui main; showui "options"]
         if $mainmenu [
            ui_button "hold2" [uitext "Credits"        0.6] 0 0.04 [hideui main; showui "credits"]
         ]
         uispace 0 0.01 [ui_bar 1]
         if $isconnected [
            ui_button "hold2" [uitext "Disconnect" 0.6] 0 0.04 [disconnect]
         ] [
            ui_button "hold2" [uitext "Quit" 0.6] 0 0.04 [quit]
         ]
         uiclamp* 1 1
      ]
      if (! $mainmenu) [
         uispace 0.01 0 [ui_bar 0 1]
         uivlist 0.01 [
            uitext "Change Team" 0.6
            uihlist 0 [
               uiclamp 1 1
               ui_button "hold2" [uitext (? (= $getteam 1) "^f4Azul" "^f8Azul") 0.6] 0 0.04 [team "azul"]
               ui_button "hold2" [uitext (? (= $getteam 2) "^f4Rojo" "^f3Rojo") 0.6] 0 0.04 [team "rojo"]
               uiclamp* 1 1
            ]
         ]
      ]
   ]
]

///////////////////////////////////////////////////////////////////////////////
//  Menu - Player Setup                                                      //
///////////////////////////////////////////////////////////////////////////////
_ps_pmdl = 0
_ps_team = 0
_ps_weap = 1

ui_menu "player_setup" [
   uihlist 0.01 [
      uivlist 0.01 [
         uiplayerpreview $_ps_pmdl (case $_ps_team 0 $playercolor 1 $playercolorazul 2 $playercolorrojo) $_ps_team $_ps_weap 0.4 0.5 [
            _style_generic_default
            uiclamp* 1 1 1 1
         ]
         uihlist 0 [
            uiclamp 1 1
            ui_button "" [
               ui_radio [= $_ps_weap 0] 0.016
               uitext "Railgun" 0.6
            ] 0 0.032 [_ps_weap = 0] -1
            ui_button "" [
               ui_radio [= $_ps_weap 1] 0.016
               uitext "Pulse Rifle" 0.6
            ] 0 0.032 [_ps_weap = 1] -1
            uiclamp* 1 1
         ]
      ]
      uivlist 0.01 [
         uialign 0 -1
         uifield name 15 [] 0.7 _style_generic_focus
         looplist3 v t l [
            "playercolor"     0 16
            "playercolorazul" 1  4
            "playercolorrojo" 2  4
         ] [
            uivlist 0.006 [
               uialign 0 1
               if $t [uitext (? (= $t 1) "^f8Azul" "^f3Rojo") 0.7]
               uigrid 4 0.006 0.006 [
                  uihover [_ps_team = $t]
                  loop i $l [
                     uicolor (getplayercolor $i $t) 0.045 0.045 [
                        uioutline (? (= $$v $i) $_c_fill $_c_line)
                        uiclamp- 1 1 1 1
                        //if (= $$v $i) [uioutline $_c_fill ; uiclamp- 1 1 1 1]
                        uipress [$v $i]
                     ]
                  ]
               ]
            ]
         ]
      ]
   ]
] [] [] [] [uitext "Player Setup" 0.85]

///////////////////////////////////////////////////////////////////////////////
//  Menu - Credits                                                           //
///////////////////////////////////////////////////////////////////////////////
creditlist = [
   ui_title [
      uispace 0.015 0 [
         uialign -1 0
         uihlist 0.01 [
            uiimage "media/interface/cube.png" 0.03 0.03
            uitext [@@@@arg1] 0.7
         ]
      ]
   ] 0 0.032
   uispace 0.03 0 [
      uifill 0 0 [
         uiclamp 1 1
         uimodhgradient 0xC0C0C0 0xFFFFFF
         uiclamp- 1 1 1 1
         uispace 0.02 0.01 [
            uialign -1 0
            uivlist 0 [
               looplist i $arg2 [
                  uitext (strreplace (strreplace $i "^"" "^fs^f8^"" "^"^fr") ": " "^f4: ") 0.6
               ]
               uialign* -1 0
            ]
         ]
      ]
   ]
   uispace 0 0.01
]

ui_menu "credits" [
   uihlist 0.01 [
      uiscroll 0.8 0.8 [
         uifill 0.77
         uivlist 0.0008 [
            uiclamp 1 1
            uialign 0 -1
            creditlist "Project" [
               [Lee "eihrul" Salzman : Code Gatekeeper]
               [Shane "Nieb" Nieb : Media Gatekeeper]
            ]
            creditlist "Code" [
               [Lee "eihrul" Salzman]
               [Benjamin Segovia]
               [Kevin "Hirato Kirata" Meyer]
            ]
            creditlist "Art 2D/3D" [
               [Shane "Nieb" Nieb : Playermodel, Mapmodels, Textures]
            ]
            creditlist "Sound" [
               [Shane "Nieb" Nieb]
               ["Q009"]
            ]
            creditlist "Cartography" [
               [Shane "Nieb" Nieb : complex]
            ]
            creditlist "Kudos" [
               [Kevin "Hirato Kirata" Meyer : CubeScript Documentation]
               ["RaZgRiZ" : UI Documentation]
               [Daniel "q66" Kolesa : Build-system Improvements]
            ]
            creditlist "Cube Engine 2" [
               [Wouter "Aardappel" van Oortmerssen]
               [Lee "eihrul" Salzman]
               [Mike "Gilt" Dysart]
               [Robert "baby-rabbit" Pointon]
               [Quinton "Quin" Reeves]
               [John "geartrooper" Siar]
               [and others...]
               "http://sauerbraten.org/"
            ]
            uiclamp* 1 1
         ]
      ]
      ui_vscroll 0.02 0.8 0.5 0.5
   ]
] [] [] [] [uitext "Tesseract Contributors" 0.85]

///////////////////////////////////////////////////////////////////////////////
//  Dialog - Display Changes                                                 //
///////////////////////////////////////////////////////////////////////////////
ui_menu "changes" [
   uivlist 0.02 [
      uiclamp 1 1
      ui_title [uitext "Apply changes for:" 0.6] 0 0.03
      uiclamp- 1 1
      uivlist 0.01 [
         loop i $pendingchanges [
            uitext (concatword "^f2" (pendingchanges $i)) 0.6
         ]
      ]
      uihlist 0 [
         ui_button "hold2" [uitext "Yes" 0.6] 0.18 0.032 [applychanges]
         ui_button "hold2" [uitext "No"  0.6] 0.18 0.032 [hideui "changes"]
      ]
   ]
] [if (= $pendingchanges 0) [hideui "changes"]]


///////////////////////////////////////////////////////////////////////////////
//  Notepad                                                                  //
///////////////////////////////////////////////////////////////////////////////
notepadfile = "untitled.txt"
_cmc_from   = ""
_cmc_to     = ""

ui_menu "notepad" [
   uivlist 0.01 [
      uihlist 0.01 [
         uialign -1 0
         uifield notepadfile -35 [] 0.6 [
            uioutline (uifocus? 0xFF0000 $_c_line)
            uiclamp- 1 1 1 1
         ]
         ui_button "hold2" [uitext "Load"       0.6] 0.04 0.032 [textfocus $notepadfile; textload $notepadfile]
         ui_button "hold2" [uitext "Save"       0.6] 0.04 0.032 [textfocus $notepadfile; textsave $notepadfile]
         ui_button "hold2" [uitext "Exec"       0.6] 0.04 0.032 [textfocus $notepadfile; textexec]
         uispace 0.01 0 [ui_bar 0 1]
         ui_button "hold2" [uitext "Copy"       0.6] 0.04 0.032 [textfocus $notepadfile; textcopy]
         ui_button "hold2" [uitext "Paste"      0.6] 0.04 0.032 [textfocus $notepadfile; textpaste]
         ui_button "hold2" [uitext "Select All" 0.6] 0.04 0.032 [textfocus $notepadfile; textselectall]
         uispace 0.01 0 [ui_bar 0 1]
         ui_button "hold2" [uitext "Clear All"  0.6] 0.04 0.032 [textfocus $notepadfile; textclear]
      ]
      uitexteditor $notepadfile -80 20 1 "" 0 [
         uioutline (uifocus? 0xFF0000 $_c_line)
         uiclamp- 1 1 1 1
      ]
      textinit $notepadfile $notepadfile
   ]
] [] [] [] [uitext "Notepad" 0.85]

notepad = [
   if $numargs [notepadfile = $arg1]
   showui "notepad"
]

mapnotepad = [
   notepadfile = (concatword "media/map/" (? $numargs $arg1 $mapname) ".cfg")
   showui "notepad"
]

copymapcfg = [
   if $numargs [
      _cmc_from = $arg1
      _cmc_to   = (? (= $numargs 2) $arg2 $mapname)
      if (findfile (concatword "media/map/" $_cmc_from ".cfg")) [
         if (findfile (concatword "media/map/" $_cmc_to ".cfg")) [showui "cmc_confirm"] [
            textfocus "#copymapcfg"
            textload (concatword "media/map/" $_cmc_from ".cfg")
            textsave (concatword "media/map/" $_cmc_to   ".cfg")
         ]
      ] [
         echo (concatword "^f7Map config "^f8^" $_cmc_from "^f7^" does not exist!")
         _cmc_from = "" ; _cmc_to = ""
      ]
   ]
]

ui_menu "cmc_confirm" [
   uivlist 0.02 [
      uiclamp 1 1
      ui_title [uitext "Copy map config:" 0.6] 0 0.03
      uiclamp- 1 1
      uivlist 0.01 [
         uitext (concatword "^f2" $_cmc_from) 0.6
         ui_iso_triangle 0 0.01 0.01 180
         uitext (concatword "^f2" $_cmc_to) 0.6
      ]
      uihlist 0 [
         ui_button "hold2" [uitext "Proceed" 0.6] 0.18 0.032 [
            textfocus "#copymapcfg"
            textload (concatword "media/map/" $_cmc_from ".cfg")
            textsave (concatword "media/map/" $_cmc_to   ".cfg")
            hideui "cmc_confirm"
         ]
         ui_button "hold2" [uitext "Cancel"  0.6] 0.18 0.032 [hideui "cmc_confirm"]
      ]
   ]
] [
   if (|| [=s $_cmc_from ""] [=s $_cmc_to ""]) [hideui "cmc_confirm"]
] [_cmc_from = "" ; _cmc_to = ""]

