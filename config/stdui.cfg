//Creates a basic menu with no title nor close button
//args: name, contents, onshow, onhide, children, maybetitle, allowinput
UIMenuNoTitle = [
    do [
        newui [@@arg1] [
            uivlist 0 [
                @@@arg6
                uicolor 0x99080808 0.04 0.04 [
                    uiclamp 1 1 0 0
                    uioutline 0x343332
                        uiclamp- 1 1 1 1
                    uispace 0.01 0.01 [
                        @@@@@arg2
                    ]
                ]
            ]
            @@arg5
        ] [@@arg3] [@@arg4] @arg7
    ]
]

//Creates a basic menu with a title, but without a close button
//args: name, contents, title, onshow, onhide, children, extratitle
UIMenuNoClose = [
    UIMenuNoTitle $arg1 $arg2 $arg4 $arg5 $arg6 [
        uivgradient 0xE61A1A1A 0xE60C0C0C 0.04 0.04 [
            uiclamp 1 1 0 0
            uioutline 0xC0C0C0
                uiclamp- 1 1 1 1
            uitext [@@@arg3] 0.9
            @@arg7
            uispace 0.01 0 [
                uialign 1 0
                UICloseButton [@@@@arg1]
            ]
        ]
    ]
]

//Creates a basic menu with a title and a close button
//args: name, contents, title, onshow, onhide, children, extratitle
UIMenu = [
    UIMenuNoClose $arg1 $arg2 $arg3 $arg4 $arg5 $arg6 [
        uispace 0.01 0 [
            uialign 1 0
            UICloseButton [@@@arg1]
        ]
        @arg7
    ]
]

//Creates a basic menu with a title and a close button, auto-hides outside of edit mode
//args: name, contents, title, onshow, onhide, children, extratitle
UIEditMenu = [
    UIMenu $arg1 [if $editing [@@arg2] [hideui [@@@arg1]]] $arg3 $arg4 $arg5 $arg6 $arg7
]
 
UICloseButton = [
    uifill 0.025 0.025 [
        uialign 1 0
        uihover+ [uicolor 0x585858] [uicolor 0x1A1A1A]
            uiclamp- 1 1 1 1
        uitext "X" 0.7
        uihold [uimodcolor 0xB3B3B3; uiclamp- 1 1 1 1]
        uioutline 0xC0C0C0
            uiclamp- 1 1 1 1
        uirelease [hideui $arg1]
    ]
]

//creates a button with a centred text element
//args: text, action, children
UIButton = [
    uifill 0.16 0.04 [
        uiclamp 1 1 0 0
        uihover+ [uicolor 0x585858] [uicolor 0x1A1A1A]               
            uiclamp- 1 1 1 1
        uispace 0.01 0 [
            uitext $arg1 0.9
        ]
        uihold [uimodcolor 0xB3B3B3; uiclamp- 1 1 1 1]
        uioutline 0xC0C0C0
            uiclamp- 1 1 1 1
        uirelease [arg2]
        arg3
    ]
]

//creates a conditional button with a centred text element
//args: text, cond, action, children
UICondButton = [
    uifill 0.16 0.04 [
        uiclamp 1 1 0 0
        if (arg2) [
            uihover+ [uicolor 0x585858] [uicolor 0x1A1A1A]
                uiclamp- 1 1 1 1
            uispace 0.01 0 [
                uitext $arg1 0.9
            ]
            uihold [uimodcolor 0xB3B3B3; uiclamp- 1 1 1 1]
            uirelease [arg3]
        ] [
            uicolor 0
                uiclamp- 1 1 1 1
            uispace 0.01 0 [
                uitext $arg1 0.9
            ]
            uimodcolor 0x4D4D4D
                uiclamp- 1 1 1 1
        ]
        uioutline 0xC0C0C0
            uiclamp- 1 1 1 1
        arg4
    ]
]

//args: step, size, text
UIScrollArrow = [
    uiscrollarrow $arg1 [
        uihover [uicolor 0x585858] [uicolor 0x1A1A1A]               
            uiclamp- 1 1 1 1
        uitext $arg3 0.5
        uihold [uimodcolor 0xB3B3B3; uiclamp- 1 1 1 1]
        uioutline 0xC0C0C0 $arg2 $arg2
            uiclamp- 1 1 1 1
    ]
]

//Creates a uiscroll area with a uivscrollbar at the right
//args: X-Size, Y-Size, ArrowSize, ScrollSpeed, Contents
UIVScroller = [
    uihlist 0.01 [
        uiscroll (-f $arg1 $arg3) $arg2 [
            uifill (-f $arg1 $arg3) $arg2
            arg5
        ]
        uivlist 0 [
            UIScrollArrow (-f 0 $arg4) $arg3 "/\"
            uivscrollbar [
                uicolor 0x080808 $arg3 (-f $arg2 (*f $arg3 2)) [
                    uiclamp 1 1 1 1
                    uioutline 0xC0C0C0
                        uiclamp- 1 1 1 1
                ]
                uiscrollbutton [
                    uihover [uicolor 0x585858] [uicolor 0x1A1A1A]     
                        uiclamp- 1 1 1 1
                    uihold [uimodcolor 0xB3B3B3; uiclamp- 1 1 1 1]
                    uioutline 0xC0C0C0 $arg3 $arg3
                        uiclamp- 1 1 1 1
                ]
            ]
            UIScrollArrow $arg4 $arg3 "\/"
        ]
    ]
]

//args: StepSize, ArrowSize, Text
UISliderArrow = [
    uisliderarrow $arg1 [
        uihover [uicolor 0x585858] [uicolor 0x1A1A1A]     
            uiclamp- 1 1 1 1
        uitext $arg3 0.5
        uihold [uimodcolor 0xB3B3B3; uiclamp- 1 1 1 1]
        uioutline 0xC0C0C0 $arg2 $arg2
            uiclamp- 1 1 1 1
    ]
]

//Creates a horizontal slider for adjusting a variable
//args: Var, Min, Max, OnChange, Length, ArrowSize, StepSize, Children
UIHSlider = [
    uihlist 0 [
        UISliderArrow (-f 0 $arg7) $arg6 "-"
        uihslider $arg1 $arg2 $arg3 $arg7 $arg4 [
            uicolor 0x080808 (-f $arg5 (*f $arg6 2)) $arg6 [
                uiclamp 1 1 1 1
                uioutline 0xC0C0C0
                    uiclamp- 1 1 1 1
            ]
            uisliderbutton [
                uihover [uicolor 0x585858] [uicolor 0x1A1A1A]     
                    uiclamp- 1 1 1 1
                uispace 0.001 0 [
                    uitext $$arg1 (minf 1 (*f $arg6 40))
                ]
                uihold [uimodcolor 0xB3B3B3; uiclamp- 1 1 1 1]
                uioutline 0xC0C0C0 $arg6 $arg6
                    uiclamp- 1 1 1 1
            ]
            arg8
        ]
        UISliderArrow $arg7 $arg6 "+"
    ]
]
    
