///////////////////////////////////////////////////////////////////////////////
//  F1 Information                                                           //
///////////////////////////////////////////////////////////////////////////////
menu_info = [if (isconnected) [toggleui "game_info"] []]

gi_edit_info = [
   uivlist 0 [
      uitext "^f4Gamemode: Edit" 0.7
      uiline 0xFFFFFF; uiclamp- 1 1
      uispace 0.01 0.01 [
         uiclamp 1 1 1 1
         uivlist 0 [
            uiclamp 1 1 1 1
            uitext "Edit maps with multiple players simultaneously." 0.6
            uitext " " 0.6
            uialign* -1 -1
         ]
      ]
      uialign* -1 -1
   ]
]
gi_dm_info = [
   uivlist 0 [
      uitext "^f4Gamemode: Deathmatch" 0.7
      uiline 0xFFFFFF; uiclamp- 1 1
      uispace 0.01 0.01 [
         uiclamp 1 1 1 1
         uivlist 0 [
            uiclamp 1 1 1 1
            uitext "Gamemode description here." 0.6
            uitext " " 0.6
            uialign* -1 -1
         ]
      ]
      uialign* -1 -1
   ]
]
gi_tdm_info = [
   uivlist 0 [
      uitext "^f4Gamemode: Team-Deathmatch" 0.7
      uiline 0xFFFFFF; uiclamp- 1 1
      uispace 0.01 0.01 [
         uiclamp 1 1 1 1
         uivlist 0 [
            uiclamp 1 1 1 1
            uitext "Gamemode description here." 0.6
            uitext " " 0.6
            uialign* -1 -1
         ]
      ]
      uialign* -1 -1
   ]
]
gi_ctf_info = [
   uivlist 0 [
      uitext "^f4Gamemode: Capture The Flag" 0.7
      uiline 0xFFFFFF; uiclamp- 1 1
      uispace 0.01 0.01 [
         uiclamp 1 1 1 1
         uivlist 0 [
            uiclamp 1 1 1 1
            uitext "Gamemode description here." 0.6
            uitext " " 0.6
            uitext "Scoring:" 0.6
            uihlist 0.01 [
               uivlist 0 [
                  uitext "^f010" 0.6
                  uitext "^f025" 0.6
                  uitext "^f3-20" 0.6
                  uitext "^f3-50" 0.6
                  uitext "^f025" 0.6
                  uitext "^f050" 0.6
                  uitext "^f025" 0.6
                  uitext "^f0100" 0.6
                  uialign* 1 -1
               ]
               uivlist 0 [
                  uitext "Kill enemy" 0.6
                  uitext "Kill enemy with flag" 0.6
                  uitext "Kill teammate" 0.6
                  uitext "Kill teammate with flag" 0.6
                  uitext "Pickup dropped team flag" 0.6
                  uitext "Pickup flag from enemy base" 0.6
                  uitext "Pickup dropped enemy flag" 0.6
                  uitext "Score flag" 0.6
                  uialign* -1 -1
               ]
               uialign* -1 -1
            ]
            uialign* -1 -1
         ]
      ]
      uialign* -1 -1
   ]
]
gi_pulse_info = [
   uivlist 0 [
      uitext "^f4Weapon: eP32 Pulse Rifle" 0.7
      uiline 0xFFFFFF; uiclamp- 1 1
      uispace 0.01 0.01 [
         uiclamp 1 1 1 1
         uivlist 0 [
            uiclamp 1 1 1 1
            uitext "The ^f8eP32 Pulse Rifle ^f7designed and produced by EihrulCorp..." 0.6
            uitext " " 0.6
            // EP32 Pulserifle
            // (render out picture of gun)
            // apporoximate firing rate of 1 round every 0.7seconds
            // fast moving projectile
            // developed by EihrulCorp
            uialign* -1 -1
         ]
      ]
      uialign* -1 -1
   ]
]
gi_rail_info = [
   uivlist 0 [
      uitext "^f4Weapon: NT-R7 Railgun" 0.7
      uiline 0xFFFFFF; uiclamp- 1 1
      uispace 0.01 0.01 [
         uiclamp 1 1 1 1
         uivlist 0 [
            uiclamp 1 1 1 1
            uitext "The ^f8NT-R7 Railgun ^f7developed at NiebTek Labs..." 0.6
            uitext " " 0.6
            // NT-R7 Railgun
            // (render out picture of gun)
            // apporoximate firing rate of 1 round every 1.4seconds
            // instant-raytrace bullet/super-sonic ultra-high-speed bullet?
            // developed by NiebTek
            uialign* -1 -1
         ]
      ]
      uialign* -1 -1
   ]
]

UImenu "game_info" [
   uihlist 0.01 [
      uiscroll 0.9 0.7 [
         uifill 0.87 0.7
         uivlist 0.01 [
            uiclamp 1 1 0 0
            case (getnextmode) [
               0] [gi_edit_info] [
               1] [gi_dm_info] [
               2] [gi_dm_info] [
               3] [gi_tdm_info] [
               4] [gi_tdm_info] [
               5] [gi_ctf_info] [
               6] [gi_ctf_info]
            case (getnextmode) [
               0] [gi_pulse_info; gi_rail_info] [
               1] [gi_rail_info] [
               2] [gi_pulse_info] [
               3] [gi_rail_info] [
               4] [gi_pulse_info] [
               5] [gi_rail_info] [
               6] [gi_pulse_info]
            uiclamp* 1 1 0 0
         ]
         uialign* -1 -1
      ]
      UIvscroll 0.02 0.52 3
   ]
] [] [] [] [uitext "Information" 0.8]

///////////////////////////////////////////////////////////////////////////////
//  F1 Create/Modify Entity                                                  //
///////////////////////////////////////////////////////////////////////////////
menu_entity = [if (enthavesel) [toggleui "editentity"] [toggleui "addentity"]]

UImenu "editentity" [
   uifill 0.5 0.5
] [] [] [] [uitext "Edit Entity" 0.8]

UImenu "addentity" [
   uifill 0.5 0.5
] [] [] [] [uitext "Add Entity" 0.8]

///////////////////////////////////////////////////////////////////////////////
//  F2 Texture List                                                          //
///////////////////////////////////////////////////////////////////////////////
_sel_texture = -1

UIpanel "texture" [
   uivlist 0.006 [
      local t ; t = (>= $_sel_texture 0)
      uihlist 0.01 [
         uimodcolor $c_boxmod 0.192 0.192 [
            uivslotview $(? $t _sel_texture getseltex)
            uioutline $c_line
            uiclamp* 1 1 1 1
            uifont "default" [
               uialign 0 1
               uicolortext (? $t "PREVIEW" "SELECTION") 0x80FFFFFF 0.5
            ]
         ]
         uivlist 0.01 [
            UIhscroll $_res_maxuiwidth 0.02 3.28
            uiscroll $_res_maxuiwidth 0.162 [
               uialign -1 -1
               uimodcolor 0xFFFFFF
               uiclamp- 1 1 1 1
               uihover [] [_sel_texture = -1]
               uigrid (div (+ (mod $numvslots 2) $numvslots) 2) 0.002 0.002 [
                  looptexmru i [
                     uivslotview $i 0.08 0.08 [
                        if (!=s (gettexname $i) "") [
                           uirelease [
                              hideui "texture"
                              settex $i
                           ]
                           style_img_hold1
                           uihover [_sel_texture = $i]
                        ]
                     ]
                  ]
               ]
            ]
         ]
      ]
      if $t [
         uihlist 0.03 [
            uialign -1 0
            uifill 0.192 0 [
               uispace 0.02 0 [
                  uialign -1 0
                  uihlist 0.01 [
                     UItriangle 0 0.01 0.01 270
                     uitext (concatword "^f2" $_sel_texture) 0.7
                  ]
               ]
            ]
            uiclip $_res_maxuiwidth 0 [
               uihlist 0.01 [
                  local l n
                  n = (strreplace (gettexname $_sel_texture) "\" "/")
                  l = (pathlen $n)
                  UItriangle 0 0.01 0.01 270
                  uitext (concatword (if (> $l 1) [
                     strreplace (concatword (
                        getpath $n 0 (- $l 1)
                     ) "/") "/" "^f4/^fS"
                  ]) "^f8" (getfolder $n (- $l 1))) 0.6
               ]
            ]
         ]
      ]
   ]
] [
   _sel_texture = -1
   _res_maxuiwidth  = (-f (divf $screenw $screenh) 0.012 0.192)
   hideui "mapmodel"
] [] [
   uialign 0 -1
   uiclamp 1 1
]

///////////////////////////////////////////////////////////////////////////////
//  F3 Mapmodel                                                              //
///////////////////////////////////////////////////////////////////////////////
UIpanel "mapmodel" [
   uieschide 0
   uihlist 0.01 [
      if 0 [
         UIvscroll 0.02 0.81 (*f (min (+ 1 (div $nummapmodels 100)) 10) 5.4)
      ] [
         UIvscroll 0.02 0.81 (*f (min (+ 1 (div $nummapmodels 50)) 5) 2.7)
      ]
      uiscroll 0 0.81 [
         uivlist 0 [
            uigrid 2 0 0 [// (+ $_res_wide 1)
               loop i $nummapmodels [
                  uigroup [
                     uimodelpreview (mapmodelname $i 1) "mapmodel" 0.12 0.135 [
                        uihover [
                           uihold [uioutline 0xFFFFFF] [
                              uieschold [uioutline 0xFFFFFF] [
                                 uioutline 0x80FFFFFF
                              ]
                           ]
                           uiclamp* 1 1 1 1
                           _sel_mapmodel = $i
                           uirelease [
                              newent mapmodel $i
                              _io_mapmodel = 0
                           ]
                           uiescrelease [
                              if (enthavesel) [
                                 entset "mapmodel" $i
                                 _io_mapmodel = 0
                              ]
                           ]
                        ] [
                           if (= $_sel_mapmodel $i) [
                              uioutline 0x40FFFFFF
                              uiclamp- 1 1 1 1
                           ]
                        ]
                     ]
                     uispace 0.01 0.006 [
                        uialign -1 -1
                        uicolortext $i 0x80FFFFFF 0.6
                     ]
                  ]
               ]
            ]
         ]
      ]
   ]
] [
   _sel_mapmodel = -1
   hideui "texture"
] [] [uialign -1 -1]

///////////////////////////////////////////////////////////////////////////////
//  F4 Material                                                              //
///////////////////////////////////////////////////////////////////////////////
UImenu "material" [
   uifill 0.5 0.5
] [] [] [] [uitext "Edit Material" 0.8]

///////////////////////////////////////////////////////////////////////////////
//  F5 Save Map                                                              //
///////////////////////////////////////////////////////////////////////////////
menu_savemap = [if (! $mapname) [toggleui "savemap"] [savemap]]

UImenu "savemap" [
   uifill 0.5 0.5
] [] [] [] [uitext "Save Map" 0.8]

///////////////////////////////////////////////////////////////////////////////
//  F6 Load Map                                                              //
///////////////////////////////////////////////////////////////////////////////
UImenu "map_browser" [
   uihlist 0.01 [
      uivlist 0 [
         uialign 0 -1
         UItitle [uitext $_sel_loadmap 0.7] 0.3 0.03
         uiimage "media/interface/cube.png" 0.3 0.3 [
            uiimage (concatword "media/map/" $_sel_loadmap ".png") 0.3 0.3 [
               uioutline $c_line
               uiclamp- 1 1 1 1
            ]
         ]
         uifill 0.0 0.19
         UItitle [uitext "Gamemode" 0.7] 0.3 0.03
         uigrid 3 0 0 [
            nextmode = (getnextmode)
            looplist2 n i [
               "Edit"  0 "rDM"  1 "pDM"  2
               ""     -1 "rTDM" 3 "pTDM" 4
               ""     -1 "rCTF" 5 "pCTF" 6
            ] [
               if (< $i 0) uifill [
                  UIbutton "" [
                     UIradio [= $nextmode $i] 0.016
                     uitext $n 0.6
                  ] 0.1 0.03 [mode $i] -1
               ]
            ]
         ]
      ]
      uiscroll 0.4 0.64 [
         uifill 0.37 0.64
         uivlist 0 [
            uialign 0 -1
            uiclamp 1 1
            loopfiles i "media/map" "ogz" [
               UIbutton (? (!=s $_sel_loadmap $i) "hold2") [
                  uitext $i 0.6
               ] 0 0.04 [
                  hideui "loadmap"
                  map $i
               ] -1 [uihover [_sel_loadmap = $i]]
            ]
            uiclamp* 1 1
         ]
      ]
      UIvscroll 0.02 0.64 1.6
   ]
] [_sel_loadmap = ""] [] [] [uitext "Map Browser" 0.8]

///////////////////////////////////////////////////////////////////////////////
//  F7 Environment Settings                                                  //
///////////////////////////////////////////////////////////////////////////////
envs_menu_desc = ["Skybox" "Cloudbox" "Cloudlayer"]
envs_menu_var  = [skyboxcolour cloudboxcolour cloudcolour]
envs_menu_val  = [16777215 16777215 16777215]

// ui_skybox "SKYBOX/PATH/HERE" Y
ui_skybox = [
   uigroup [
      arg3 = (=s $skybox $arg1)
      arg4 = (uihover?)
      uihlist 0 [
         looplist i "ft lf bk rt" [
            uiimage (format "media/sky/%1_%2.png" $arg1 $i) $arg2 $arg2
         ]
      ]
      uispace 0 (*f $arg2 0.05) [
         uialign 0 1
         uifont "default_outline" [
            uicolortext (? (=s $arg1 "") "[no skybox]" $arg1) (
               ? (|| $arg3 $arg4) 0xFFFFFF 0xA0FFFFFF
            ) 0.6
         ]
      ]
      if $arg3 [uioutline $c_fill] [
         uirelease [skybox $arg1]
         uioutline $c_line
      ]
      uiclamp- 1 1 1 1
   ]
]

_colors_r = 0.0
_colors_g = 0.0
_colors_b = 0.0
_colors_bw = 0.0
_colors_int = 0
_colors_hex = 0x000000
_colors_grayscale = 0

envs_sel = 0
envs_var_sel = ""
envs_val_sel = ""

envs_sky_skybox = [
   ""
   "0ptikz/partly-cloudy"
   "0ptikz/sunrise"
   "0ptikz/sunset"
   "cgtextures/clear01"
   "cgtextures/clear-sunset01"
   "cgtextures/dusk01"
   "cgtextures/overcast01"
   "cgtextures/sunset01"
   "lump"
]

UImenu "env_settings" [
   uihlist 0.01 [
      uivlist 0 [
         loop i (listlen $envs_menu_desc) [
            UIbutton (? (= $envs_sel $i) "hold1" "hold2") [
               uitext @(at $envs_menu_desc $i) 0.6
            ] 0.16 0.032 [
               envs_sel = @i
               envs_var_sel = (at $envs_menu_var @i)
               envs_val_sel = (at $envs_menu_val @i)
               _colors_int = $envs_val_sel
               inttorgb $_colors_int _colors_r _colors_g _colors_b
            ]
         ]
      ]
      UIbar 0 1
      case $envs_sel 0 [
         uihlist 0.01 [
            // pick skybox
            uihlist 0.01 [
               uialign 0 -1
               uiscroll 0.4 0.406 [
                  uifill 0.37 0.406
                  uivlist 0.002 [
                     looplist i $envs_sky_skybox [
                        ui_skybox $i 0.1
                     ]
                  ]
               ]
               UIvscroll 0.02 0.406 2.04
            ]
            // color area
            uivlist 0.01 [
               uifont "default_outline" [
                  uivgradient $$envs_var_sel $_colors_int 0.096 0.048 [
                     if (!= $$envs_var_sel $_colors_int) [
                        uicolortext (? (|| uihover? uihold?) "OK" "Apply?") (
                           ? (uihold?) 0xFFFFFF 0x80FFFFFF
                        ) 0.6
                        uirelease [$envs_var_sel $_colors_int]
                     ]
                     uioutline $c_line ; uiclamp- 1 1 1 1
                  ]
               ]
               uihlist 0 [
                  loop i 3 [
                     n = (? $_colors_grayscale "bw" (at "r g b" $i))
                     UIvcolorslider [_colors_@n] 255 0 0x0 (
                        if $_colors_grayscale 0xFFFFFF [>> 0xFF0000 (* $i 8)]
                     ) 0.032 0.4 [
                        if $_colors_grayscale [looplist i "r g b" [[_colors_@i] = $_colors_@n]]
                        _colors_int = (rgbtoint $_colors_r $_colors_g $_colors_b)
                        //_colors_hex = (rgbtohex $_colors_r $_colors_g $_colors_b)
                     ]
                  ]
               ]
               UIbutton "" [
                  UIcheckbox $_colors_grayscale 0.016
                  uitext "Gray" 0.6
               ] 0 0.032 [
                  if (! $_colors_grayscale) [
                     _colors_bw = (div (+ $_colors_r $_colors_g $_colors_b) 3)
                     looplist i "r g b" [[_colors_@i] = $_colors_bw]
                     _colors_int = (rgbtoint $_colors_r $_colors_g $_colors_b)
                     //_colors_hex = (rgbtohex $_colors_r $_colors_g $_colors_b)
                  ] ; _colors_grayscale = (! $_colors_grayscale)
               ] -1 [uiclamp 1 1]
            ]
         ]
      ] 1 [
         
         
         
      ]
   ]
] [
   envs_var_sel = (at $envs_menu_var $envs_sel)
   envs_val_sel = (at $envs_menu_val $envs_sel)
   _colors_int = $envs_val_sel
   inttorgb $_colors_int _colors_r _colors_g _colors_b
] [] [] [uitext "Environment Settings" 0.8]

///////////////////////////////////////////////////////////////////////////////
//  F8 World Geometry Prefab                                                 //
///////////////////////////////////////////////////////////////////////////////
UImenu "geo_prefab" [
   uifill 0.5 0.5
] [] [] [] [uitext "World Geometry Prefab" 0.8]

///////////////////////////////////////////////////////////////////////////////
//  F9 Miscellaneous Editing                                                 //
///////////////////////////////////////////////////////////////////////////////
UImenu "misc_editing" [
   uifill 0.5 0.5
] [] [] [] [uitext "Miscellaneous Editing" 0.8]

///////////////////////////////////////////////////////////////////////////////
//  F10 Default Editbind Quick Reference                                     //
///////////////////////////////////////////////////////////////////////////////
UImenu "editbind_reference" [
   uiimage "doc/Editbind_Reference.png" 1.375 0.825 []
] [] [] [] [uitext "Default Editbind Quick Reference" 0.8]

UImenu "gamebind_reference" [
   uiimage "doc/Gamebind_Reference.png" 1.1 0.825 []
] [] [] [] [uitext "Default Gamebind Quick Reference" 0.8]

///////////////////////////////////////////////////////////////////////////////
//  F11 Fullscreen Console                                                   //
///////////////////////////////////////////////////////////////////////////////
UIpanel "fullconsole" [
   uiallowinput 0
   uiconsole 0 (divf $fullconsize 100) [uiclamp 1 1]
] [] [] [
   uiclamp 1 1
   uialign 0 -1
]
