// standard menu definitions
// don't modify, add personal menus to autoexec.cfg instead

exec "config/menu_fkey.cfg" // F# Key Menus

togglemainmenu = [|| (hidetopui) [toggleui main]]

bind ESCAPE [togglemainmenu]

UIMenuNoTitle "fullconsole" [
   uiallowinput 0
   uiconsole 0 (divf $fullconsize 100) [uiclamp 1 1 0 0]
] [] [] [uiclamp 1 1 0 0; uialign 0 -1] [] 0xBB080808

UIMenuNoTitle "editstats" [
   uiallowinput 0
   uiabovehud
   uifill 0.4
   uivlist 0 [
      uicontext (edithud)
      uicontext (format "cube %1%2" $selchildcount (if $showmat [selchildmat ": "]))
      uihlist 0 [
         uicontextfill 12 0 [uicontext (format "wtr:%1k(%2%%)" $editstatwtr $editstatvtr); uialign- -1 0]
         uicontextfill 12 0 [uicontext (format "wvt:%1k(%2%%)" $editstatwvt $editstatvvt); uialign- -1 0]
         uicontextfill 11 0 [uicontext (format "evt:%1k" $editstatevt); uialign- -1 0]
         uicontextfill 7 0 [uicontext (format "eva:%1k" $editstateva); uialign- -1 0]
      ]
      uihlist 0 [
         uicontextfill 12 0 [uicontext (concatword "ond:" $editstatocta); uialign- -1 0]
         uicontextfill 12 0 [uicontext (concatword "va:" $editstatva); uialign- -1 0]
         uicontextfill 11 0 [uicontext (format "gl:%1(%2)" $editstatglde $editstatgeombatch); uialign- -1 0]
         uicontextfill 7 0 [uicontext (concatword "oq:" $editstatoq); uialign- -1 0]
         uicontextfill 7 0 [uicontext (concatword "pvs:" $editstatpvs); uialign -1 0]
      ]
      uialign* -1 0
   ]
] [] [] [uialign -1 1] [] 0xBB080808

edittoggled = [
   if $editing [showui editstats] [hideui editstats]
]

///////////////////////////////////////////////////////////////////////////////
//  Menu - Main                                                              //
///////////////////////////////////////////////////////////////////////////////
UIMenuNoTitle "main" [
   uivlist 0.0075 [
      UIButton "Multiplayer"  [hideui main; showui servers]
      UIButton "Player Setup" [hideui main; showui player_setup]
      uispace 0 0.01
      UIButton "Load Map"     [hideui main; showui loadmap]
      uispace 0 0.01
      UIButton "Options"      [hideui main; showui options]
      UIButton "Credits"      [hideui main; showui credits]
      uispace 0 0.01
      if (isconnected) [UIButton "Disconnect" [disconnect]] [UIButton "Quit" [quit]]
   ]
]

///////////////////////////////////////////////////////////////////////////////
//  Menu - Server Browser                                                    //
///////////////////////////////////////////////////////////////////////////////
UIMenu "servers" [
   refreshservers
   UIVScroller 1.0 0.8 0.02 3.0 [
      uitable 0.01 [
         uialign 0 -1
         uitableheader [
            uitext ping 0.7
            uitext players 0.7
            uitext mode 0.7
            uitext map 0.7
            uitext time 0.7
            uitext master 0.7
            uitext port 0.7
            uitext description 0.7
            uialign* -1 0
         ] [
            uiline 0xFFFFFF; uiclamp- 1 1; uialign- 0 1
         ]
         loop i (numservers) [
            if (servinfovalid $i) [
               uitablerow [
                     uitext (servinfoping $i) 0.7
                     uitext (servinfoplayers $i) 0.7
                     uitext (servinfomodename $i) 0.7
                     uitext (servinfomap $i) 0.7
                     uitext (servinfotime $i) 0.7
                     uitext (servinfomastermodename $i) 0.7
                     uitext (servinfoport $i) 0.7
                     uitext (servinfodesc $i) 0.7
                     uialign* -1 0
               ] [
                  uihold [
                     uicolor 0x404040; uiclamp -1 1 1 1
                     uioutline 0xC0C0C0; uiclamp- 1 1 1 1
                  ] [
                     uihover [
                        uicolor 0x585858; uiclamp- 1 1 1 1
                        uioutline 0xC0C0C0; uiclamp- 1 1 1 1
                     ]
                  ]
                  uirelease [connectservinfo $i]
               ]
            ] [
               uitablerow [] [
                  uitext (servinfodesc $i) 0.7
               ]
            ]
         ]
      ]
   ]
] "Server Browser" [initservers]

///////////////////////////////////////////////////////////////////////////////
//  Menu - Player Setup                                                      //
///////////////////////////////////////////////////////////////////////////////
UIMenu "player_setup" [
    uivlist 0.01 [
        uifield name 15 [] 1 [uioutline (uifocus? 0xFF0000 0x4f4d4c); uiclamp- 1 1 1 1]
        uiplayerpreview 0 0 1 0.25 0.25
    ]
] "Player Setup"

///////////////////////////////////////////////////////////////////////////////
//  Menu - Options                                                           //
///////////////////////////////////////////////////////////////////////////////
UIMenu "options" [
   hidechanges 1
   uifill 0.3 0.3
   uitable 0.01 [
      uitablerow [UICheckBox hdr; uitext "HDR" 1 [uitextfill 20]; uialign* -1 0]
      uitablerow [
         uifill 0.03 0.03; uitext "water"
         uihlist 0.01 [
            UICheckBox waterreflect; uitext "reflection"
            UICheckBox caustics; uitext "caustics"
            UICheckBox vertwater; uitext "animation"
         ]
         uialign* -1 0
      ]
      uitablerow [uifill 0.03 0.03; uitext "shadows"; UIRadioSlider smfilter = [0 low 1 medium 2 high 3 ultra] 7; uialign* -1 0]
      uitablerow [
         UICheckBox gi; uitext "global illumination"
         if $gi [
            UIRadioSlider rhtaps = [12 low 20 medium 32 high] 7
         ]
         uialign* -1 0
      ]
      uitablerow [UICheckBox ao; uitext "SSAO"; uialign* -1 0]
      uitablerow [
         uifill 0.03 0.03; uitext "anisotropic filtering"
         UIRadioSlider aniso = [0 off 2 2x 4 4x 8 8x 16 16x] 4
         uialign* -1 0
      ]
      uitablerow [
         uifill 0.03 0.03; uitext "multisample AA"
         UIRadioSlider msaa = [0 off 2 2x 4 4x 8 8x 16 16x] 4
         uialign* -1 0
      ]
      uitablerow [UICheckBox tqaa; uitext "temporal AA"; uialign* -1 0]
      uitablerow [
         UICheckBox smaa; uitext "morphological AA"
         if $ao [
            UIRadioSlider smaaquality = [0 low 1 medium 2 high 3 ultra] 7 [
               smaacoloredge (>= $smaaquality 3)
            ]
         ]
         uialign* -1 0
      ]
      uitablerow [
         UICheckBox fxaa; uitext "FXAA"
         if $fxaa [
            UIRadioSlider fxaaquality = [0 low 1 medium 2 high 3 ultra] 7
         ]
         uialign* -1 0
      ]
      uitablerow [
         UICheckBox grass; uitext "grass"
         if $grass [
            UIRadioSlider grassdist = [128 "quick fade" 256 "moderate fade" 512 "slow fade"] 13
         ]
         uialign* -1 0
      ]
      uitablerow [UICheckBox softparticles; uitext "soft particles"; uialign* -1 0]
      uitablerow [
         UICheckBox decals; uitext "decals"
         if $decals [
            UIRadioSlider decalfade = [10000 "quick fade" 60000 "slow fade"] 13 [
               maxdecaltris (? (> $decalfade 10000) 4096 1024)
            ]
         ]
         uialign* -1 0
      ]
      uitablerow [
         UICheckBox vsync; uitext "v-sync"
         if $vsync [
            uihlist 0.01 [
               UICheckBox vsynctear; uitext "tear"
            ]
         ]
         uialign* -1 0
      ]
      uitablerow [
         uifill 0.03 0.03; uitext "gamma"
         UIHSlider gamma 30 300 [] 0.5 $uitextscale 1
         uialign* -1 0
      ]
   ] 
] "Options" [] [if (pendingchanges) [showui changes]]

///////////////////////////////////////////////////////////////////////////////
//  Menu - Credits                                                           //
///////////////////////////////////////////////////////////////////////////////
creditlist = [
   uicolortext $arg1 0x808080 0.6 [uialign -1 0]
   uiline 0xFFFFFF; uiclamp- 1 1
   uispace 0.01 0 [
      uialign -1 0
      uivlist 0.001 [
         looplist credit $arg2 [
            uitext (strreplace (strreplace $credit "^"" "^fs^f8^"" "^"^fr") ": " "^f4: ") 0.6
         ]
         uialign* -1 0
      ]
   ]
   uispace 0 0.01
]

UIMenu "credits" [
   UIVScroller 0.8 0.8 0.02 3.0 [
      uivlist 0.001 [
         uialign 0 -1
         creditlist "Project" [
            [Lee "eihrul" Salzman]
            [Shane "Nieb" Nieb]
         ]
         creditlist "Code" [
            [Lee "eihrul" Salzman]
            [Benjamin Segovia]
            ["Hirato"]
         ]
         creditlist "Art 2D/3D" [
            [Shane "Nieb" Nieb : Mapmodels, Textures]
            [Gregor Koch : Playermodel]
         ]
         creditlist "Sound" [
            [Shane "Nieb" Nieb]
            ["Q009"]
         ]
         creditlist "Cartography" [
            [Shane "Nieb" Nieb : complex]
         ]
         creditlist "Kudos" [
            ["RaZgRiZ" : UI Documentation]
         ]
         creditlist "Cube Engine 2" [
            [Wouter "Aardappel" van Oortmerssen]
            [Lee "eihrul" Salzman]
            [Mike "Gilt" Dysart]
            [Robert "baby-rabbit" Pointon]
            [Quinton "Quin" Reeves]
            [John "geartrooper" Siar]
            [and others...]
            "http://sauerbraten.org/"
         ]
      ]
   ]
] "Tesseract Contributors"

///////////////////////////////////////////////////////////////////////////////
//  Dialog - Display Changes                                                 //
///////////////////////////////////////////////////////////////////////////////
UIMenuNoTitle "changes" [
   uivlist 0.01 [
      uitext "The follow settings have changed:"
      uispace 0 0.01
      loop i (pendingchanges) [
         uitext (pendingchanges $i)
      ]
      uispace 0 0.01
      uitext "Apply changes now?"
      uihlist 0 [
         UIButton "Yes" [applychanges]
         uispace 0.02 0
         UIButton "No" [hideui changes]
      ]
   ]
]

///////////////////////////////////////////////////////////////////////////////
//  Scoreboard                                                               //
///////////////////////////////////////////////////////////////////////////////
scoreboardtable = [
   uitable 0.01 [
      uiclamp 1 1 0 0
      uialign 0 -1
      if (> $arg1 0) [
         uitableheader [] [
            if (= $arg1 1) [uicolor 0x3333C0] [uicolor 0xC03333]
               uiclamp- 1 1 1 1
            uispace 0.01 0 [
               uialign -1 0
               uitext (getteamname $arg1) 0.9
            ]
            uispace 0.01 0 [
               uialign 1 0
               uitext (getteamscore $arg1) 0.9
            ]
         ]
      ]
      uitableheader [
         if (scoreboardshowfrags) [uitext frags 0.75]
         uitext name 0.75
         if (scoreboardmultiplayer) [
            if $showpj [uitext pj 0.75]
            if $showping [uitext ping 0.75]
         ]
         if (scoreboardshowclientnum) [uitext cn 0.75]
         uialign* -1 0
      ] [
         uiline 0xFFFFFF; uiclamp- 1 1; uialign- 0 1
      ]
      loopscoreboard cn $arg1 [
         uitablerow [
            if (scoreboardshowfrags) [uitext (getclientfrags $cn) 0.9]
            uicolortext (getclientcolorname $cn) (scoreboardstatus $cn)
            if (scoreboardmultiplayer) [
               if $showpj [uitext (scoreboardpj $cn) 0.9]
               if $showping [uitext (scoreboardping $cn) 0.9]
            ]
            if (scoreboardshowclientnum) [uitext $cn 0.9] 
            uialign* -1 0
         ] [
            pushif sbhigh (scoreboardhighlight $cn) [
               uicolor $sbhigh; uiclamp- 1 1 1 1
            ]
         ]   
      ]
   ]
]

scoreboardspectators = [
   uifill 0 0.01
   if (scoreboardshowclientnum) [
      uitable 0.01 [
         uiclamp 1 1 1 1
         uialign 0 -1
         uitableheader [
            uitext spectator 0.75
            uitext cn 0.75
            uialign* -1 0
         ] [
            uiclamp 1 1 1 1
            uiline 0xFFFFFF; uiclamp- 1 1; uialign- 0 1
         ]
         loopscoreboard cn -1 [
            uitablerow [
               uicolortext (getclientcolorname $cn) (scoreboardstatus $cn)
               uitext $cn 0.9
               uialign* -1 0
            ] [
               pushif sbhigh (scoreboardhighlight $cn) [
                  uicolor $sbhigh; uiclamp- 1 1 1 1
               ]
            ]
         ]
      ]
   ] [
      uigroup [
         uiclamp 1 1 1 1
         uitext spectators 0.75
         uiline 0xFFFFFF; uiclamp- 1 1; uialign- 0 1
      ]
      uigrid 3 0.01 [
         uiclamp 1 1 1 1
         loopscoreboard cn -1 [
            uicolortext (getclientcolorname $cn) (scoreboardstatus $cn) 0.75 [
               pushif sbhigh (scoreboardhighlight $cn) [
                  uicolor $sbhigh; uiclamp- 1 1 1 1
               ]
            ]
         ]
      ]
   ]
]

UIMenuNoTitle scoreboard [uiallowinput 0; if $mainmenu [hideui scoreboard] [
   refreshscoreboard
   uispace 0.01 0.01 [
      uivlist 0.01 [
         pushif sbserv (scoreboardservinfo) [
            uitext $sbserv 0.9
         ]
         uihlist 0.01 [
            uitext (scoreboardmode) 0.9
            uispace 0.01 0 [uiclamp 0 0 1 1; uiline 0xC0C0C0; uiclamp- 0 0 1 1]
            uitext (scoreboardmap) 0.9
            pushif sbtime (scoreboardtime) [
               uispace 0.01 0 [uiclamp 0 0 1 1; uiline 0xC0C0C0; uiclamp- 0 0 1 1]
               uitext $sbtime 0.9
            ]
            if (!= $gamespeed 100) [
               uispace 0.01 0 [uiclamp 0 0 1 1; uiline 0xC0C0C0; uiclamp- 0 0 1 1]
               uitext $prettygamespeed 0.9
            ]
            if $paused [
               uispace 0.01 0 [uiclamp 0 0 1 1; uiline 0xC0C0C0; uiclamp- 0 0 1 1]
               uitext paused 0.9
            ]
         ]
         if (m_teammode (getmode)) [
            uihlist 0.02 [
               uiclamp 1 1 1 1
               scoreboardtable 1
               scoreboardtable 2
            ]
         ] [
            scoreboardtable 0
         ]
         if (numscoreboard -1) [
            scoreboardspectators
         ]
      ]
   ]
]] [] [] [] [] 0xBB080808
