///////////////////////////////////////////////////////////////////////////////
//  Menu - Options                                                           //
///////////////////////////////////////////////////////////////////////////////
opt_menu          = 0
opt_column1_wA    = 0.025
opt_column1_wB    = 0.270
opt_column1_w     = (+f $opt_column1_wA $opt_column1_wB)
opt_column2_w     = 0.363
opt_row_h         = 0.032
opt_bar_padding_v = 0.020
opt_UIbar = [
   uitablerow [
      uifill $opt_column1_w $opt_bar_padding_v [UIbar 1]
      uifill $opt_column2_w $opt_bar_padding_v [UIbar 1]
      uialign* -1 0
   ]
]
opt_decalsec = [] //ms to s
opt_ratio_4:3 = [
   "640 480"
   "800 600"
   "1024 768"
   "1152 864"
   "1280 960"
   "1400 1050"
   "1600 1200"
   "1920 1440"
   "2560 1920"
]
opt_ratio_5:4 = [
   "800 640"
   "960 768"
   "1280 1024"
   "1440 1152"
   "1600 1280"
   "1920 1536"
   "2560 2048"
]
opt_ratio_16:9 = [
   "800 450"
   "1024 600"
   "1280 720"
   "1366 768"
   "1600 900"
   "1920 1080"
   "2048 1152"
   "2560 1440"
]
opt_ratio_16:10 = [ 
   "768 480"
   "1024 640"
   "1280 800"
   "1440 900"
   "1680 1050"
   "1920 1200"
   "2560 1600"
]
opt_ratios = [4:3 5:4 16:9 16:10]
opt_ratios_assoc = (concat "custom" "custom" (looplistconcat ratio $opt_ratios [concat $ratio $ratio]))
opt_findratio = [
   looplist ratio $opt_ratios [
      looplist res $[opt_ratio_@ratio] [
         if (=s $res $opt_resolution) [opt_aspectratio = $ratio] 
      ]
   ]
]

UImenu "options" [
   hidechanges 1
   uihlist 0 [
      UIvtab 0.2 (*f 0.048 6) opt_menu [
         0 "Game"          []
         1 "Edit"          []
         2 "Graphics"       []
         3 "Display"       []
         4 "Resolution"    []
         5 "Audio"         []
         //* "Console"       []
      ] 1
      uialign- 0 -1
      UIbar 0 1
      uifill 0.02
      uiscroll 0.67 0.6 [
         uifill 0.67 0.6 [
            uialign 0 -1
            uitable 0 0 [
               uialign -1 -1
               case $opt_menu 0 [//Game////////////////////////////////////////////////////////////
                  opt_UIbar//----------------------------------------------------------------------
                  uitablerow [
                     uihlist 0.01 [uifill $opt_column1_wA ; uitext "FOV" 0.6]
                     uihlist 0 [
                        UIhslider fov 10 150 5 0.3 $opt_row_h [uitext $fov 0.6]
                        uifield fov 3 [] 0.65 style_generic_focus
                     ]
                     uialign* -1 0
                  ]
                  uitablerow [
                     uihlist 0.01 [uifill $opt_column1_wA; uitext "Sensitivity" 0.6]
                     uihlist 0 [
                        UIhslider sensitivity 0.1 40.0 0.1 0.3 $opt_row_h [uitext $sensitivity 0.6]
                        uifield sensitivity 3 [] 0.65 style_generic_focus
                     ]
                     uialign* -1 0
                  ]
                  uitablerow [
                     uihlist 0.01 [uifill $opt_column1_wA ; uitext "Zoom FOV" 0.6]
                     uihlist 0 [
                        UIhslider zoomfov 10 60 1 0.3 $opt_row_h [uitext $zoomfov 0.6]
                        uifield zoomfov 3 [] 0.65 style_generic_focus
                     ]
                     uialign* -1 0
                  ]
                  uitablerow [
                     uihlist 0.01 [uifill $opt_column1_wA ; uitext "Zoom Sensitivity" 0.6]
                     uihlist 0 [
                        UIhslider zoomsens 0.1 20.0 0.1 0.3 $opt_row_h [uitext $zoomsens 0.6]
                        uifield zoomsens 3 [] 0.65 style_generic_focus
                     ]
                     uialign* -1 0
                  ]
                  uitablerow [
                     UIbutton "hold2" [
                        UIcheckbox $zoomtoggle 0.016
                        uitext "Zoom Toggle" 0.6
                     ] $opt_column1_wB $opt_row_h [zoomtoggle (! $zoomtoggle)] -1
                     uialign* -1 0
                  ]
                  uitablerow [
                     UIbutton "hold2" [
                        UIcheckbox $invmouse 0.016
                        uitext "Invert Mouse" 0.6
                     ] $opt_column1_wB $opt_row_h [invmouse (! $invmouse)] -1
                     uialign* -1 0
                  ]
                  uitablerow [
                     uihlist 0.01 [uifill $opt_column1_wA ; uitext "UI Sensitivity" 0.6]
                     uihlist 0 [
                        UIhslider uisensitivity 0.1 4.0 0.1 0.3 0.032 [uitext $uisensitivity 0.6]
                        uifield uisensitivity 3 [] 0.65 style_generic_focus
                     ]
                     uialign* -1 0
                  ]
                  opt_UIbar//----------------------------------------------------------------------
                  uitablerow [
                     UIbutton "hold2" [
                        UIcheckbox $showfps 0.016
                        uitext "Show FPS" 0.6
                     ] $opt_column1_wB $opt_row_h [showfps (! $showfps)] -1
                     uialign* -1 0
                  ]
                  uitablerow [
                     UIbutton "hold2" [
                        UIcheckbox $wallclock 0.016
                        uitext "Show Clock" 0.6
                     ] $opt_column1_wB $opt_row_h [wallclock (! $wallclock)] -1
                     uialign* -1 0
                     uihlist 0 [
                        UIbutton "hold2" [
                           UIcheckbox $wallclock24 0.016
                           uitext (concatword (? (! $wallclock) "^f4") "24 Hour") 0.6
                        ] 0.16 $opt_row_h [wallclock24 (! $wallclock24)] -1
                        UIbutton "hold2" [
                           UIcheckbox $wallclocksecs 0.016
                           uitext (concatword (? (! $wallclock) "^f4") "Seconds") 0.6
                        ] 0.16 $opt_row_h [wallclocksecs (! $wallclocksecs)] -1
                     ]
                     uialign* -1 0
                  ]
                  uitablerow [
                     UIbutton "hold2" [
                        UIcheckbox $hudgun 0.016
                        uitext "Show Weapon" 0.6
                     ] $opt_column1_wB $opt_row_h [hudgun (! $hudgun)] -1
                     uialign* -1 0
                  ]
                  uitablerow [
                     UIbutton "hold2" [
                        UIcheckbox $damagescreen 0.016
                        uitext "Bloody Screen" 0.6
                     ] $opt_column1_wB $opt_row_h [damagescreen (! $damagescreen)] -1
                     uialign* -1 0
                  ]
                  uitablerow [
                     UIbutton "hold2" [
                        UIcheckbox $blood 0.016
                        uitext "Blood" 0.6
                     ] $opt_column1_wB $opt_row_h [blood (! $blood)] -1
                     uialign* -1 0
                  ]
                  opt_UIbar//----------------------------------------------------------------------
                  uitablerow [
                     UIbutton "hold2" [
                        UIcheckbox (! $hidedead) 0.016
                        uitext "Show Dead Players" 0.6
                     ] $opt_column1_wB $opt_row_h [hidedead (! $hidedead)] -1
                     UIhslider ragdollmillis 0 300000 5000 0.3 $opt_row_h [uitext (concatword (? $hidedead "^f4") (div $ragdollmillis 1000)) 0.6]
                     uialign* -1 0
                  ]
                  uitablerow [
                     uihlist 0.01 [uifill $opt_column1_wA ; uitext (concatword (? $hidedead "^f4") "Ragdoll Velocity") 0.6]
                     UIhslider deadpush 1 20 1 0.3 $opt_row_h [uitext (concatword (? $hidedead "^f4") $deadpush) 0.6]
                     uialign* -1 0
                  ]
                  uitablerow [
                     uihlist 0.01 [uifill $opt_column1_wA ; uitext "Fullbright Models" 0.6]
                     uihlist 0 [
                        UIlistslider fullbrightmodels = [0 "^f4off" 60 "^f1subtle" 100 "^f0bright" 150 "^f2overbright" 200 "^f3max"] 0.3 $opt_row_h
                        uifield fullbrightmodels 3 [] 0.65 style_generic_focus
                     ]
                     uialign* -1 0
                  ]
                  opt_UIbar//----------------------------------------------------------------------
                  uitablerow [
                     uihlist 0.01 [uifill $opt_column1_wA ; uitext "Radar" 0.6]
                     uihlist 0 [
                        UIbutton "hold2" [
                           UIcheckbox $radarteammates 0.016
                           uitext "Teammates" 0.6
                        ] 0.15 $opt_row_h [radarteammates (! $radarteammates)] -1
                        UIbutton "hold2" [
                           UIcheckbox $showminimap 0.016
                           uitext "Mini-Map" 0.6
                        ] 0.15 $opt_row_h [showminimap (! $showminimap)] -1
                     ]
                     uialign* -1 0
                  ]
                  opt_UIbar//----------------------------------------------------------------------
                  uitablerow [
                     uihlist 0.01 [uifill $opt_column1_wA ; uitext "Scoreboard" 0.6]
                     uivlist 0 [
                        uihlist 0 [
                           UIbutton "hold2" [
                              UIcheckbox $showping 0.016
                              uitext "Ping" 0.6
                           ] 0.15 $opt_row_h [showping (! $showping)] -1
                           UIbutton "hold2" [
                              UIcheckbox $showpj 0.016
                              uitext "Packet-Jump" 0.6
                           ] 0.15 $opt_row_h [showpj (! $showpj)] -1
                        ]
                        uihlist 0 [
                           UIbutton "hold2" [
                              UIcheckbox $showclientnum 0.016
                              uitext "Client#" 0.6
                           ] 0.15 $opt_row_h [showclientnum (! $showclientnum)] -1
                           UIbutton "hold2" [
                              UIcheckbox $showspectators 0.016
                              uitext "Spectators" 0.6
                           ] 0.15 $opt_row_h [showspectators (! $showspectators)] -1
                        ]
                        uialign* -1 -1
                     ]
                     uialign* -1 0
                  ]
                  opt_UIbar//----------------------------------------------------------------------
               ] 1 [//Edit/////////////////////////////////////////////////////////////////////////
                  opt_UIbar//----------------------------------------------------------------------
                  uitablerow [
                     uihlist 0.01 [uifill $opt_column1_wA; uitext "Shift Floatspeed" 0.6]
                     uihlist 0 [
                        UIhslider editfloatspeed 10 10000 10 0.3 $opt_row_h [uitext $editfloatspeed 0.6]
                        uifield editfloatspeed 4 [] 0.65 style_generic_focus
                     ]
                     uialign* -1 0
                  ]
                  opt_UIbar//----------------------------------------------------------------------
               ] 2 [//Graphics//////////////////////////////////////////////////////////////////////
                  opt_UIbar//----------------------------------------------------------------------
                  uitablerow [
                     uihlist 0.01 [uifill $opt_column1_wA ; uitext "Shadow Resolution" 0.6]
                     UIlistslider smsize = [10 "^f4lowest" 11 "^f1low" 12 "^f0medium" 13 "^f2high" 14 "^f3ultra"] 0.3 $opt_row_h
                     uialign* -1 0
                  ]
                  uitablerow [
                     uihlist 0.01 [uifill $opt_column1_wA ; uitext "Shadow Filtering" 0.6]
                     UIlistslider smfilter = [0 "^f1low" 1 "^f0medium" 2 "^f2high" 3 "^f3ultra"] 0.3 $opt_row_h
                     uialign* -1 0
                  ]
                  uitablerow [
                     UIbutton "hold2" [
                        UIcheckbox $gi 0.016
                        uitext "Global Illumination" 0.6
                     ] $opt_column1_wB $opt_row_h [gi (! $gi)] -1
                     UIlistslider rhtaps = [
                        12 @(concatword (? (! $gi) "^f4" "^f1") "low")
                        20 @(concatword (? (! $gi) "^f4" "^f0") "medium")
                        32 @(concatword (? (! $gi) "^f4" "^f2") "high")
                     ] 0.3 $opt_row_h
                     uialign* -1 0
                  ]
                  uitablerow [
                     UIbutton "hold2" [
                        UIcheckbox $ao 0.016
                        uitext "SSAO" 0.6
                     ] $opt_column1_wB $opt_row_h [ao (! $ao)] -1
                     UIlistslider aotaps = [
                        5  @(concatword (? (! $ao) "^f4" "^f1") "low")
                        8  @(concatword (? (! $ao) "^f4" "^f0") "medium")
                        12 @(concatword (? (! $ao) "^f4" "^f2") "high")
                     ] 0.3 $opt_row_h [aobilateral (? (>= $aotaps 8) 4 3)]
                     uialign* -1 0
                  ]
                  opt_UIbar//----------------------------------------------------------------------
                  uitablerow [
                     uihlist 0.01 [uifill $opt_column1_wA ; uitext "Water" 0.6]
                     uivlist 0 [
                        uihlist 0 [
                           UIbutton "hold2" [
                              UIcheckbox $waterreflect 0.016
                              uitext "reflection" 0.6
                           ] 0.15 $opt_row_h [waterreflect (! $waterreflect)] -1
                           UIbutton "hold2" [
                              UIcheckbox $caustics 0.016
                              uitext "caustics" 0.6
                           ] 0.15 $opt_row_h [caustics (! $caustics)] -1
                        ]
                        UIbutton "hold2" [
                           UIcheckbox $vertwater 0.016
                           uitext "animation" 0.6
                        ] 0.15 $opt_row_h [vertwater (! $vertwater)] -1
                        uialign* -1 -1
                     ]
                     uialign* -1 0
                  ]
                  uitablerow [
                     UIbutton "hold2" [
                        UIcheckbox $softparticles 0.016
                        uitext "Soft Particles" 0.6
                     ] $opt_column1_wB $opt_row_h [softparticles (! $softparticles)] -1
                     uialign* -1 0
                  ]
                  uitablerow [
                     UIbutton "hold2" [
                        UIcheckbox $decals 0.016
                        uitext "Decals" 0.6
                     ] $opt_column1_wB $opt_row_h [decals (! $decals)] -1
                     uihlist 0 [
                        decalfadesecs = (div $decalfade 1000)
                        UIlistslider decalfadesecs = [
                           2 @(concatword (? (! $decals) "^f4" "^f1") "instant-fade")
                           15 @(concatword (? (! $decals) "^f4" "^f0") "quick-fade")
                           30 @(concatword (? (! $decals) "^f4" "^f2") "moderate-fade")
                           60 @(concatword (? (! $decals) "^f4" "^f3") "slow-fade")
                        ] 0.3 $opt_row_h [
                           decalfade = (* $decalfadesecs 1000)
                           maxdecaltris (? (> $decalfade 15000) 8192 2048)
                        ]
                        uifield decalfadesecs 4 [
                           decalfade = (* $decalfadesecs 1000)
                           maxdecaltris (? (> $decalfade 15000) 8192 2048)
                        ] 0.65 style_generic_focus
                     ]
                     uialign* -1 0
                  ]
                  uitablerow [
                     UIbutton "hold2" [
                        UIcheckbox $grass 0.016
                        uitext "3D Grass" 0.6
                     ] $opt_column1_wB $opt_row_h [grass (! $grass)] -1
                     uihlist 0 [
                        UIlistslider grassdist = [
                           128 @(concatword (? (! $grass) "^f4" "^f1") "quick-fade")
                           256 @(concatword (? (! $grass) "^f4" "^f0") "moderate-fade")
                           512 @(concatword (? (! $grass) "^f4" "^f2") "slow-fade")
                           1024 @(concatword (? (! $grass) "^f4" "^f3") "slower-fade")
                        ] 0.3 $opt_row_h
                        uifield grassdist 4 [] 0.65 style_generic_focus
                     ]
                     uialign* -1 0
                  ]
                  opt_UIbar//----------------------------------------------------------------------
               ] 3 [//Display//////////////////////////////////////////////////////////////////////
                  opt_UIbar//----------------------------------------------------------------------
                  uitablerow [
                     uihlist 0.01 [uifill $opt_column1_wA ; uitext "Anisotropic Filtering" 0.6]
                     UIlistslider aniso = [0 "^f4off" 2 "^f12x" 4 "^f04x" 8 "^f28x" 16 "^f316x"] 0.3 $opt_row_h
                     uialign* -1 0
                  ]
                  uitablerow [
                     uihlist 0.01 [uifill $opt_column1_wA ; uitext "Texture Filtering" 0.6]
                     uihlist 0 [
                        UIbutton "hold2" [
                           UIcheckbox $bilinear 0.016
                           uitext "Bilinear" 0.6
                        ] 0.15 $opt_row_h [bilinear (! $bilinear)] -1
                        UIbutton "hold2" [
                           UIcheckbox $trilinear 0.016
                           uitext "Trilinear" 0.6
                        ] 0.15 $opt_row_h [trilinear (! $trilinear)] -1
                     ]
                     uialign* -1 0
                  ]
                  opt_UIbar//----------------------------------------------------------------------
                  uitablerow [
                     UIbutton "hold2" [
                        UIcheckbox $smaa 0.016
                        uitext "Morphological AA" 0.6
                     ] $opt_column1_wB $opt_row_h [smaa (! $smaa)] -1
                     UIlistslider smaaquality = [
                        0 @(concatword (? (! $smaa) "^f4" "^f1") "low")
                        1 @(concatword (? (! $smaa) "^f4" "^f0") "medium")
                        2 @(concatword (? (! $smaa) "^f4" "^f2") "high")
                        3 @(concatword (? (! $smaa) "^f4" "^f3") "ultra")
                     ] 0.3 $opt_row_h [smaacoloredge (>= $smaaquality 3)]
                     uialign* -1 0
                  ]
                  uitablerow [
                     UIbutton "hold2" [
                        UIcheckbox $tqaa 0.016
                        uitext "Temporal AA" 0.6
                     ] $opt_column1_wB $opt_row_h [tqaa (! $tqaa)] -1
                     uialign* -1 0
                  ]
                  uitablerow [
                     UIbutton "hold2" [
                        UIcheckbox $fxaa 0.016
                        uitext "FXAA" 0.6
                     ] $opt_column1_wB $opt_row_h [fxaa (! $fxaa)] -1
                     UIlistslider fxaaquality = [
                        0 @(concatword (? (! $fxaa) "^f4" "^f1") "low")
                        1 @(concatword (? (! $fxaa) "^f4" "^f0") "medium")
                        2 @(concatword (? (! $fxaa) "^f4" "^f2") "high")
                        3 @(concatword (? (! $fxaa) "^f4" "^f3") "ultra")
                     ] 0.3 $opt_row_h
                     uialign* -1 0
                  ]
                  uitablerow [
                     uihlist 0.01 [uifill $opt_column1_wA ; uitext "Multisample AA" 0.6]
                     UIlistslider msaa = [0 "^f4off" 2 "^f12x" 4 "^f04x" 8 "^f28x" 16 "^f316x"] 0.3 $opt_row_h
                     uialign* -1 0
                  ]
                  opt_UIbar//----------------------------------------------------------------------
                  uitablerow [
                     uihlist 0.01 [uifill $opt_column1_wA ; uitext "Gamma" 0.6]
                     uihlist 0 [
                        UIhslider gamma 30 300 5 0.3 $opt_row_h [uitext $gamma 0.6]
                        uifield gamma 3 [] 0.65 style_generic_focus
                     ]
                     uialign* -1 0
                  ]
                  uitablerow [
                     UIbutton "hold2" [
                        UIcheckbox $vsync 0.016
                        uitext "V-sync" 0.6
                     ] $opt_column1_wB $opt_row_h [vsync (! $vsync)] -1
                     UIbutton "hold2" [
                        UIcheckbox $vsynctear 0.016
                        uitext (concatword (? (! $vsync) "^f4") "Tear")  0.6
                     ] 0.16 $opt_row_h [vsynctear (! $vsynctear)] -1
                     uialign* -1 0
                  ]
                  opt_UIbar//----------------------------------------------------------------------
               ] 4 [//Resolution///////////////////////////////////////////////////////////////////
                  opt_UIbar//----------------------------------------------------------------------
                  uitablerow [
                     uihlist 0.01 [uifill $opt_column1_wA ; uitext "Aspect Ratio" 0.6]
                     UIlistslider opt_aspectratio =s $opt_ratios_assoc 0.3 $opt_row_h [
                        if (=s $opt_aspectratio "custom") [] [
                           opt_resolution = (at $[opt_ratio_@opt_aspectratio] 1)
                           screenw (at $opt_resolution 0)
                           screenh (at $opt_resolution 1)
                        ]
                     ]
                     uialign* -1 0
                  ]
                  uitablerow [
                     UIbutton "hold2" [
                        UIcheckbox (!=s $opt_aspectratio "custom") 0.016
                        uitext "Preset Resolution" 0.6
                     ] $opt_column1_wB $opt_row_h [
                        if (=s $opt_aspectratio "custom") [
                           opt_findratio
                           if (=s $opt_aspectratio "custom") [
                              opt_aspectratio = "16:10"
                              opt_resolution = (at $[opt_ratio_@opt_aspectratio] 1)
                              screenw (at $opt_resolution 0)
                              screenh (at $opt_resolution 1)
                           ]
                        ] [
                           opt_aspectratio = "custom"
                        ]
                     ] -1
                     if (=s $opt_aspectratio "custom") [
                        uihlist 0.009 [
                           uifield screenw 5 [] 0.65 style_generic_focus
                           uitext "x" 0.6
                           uifield screenh 5 [] 0.65 style_generic_focus
                           UIbutton "" [uitext "Native" 0.6] 0.1 $opt_row_h [
                              opt_resolution = (concat $desktopw $desktoph)
                              opt_findratio
                              screenw (at $opt_resolution 0)
                              screenh (at $opt_resolution 1)
                           ]
                        ]
                     ] [
                        UIlistslider opt_resolution = (
                           looplistconcat i $[opt_ratio_@opt_aspectratio] [
                              result (concat (escape $i) (strreplace $i " " "x"))
                           ]
                        ) 0.3 $opt_row_h [
                            screenw (at $opt_resolution 0)
                            screenh (at $opt_resolution 1)
                        ]
                     ]
                     uialign* -1 0
                  ]
                  opt_UIbar//----------------------------------------------------------------------
               ] 5 [//Audio////////////////////////////////////////////////////////////////////////
                  opt_UIbar//----------------------------------------------------------------------
                  uitablerow [
                     UIbutton "hold2" [
                        UIcheckbox $sound 0.016
                        uitext "Enable Audio" 0.6
                     ] $opt_column1_wB $opt_row_h [sound (! $sound)] -1
                     uialign* -1 0
                  ]
                  opt_UIbar//----------------------------------------------------------------------
                  uitablerow [
                     uihlist 0.01 [uifill $opt_column1_wA ; uitext (concatword (? (! $sound) "^f4") "Sound Volume") 0.6]
                     uihlist 0 [
                        UIhslider soundvol 0 255 5 0.3 $opt_row_h [uitext $soundvol 0.6]
                        uifield soundvol 3 [] 0.65 style_generic_focus
                     ]
                     uialign* -1 0
                  ]
                  uitablerow [
                     uihlist 0.01 [uifill $opt_column1_wA ; uitext (concatword (? (! $sound) "^f4") "Music Volume") 0.6]
                     uihlist 0 [
                        UIhslider musicvol 0 255 5 0.3 $opt_row_h [uitext $musicvol 0.6]
                        uifield musicvol 3 [] 0.65 style_generic_focus
                     ]
                     uialign* -1 0
                  ]
                  opt_UIbar//----------------------------------------------------------------------
                  uitablerow [
                     UIbutton "hold2" [
                        UIcheckbox $hitsound 0.016
                        uitext (concatword (? (! $sound) "^f4") "Hit Confirmation") 0.6
                     ] $opt_column1_wB $opt_row_h [hitsound (! $hitsound)] -1
                     uialign* -1 0
                  ]
                  opt_UIbar//----------------------------------------------------------------------
                  uitablerow [
                     UIbutton "hold2" [
                        UIcheckbox $mumble 0.016
                        uitext "Mumble Positional Audio" 0.6
                     ] $opt_column1_wB $opt_row_h [mumble (! $mumble)] -1
                     uialign* -1 0
                  ]
                  opt_UIbar//----------------------------------------------------------------------
               ] 
            ]
         ]
      ]; UIvscroll 0.02 0.6 1.421 0.5
   ]
] [
   opt_aspectratio = "custom"
   opt_resolution  = (concat $screenw $screenh)
   opt_findratio
] [if (pendingchanges) [showui "changes"]] [] [uitext "Options" 0.8]

