///////////////////////////////////////////////////////////////////////////////
//  Menu - Options                                                           //
///////////////////////////////////////////////////////////////////////////////
_options_ratio_4:3   = ["800 600" "1024 768" "1280 960" "1400 1050" "1600 1200" "1920 1440" "2560 1920"]
_options_ratio_8:5   = ["768 480" "1024 640" "1280 800" "1440 900" "1680 1050" "1920 1200" "2560 1600"]
_options_ratio_16:9  = ["800 450" "1024 600" "1280 720" "1600 900" "1920 1050" "2048 1152" "2560 1440"]
_options_ratio_5:4   = ["800 640" "960 768" "1280 1024" "1440 1152" "1600 1280" "1920 1536" "2560 2048"]

ui_menu "options" [
   hidechanges 1
   uihlist 0 [
      ui_vtab 0.2 (*f 0.048 6) _options_menu [
         0 "Resolution"    []
         1 "Quality"       []
         2 "Anti-aliasing" []
         3 "Sound"         []
         4 "Playermodels"  []
         5 "Others"        []
      ] 1
      uialign- 0 -1
      ui_bar 0 1
      uifill 0.02
      uifill 0.6612 0.3 [
         uialign 0 -1
         uitable 0.01 0.02 [
            uialign -1 -1
            case $_options_menu 0 [
               uitablerow [
                  uihlist 0.01 [uifill 0.022 ; uitext "Aspect Ratio" 0.6]
                  ui_listslider _options_aspectratio = [4:3 "4:3" 8:5 "8:5" 16:9 "16:9" 5:4 "5:4"] 0.3 0.032 [
                     _options_resolution = (at $[_options_ratio_@_options_aspectratio] 1)
                     do [screenres @_options_resolution]
                     _options_customres = 0
                  ]
                  uialign* -1 0
               ]
               uitablerow [
                  ui_button "hold2" [
                     ui_checkbox (! $_options_customres) 0.016
                     uitext (concatword (? $_options_customres "^f4") "Preset Resolution") 0.6
                  ] 0.26 0.032 [
                     _options_customres = (! $_options_customres)
                     if $_options_customres [_options_aspectratio = 0] [_options_aspectratio = 16:10]
                  ] -1
                  if $_options_customres [
                     uihlist 0.009 [
                        uifield screenw 5 [] 0.65 _style_generic_focus
                        uitext "x" 0.6
                        uifield screenh 5 [] 0.65 _style_generic_focus
                        ui_button "" [uitext "Native" 0.6] 0.1 0.032 [
                           echo "^f3FEATURE NOT IMPLEMENTED"
                        ]
                     ]
                  ] [
                     ui_listslider _options_resolution = (
                        looplistconcat i $[_options_ratio_@_options_aspectratio] [
                           result (concat (escape $i) (strreplace $i " " "x"))
                        ]
                     ) 0.3 0.032 [do [screenres @_options_resolution]]
                  ]
                  uialign* -1 0
               ]
               uitablerow [
                  uispace 0 0.016 [ui_bar 1]
                  uispace 0 0.016 [ui_bar 1]
                  uiclamp* 1 1
               ]
               uitablerow [
                  ui_button "hold2" [
                     ui_checkbox $vsync 0.016
                     uitext (concatword (? (! $vsync) "^f4") "V-sync") 0.6
                  ] 0.26 0.032 [vsync (! $vsync)] -1
                  ui_button "hold2" [
                     ui_checkbox $vsynctear 0.016
                     uitext (concatword (? (! (&& $vsync $vsynctear)) "^f4") "Tear")  0.6
                  ] 0.16 0.032 [vsynctear (! $vsynctear)] -1
                  uialign* -1 0
               ]
               uitablerow [
                  uihlist 0.01 [uifill 0.022 ; uitext "Gamma" 0.6]
                  uihlist 0 [
                     ui_hslider gamma 30 300 5 0.3 0.032 [uitext $gamma 0.6]
                     uifield gamma 3 [] 0.65 _style_generic_focus
                  ]
                  uialign* -1 0
               ]
            ] 1 [
               uitablerow [
                  uihlist 0.01 [uifill 0.022 ; uitext "Anisotropic Filtering" 0.6]
                  ui_listslider aniso = [0 "^f4off" 2 "^f12x" 4 "^f04x" 8 "^f28x" 16 "^f316x"] 0.3 0.032
                  uialign* -1 0
               ]
               uitablerow [
                  uihlist 0.01 [uifill 0.022 ; uitext "Shadow Quality" 0.6]
                  ui_listslider smsize = [10 "^f4lowest" 11 "^f1low" 12 "^f0medium" 13 "^f2high" 14 "^f3ultra"] 0.3 0.032
                  uialign* -1 0
               ]
               uitablerow [
                  uihlist 0.01 [uifill 0.022 ; uitext "Shadow Filtering" 0.6]
                  ui_listslider smfilter = [0 "^f1low" 1 "^f0medium" 2 "^f2high" 3 "^f3ultra"] 0.3 0.032
                  uialign* -1 0
               ]
               uitablerow [
                  ui_button "hold2" [
                     ui_checkbox $gi 0.016
                     uitext (concatword (? (! $gi) "^f4") "Global Illumination") 0.6
                  ] 0.26 0.032 [gi (! $gi)] -1
                  ui_listslider rhtaps = [12 "^f1low" 20 "^f0medium" 32 "^f2high"] 0.3 0.032
                  uialign* -1 0
               ]
               uitablerow [
                  ui_button "hold2" [
                     ui_checkbox $ao 0.016
                     uitext (concatword (? (! $ao) "^f4") "SSAO") 0.6
                  ] 0.26 0.032 [ao (! $ao)] -1
                  ui_listslider aotaps = [5 "^f1low" 8 "^f0medium" 12 "^f2high"] 0.3 0.032 [aobilateral (? (>= $aotaps 8) 4 3)]
                  uialign* -1 0
               ]
               uitablerow [
                  uihlist 0.01 [uifill 0.022 ; uitext "Texture Filtering" 0.6]
                  uihlist 0 [
                     ui_button "hold2" [
                        ui_checkbox $bilinear 0.016
                        uitext (concatword (? (! $bilinear) "^f4") "Bilinear") 0.6
                     ] 0.15 0.032 [bilinear (! $bilinear)] -1
                     ui_button "hold2" [
                        ui_checkbox $trilinear 0.016
                        uitext (concatword (? (! $trilinear) "^f4") "Trilinear") 0.6
                     ] 0.15 0.032 [trilinear (! $trilinear)] -1
                  ]
                  uialign* -1 0
               ]
            ] 2 [
               uitablerow [
                  ui_button "hold2" [
                     ui_checkbox $fxaa 0.016
                     uitext (concatword (? (! $fxaa) "^f4") "FXAA") 0.6
                  ] 0.26 0.032 [fxaa (! $fxaa)] -1
                  ui_listslider fxaaquality = [0 "^f1low" 1 "^f0medium" 2 "^f2high" 3 "^f3ultra"] 0.3 0.032
                  uialign* -1 0
               ]
               uitablerow [
                  ui_button "hold2" [
                     ui_checkbox $smaa 0.016
                     uitext (concatword (? (! $smaa) "^f4") "Morphological AA") 0.6
                  ] 0.26 0.032 [smaa (! $smaa)] -1
                  ui_listslider smaaquality = [0 "^f1low" 1 "^f0medium" 2 "^f2high" 3 "^f3ultra"] 0.3 0.032 [smaacoloredge (>= $smaaquality 3)]
                  uialign* -1 0
               ]
               uitablerow [
                  uihlist 0.01 [uifill 0.022 ; uitext "Multisample AA" 0.6]
                  ui_listslider msaa = [0 "^f4off" 2 "^f12x" 4 "^f04x" 8 "^f28x" 16 "^f316x"] 0.3 0.032
                  uialign* -1 0
               ]
               uitablerow [
                  ui_button "hold2" [
                     ui_checkbox $tqaa 0.016
                     uitext (concatword (? (! $tqaa) "^f4") "Temporal AA") 0.6
                  ] 0.26 0.032 [tqaa (! $tqaa)] -1
               ]
            ] 3 [
               uitablerow [
                  ui_button "hold2" [
                     ui_checkbox $sound 0.016
                     uitext (concatword (? (! $sound) "^f4") "Sound Volume") 0.6
                  ] 0.26 0.032 [sound (! $sound)] -1
                  uihlist 0 [
                     ui_hslider soundvol 0 255 5 0.3 0.032 [uitext $soundvol 0.6]
                     uifield soundvol 3 [] 0.65 _style_generic_focus
                  ]
                  uialign* -1 0
               ]
               uitablerow [
                  uihlist 0.01 [uifill 0.048 ; uitext (concatword (? (! $sound) "^f4") "Music Volume") 0.6]
                  uihlist 0 [
                     ui_hslider musicvol 0 255 5 0.3 0.032 [uitext $musicvol 0.6]
                     uifield musicvol 3 [] 0.65 _style_generic_focus
                  ]
                  uialign* -1 0
               ]
               uitablerow [
                  ui_button "hold2" [
                     uifill 0.016
                     ui_checkbox $hitsound 0.016
                     uitext (concatword (? (! (&& $sound $hitsound)) "^f4") "Hit Confirmation") 0.6
                  ] 0.26 0.032 [hitsound (! $hitsound)] -1
                  uialign* -1 0
               ]
            ] 4 [
               uitablerow [
                  uihlist 0.01 [uifill 0.022 ; uitext "Fullbright Models" 0.6]
                  uihlist 0 [
                     ui_listslider fullbrightmodels = [0 "^f4off" 60 "^f1subtle" 100 "^f0bright" 150 "^f2overbright" 200 "^f3max"] 0.3 0.032
                     uifield fullbrightmodels 3 [] 0.65 _style_generic_focus
                  ]
                  uialign* -1 0
               ]
               uitablerow [
                  ui_button "hold2" [
                     ui_checkbox $forceplayermodels 0.016
                     uitext (concatword (? (! $forceplayermodels) "^f4") "Match Playermodels") 0.6
                  ] 0.26 0.032 [forceplayermodels (! $forceplayermodels)] -1
                  uialign* -1 0
               ]
               uitablerow [
                  ui_button "hold2" [
                     ui_checkbox (! $hidedead) 0.016
                     uitext (concatword (? $hidedead "^f4") "Show Dead Players") 0.6
                  ] 0.26 0.032 [hidedead (! $hidedead)] -1
                  uialign* -1 0
               ]
               uitablerow [
                  ui_button "hold2" [
                     uifill 0.016
                     ui_checkbox $ragdoll 0.016
                     uitext (concatword (? (|| $hidedead (! $ragdoll)) "^f4") "Ragdoll Deaths") 0.6
                  ] 0.26 0.032 [ragdoll (! $ragdoll)] -1
                  ui_button "hold2" [
                     ui_checkbox $ragdollmillis 0.016
                     uitext (concatword (? (|| $hidedead (! $ragdoll) (! $ragdollmillis)) "^f4") "Keep After Respawn") 0.6
                  ] 0.3 0.032 [ragdollmillis (? $ragdollmillis 0 10000)] -1
                  uialign* -1 0
               ]
               uitablerow [
                  uihlist 0.01 [uifill 0.048 ; uitext "Ragdoll Velocity" 0.6]
                  ui_hslider deadpush 1 20 1 0.3 0.032 [uitext $deadpush 0.6]
                  uialign* -1 0
               ]
            ] 5 [
               uitablerow [
                  uihlist 0.01 [uifill 0.022 ; uitext "Water" 0.6]
                  uivlist 0 [
                     uihlist 0 [
                        ui_button "hold2" [
                           ui_checkbox $waterreflect 0.016
                           uitext "reflection" 0.6
                        ] 0.15 0.032 [waterreflect (! $waterreflect)] -1
                        ui_button "hold2" [
                           ui_checkbox $caustics 0.016
                           uitext "caustics" 0.6
                        ] 0.15 0.032 [caustics (! $caustics)] -1
                     ]
                     ui_button "hold2" [
                        ui_checkbox $vertwater 0.016
                        uitext "animation" 0.6
                     ] 0.15 0.032 [vertwater (! $vertwater)] -1
                     uialign* -1 -1
                  ]
                  uialign* -1 0
               ]
               uitablerow [
                  ui_button "hold2" [
                     ui_checkbox $softparticles 0.016
                     uitext (concatword (? (! $softparticles) "^f4") "Soft Particles") 0.6
                  ] 0.26 0.032 [softparticles (! $softparticles)] -1
                  uialign* -1 0
               ]
               uitablerow [
                  ui_button "hold2" [
                     ui_checkbox $decals 0.016
                     uitext (concatword (? (! $decals) "^f4") "Decals") 0.6
                  ] 0.26 0.032 [decals (! $decals)] -1
                  uihlist 0 [
                     ui_listslider decalfade = [2000 "^f4instant fade" 15000 "^f1quick fade" 30000 "^f0moderate fade" 60000 "^f2slow fade"] 0.3 0.032 [
                        maxdecaltris (? (> $decalfade 15000) 8192 2048)
                     ]
                     uifield decalfade 5 [
                        maxdecaltris (? (> $decalfade 15000) 8192 2048)
                     ] 0.65 _style_generic_focus
                  ]
                  uialign* -1 0
               ]
               uitablerow [
                  ui_button "hold2" [
                     ui_checkbox $grass 0.016
                     uitext (concatword (? (! $grass) "^f4") "3D Grass") 0.6
                  ] 0.26 0.032 [grass (! $grass)] -1
                  uihlist 0 [
                     ui_listslider grassdist = [128 "^f1quick fade" 256 "^f0moderate fade" 512 "^f2slow fade" 1024 "^f3slower fade"] 0.3 0.032
                     uifield grassdist 5 [] 0.65 _style_generic_focus
                  ]
                  uialign* -1 0
               ]
            ]
         ]
      ]
   ]
] [
   _options_menu        = 0
   _options_customres   = 0
   _options_aspectratio = 4:3
   _options_resolution  = "800 600"
] [if (pendingchanges) [showui "changes"]] [] [uitext "Options" 0.85]