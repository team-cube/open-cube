// styles used by the elements of stdui.cfg for the game menus

textbright 90

_ui_window_padding = 0.010
_ui_button_padding = 0.006

//looplist2 a b [
//    "open"    150
//    "close"   150
//    "hover"   120
//    "release" 120
//] [[ui_sound_@a] = [playsound @(registersound [ui/@a.wav] (max 0 (min 255 $b)))]]

// preset colors used in styles below

_c_menu   = 0x202020   // window background
_c_menu_a = 0xC8202020 // window transparent background
_c_line   = 0x303030   // all-around line
_c_line_a = 0xC8383838 // all-around transparent line

_c_main1  = 0x282828   // main title gradient top
_c_main2  = 0x181818   // main title gradient bottom

_c_boxmod = 0xE0E0E0   // box modulate
_c_grdmod = 0xC4C4C4   // gradient modulate
_c_fill   = 0xF0E8E0   // white area
_c_focus  = 0xFF1008   // focus area

_c_off    = 0x484848   // checkbox/radio off
_c_on1    = 0x20D020   // checkbox on
_c_on2    = 0xD02020   // radio on

// preset colors used by the scoreboard below

_c_open1  = 0xC0202020 // open team background
_c_open2  = 0x404040   // open team outline

_c_spec1  = 0xC0303840 // spec team background
_c_spec2  = 0x607080   // spec team outline

_c_azul1  = 0xC0203070 // team 1 background
_c_azul2  = 0x4060D0   // team 1 outline

_c_rojo1  = 0xC0702020 // team 2 background
_c_rojo2  = 0xD04040   // team 2 outline

// generic styles used for combinations below

_style_generic_default = [_style_generic_box]
_style_generic_none = []

_style_generic_box = [
   uimodcolor $_c_boxmod
   uioutline $_c_line
]

_style_generic_focus = [
   uioutline (uifocus? $_c_focus $_c_line)
   uiclamp- 1 1 1 1
]

_style_generic_gradient1 = [uimodvgradient $_c_grdmod 0xFFFFFF]
_style_generic_gradient2 = [uimodvgradient 0xFFFFFF $_c_grdmod]
_style_generic_gradient3 = [uimodhgradient $_c_grdmod 0xFFFFFF]
_style_generic_gradient4 = [uimodhgradient 0xFFFFFF $_c_grdmod]

_style_generic_linesh = [
   uivlist $arg2 [
      loop i $arg1 [uiline $_c_line $arg3]
   ]
]

_style_generic_linesv = [
   uihlist $arg2 [
      loop i $arg1 [uiline $_c_line 0 $arg3]
   ]
]

// button specific styles below

_style_button_default = [_style_button_hold1]
_style_button_none = []

_style_button_hold1 = [// H
   _style_generic_default
   uihold [_style_generic_gradient1] [
      uihover [
         _style_generic_gradient2
      ]
   ]
]

_style_button_hold2 = [// H, HOVER
   uihover [
      _style_generic_default
      uihold [_style_generic_gradient1] [
         _style_generic_gradient2
      ]
   ]
]

_style_button_hold3 = [// H, ESC
   _style_generic_default
   uihold [_style_generic_gradient1] [
      uieschold [_style_generic_gradient1] [
         uihover [_style_generic_gradient2]
      ]
   ]
]

_style_button_hold4 = [// H, HOVER, ESC
   uihover [
      _style_generic_default
      uihold [_style_generic_gradient1] [
         uieschold [_style_generic_gradient1] [
            _style_generic_gradient2
         ]
      ]
   ]
]

_style_button_hold5 = [// V
   _style_generic_default
   uihold [_style_generic_gradient3] [
      uihover [_style_generic_gradient4]
   ]
]

_style_button_hold6 = [// V, HOVER
   uihover [
      _style_generic_default
      uihold [_style_generic_gradient3] [
         _style_generic_gradient4
      ]
   ]
]

_style_button_hold7 = [// V, ESC
   _style_generic_default
   uihold [_style_generic_gradient3] [
      uieschold [_style_generic_gradient3] [
         uihover [_style_generic_gradient4]
      ]
   ]
]

_style_button_hold8 = [// V, HOVER, ESC
   uihover [
      _style_generic_default
      uihold [_style_generic_gradient3] [
         uieschold [_style_generic_gradient3] [
            _style_generic_gradient4
         ]
      ]
   ]
]

// image button specific styles below

_style_img_default = [_style_img_hold1]
_style_img_none = []

_style_img_hold1 = [// HOVER
   uihold [
      uispace 0.003 0.003 [
         uialign -1 1
         ui_iso_triangle $_c_on1 0.016 0.008 135
      ]
   ] [
      uihover [
         uispace 0.003 0.003 [
            uialign -1 -1
            ui_iso_triangle 0 0.016 0.008 45
         ]
      ]
   ]
]

_style_img_hold2 = [// HOVER, ESC
   uihold [
      uispace 0.003 0.003 [
         uialign -1 1
         ui_iso_triangle $_c_on1 0.016 0.008 135
      ]
   ]
   uieschold [
      uispace 0.003 0.003 [
         uialign 1 1
         ui_iso_triangle $_c_on2 0.016 0.008 225
      ]
   ]
   if (&& uihover? [! (|| uihold? uieschold?)]) [
      uispace 0.003 0.003 [
         uialign -1 -1
         ui_iso_triangle 0 0.016 0.008 45
      ]
   ]
]

