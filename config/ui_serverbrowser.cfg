///////////////////////////////////////////////////////////////////////////////
//  Menu - Server Browser                                                    //
///////////////////////////////////////////////////////////////////////////////
UIMenu "servers" [
   refreshservers
   UIVScroller 1.1 0.8 0.02 3.0 [
      uitable 0.01 [
         uialign 0 -1
         uitableheader [
            uitext ping 0.7
            uitext players 0.7
            uitext mode 0.7
            uitext map 0.7
            uitext time 0.7
            uitext master 0.7
            uitext host 0.7
            uitext port 0.7
            uitext description 0.7
            uialign* -1 0
         ] [
            uiline 0xFFFFFF; uiclamp- 1 1; uialign- 0 1
         ]
         loop i (numservers) [
            if (servinfovalid $i) [
               uitablerow [
                     uitext (servinfoping $i) 0.7
                     uitext (servinfoplayers $i) 0.7
                     uitext (servinfomodename $i) 0.7
                     uitext (servinfomap $i) 0.7
                     uitext (servinfotime $i) 0.7
                     uitext (servinfomastermodename $i) 0.7
                     uitext (servinfoname $i) 0.7
                     uitext (servinfoport $i) 0.7
                     uitext (servinfodesc $i) 0.7
                     uialign* -1 0
               ] [
                  uihold [
                     uicolor 0x404040; uiclamp -1 1 1 1
                     uioutline 0xC0C0C0; uiclamp- 1 1 1 1
                  ] [
                     uihover [
                        uicolor 0x585858; uiclamp- 1 1 1 1
                        uioutline 0xC0C0C0; uiclamp- 1 1 1 1
                     ]
                  ]
                  uirelease [connectservinfo $i]
               ]
            ] [
               uitablerow [] [
                  uitext (servinfodesc $i) 0.7
               ]
            ]
         ]
      ]
   ]
] "Server Browser" [initservers]
