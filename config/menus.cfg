// standard menu definitions
// don't modify, add personal menus to autoexec.cfg instead

UIMenuNoTitle "changes" [
    uivlist 0.01 [
        uitext "the follow settings have changed:"
        uispace 0 0.01
        loop i (pendingchanges) [
            uitext (pendingchanges $i)
        ]
        uispace 0 0.01
        uitext "apply changes now?"
        uihlist 0 [
            UIButton "yes" [ applychanges ]
            uispace 0.02 0
            UIButton "no" [ hideui changes ]
        ]
    ]
]

UIMenu "main" [
    uihlist 0.01 [
        uivlist 0.01 [
            UIButton "Load Map" [
            ]
            UICondButton "Editing" [ $editing ] [
            ]
            UIButton "Multiplayer" [
                hideui main
                showui servers
            ]
            UIButton "Options" [
            ]
            UIButton "Credits" [
            ]
            uispace 0 0.025
            UIButton "Exit" [ quit ]
        ]
        uifill 0.9 0.8 [
        ]
    ]
] "Main Menu"

bind ESCAPE [toggleui main]

UIMenu "textures" [
    UIVScroller 1.3 0.8 0.03 .5 [
        uigrid 12 0.015 [
            loop i (texmru) [
                uivslotview (texmru $i) 0.09 0.09 [
                    uihold [uioutline 0.75 0.75 0.75; uiclamp- 1 1 1 1] [uihover [uioutline 1 1 1; uiclamp- 1 1 1 1]]
                    uirelease [hideui textures; settex (texmru $i)]
                ]
            ]
        ]
    ]
] "Textures"

UIMenu "mapmodels" [
    uihlist 0.01 [
        if (mapmodelname $mapmodelpreviewindex) [
            uimodelpreview (mapmodelname $mapmodelpreviewindex 1) "mapmodel" 0.6 0.6 [
                uihold [uioutline 0.75 0.75 0.75; uiclamp- 1 1 1 1] [uihover [uioutline 1 1 1; uiclamp- 1 1 1 1]]
                uirelease [
                    hideui mapmodels
                    newent mapmodel $mapmodelpreviewindex
                ]
            ]
        ] [
            uifill 0.6 0.6
        ] 
        UIVScroller 0.6 0.8 0.03 0.5 [
            uivlist 0.01 [
                loop i (nummapmodels) [
                    UIButton (mapmodelname $i) [hideui mapmodels; newent mapmodel $i] [
                        uihover [mapmodelpreviewindex = $i]
                    ]
                ]
            ]
        ]
    ]
] "Map Models" [mapmodelpreviewindex = -1]

UIMenu "servers" [
    refreshservers
    UIVScroller 1.3 0.8 0.03 0.5 [
        uitable 0.01 [
            uialign 0 -1
            uitableheader [
                uitext ping
                uitext players
                uitext mode
                uitext map
                uitext time
                uitext master
                uitext port
                uitext description
            ] [
                uiclamp 1 1 1 1
                uiline 1 1 1; uiclamp- 1 1;uialign- 0 1
            ]
            loop i (numservers) [
                if (servinfovalid $i) [
                    uitablerow [
                        uitext (servinfoping $i)
                        uitext (servinfoplayers $i)
                        uitext (servinfomodename $i)
                        uitext (servinfomap $i)
                        uitext (servinfotime $i)
                        uitext (servinfomastermodename $i)
                        uitext (servinfoport $i)
                        uitext (servinfodesc $i)
                    ] [
                        uiclamp 1 1 1 1
                        uihold [
                            uicolor 0.25 0.25 0.25; uiclamp -1 1 1 1
                            uioutline 0.75 0.75 0.75; uiclamp- 1 1 1 1
                        ] [
                            uihover [
                                uicolor 0.35 0.35 0.35; uiclamp- 1 1 1 1
                                uioutline 0.75 0.75 0.75; uiclamp- 1 1 1 1
                            ]
                        ]
                        uirelease [connectservinfo $i]
                    ]
                ] [
                    uitablerow [] [
                        uiclamp 1 1 1 1
                        uitext (servinfodesc $i)
                    ]
                ]
            ]
        ]
    ]
] "Server Browser" [initservers]

