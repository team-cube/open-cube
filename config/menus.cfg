// standard menu definitions
// don't modify, add personal menus to autoexec.cfg instead

if 0 [

togglemainmenu = [
    if (! (cleargui 1)) [
        showgui main
    ]
]

bind ESCAPE [togglemainmenu]

newgui main [
    guilist [
        guitext "name: " (playermodelicon)
        newname = (getname)
        guifield newname 15 [name $newname]
    ]
    guibutton (concat "model:" (playermodelname)) [guirolloveraction = (playermodelbutton $playermodel); showgui playermodel] (playermodelicon)
    guilist [
        guibutton "crosshair: " [showgui crosshair] (playermodelicon)
        guiimage (getcrosshair) [showgui crosshair] 0.5
    ]
    guibar
    guibutton "server browser.."  "showgui servers"
    if (isconnected) [
        guibar
        if (|| $editing (m_edit (getmode))) [
            guibutton "editing.." "showgui editing"
        ]
        guibutton "vote game mode / map.." "showgui gamemode"
        // guibutton "lanconnect"
        // guibutton "connect localhost"                
        guibutton "switch team" [team (? (= (getteam) 1) 2 1)]
        guibutton "toggle spectator" [spectator (! (isspectator (getclientnum)))] "spectator"
        guibutton "master.." [showgui master]
        guibar
        guibutton "options.."  "showgui options"
        guibutton "disconnect" "disconnect"         "exit"
    ] [
        guibutton "bot match.."      "showgui botmatch"
        guibar
        guibutton "options.."        "showgui options"
        guibutton "about.."          "showgui about"
        guibutton "quit"             "quit"                 "exit"
    ]
] 0

newgui about [
    guitext "Tesseract"
    guitext [by Wouter "Aardappel" van Oortmerssen, Lee "eihrul" Salzman,]
    guitext [Mike "Gilt" Dysart, Robert "baby-rabbit" Pointon,]
    guitext [John "geartrooper" Siar, Quinton "Quin" Reeves,]
    guitext [Benjamin Segovia, and others]
    guitext "(for a full list of contributors see the readme)"
    guitext "http://sauerbraten.org/"
]

genmapitems = [
    looplist curmap $arg1 [
        guibutton $curmap (concat map $curmap) "cube"
    ]
]
//17
ffamaps1 = " "
capturemaps1 = " "
ctfmaps1 = " "
conceptmaps = " "

allmaps = (concat $ffamaps1 $capturemaps1 $ctfmaps1 $conceptmaps)

showmapshot = [ 
    guibar
    guiimage (concatword "media/map/" (if (> $numargs 0) [result $arg1] [at $guirollovername 0]) ".jpg") $guirolloveraction 4 1 "data/cube.png"
]

newgui maps [
    guilist [
      guistrut 17 1
      guilist [ guistrut 15 1; genmapitems $ffamaps1 ]
      guilist [ guistrut 15 1; genmapitems $ffamaps2 ]
      showmapshot
    ]
    guitab 2
    guilist [
      guistrut 17 1
      guilist [ guistrut 15 1; genmapitems $ffamaps3 ]
      guilist [ guistrut 15 1; genmapitems $ffamaps4 ]
      showmapshot
    ]
    guitab 3
    guilist [
      guistrut 17 1
      guilist [ guistrut 15 1; genmapitems $ffamaps5 ]
      guilist [ guistrut 15 1; genmapitems $ffamaps6 ]
      showmapshot
    ]
    guitab capture
    guilist [
      guistrut 17 1
      guilist [ guistrut 15 1; genmapitems $capturemaps1 ]
      guilist [ guistrut 15 1; genmapitems $capturemaps2 ]
      showmapshot
    ]
    guitab 2
    guilist [
      guistrut 17 1
      guilist [ guistrut 15 1; genmapitems $capturemaps3 ]
      guilist [ guistrut 15 1; genmapitems $capturemaps4 ]
      showmapshot
    ]
    guitab ctf
    guilist [
        guistrut 17 1
        guilist [ guistrut 15 1; genmapitems $ctfmaps1 ]
        guilist [ guistrut 15 1; genmapitems $ctfmaps2 ]
        showmapshot
    ]
    guitab 2
    guilist [
        guistrut 17 1
        guilist [ guistrut 15 1; genmapitems $ctfmaps3 ]
        guilist [ guistrut 15 1 ]
        showmapshot
    ]

    guitab concept
    guilist [
        guistrut 17 1
        guilist [ guistrut 15 1; genmapitems $conceptmaps ]
        guilist [ guistrut 15 1 ]
        showmapshot
    ]
]

newgui capturemaps [
    guilist [
      guistrut 17 1
      guilist [ guistrut 15 1; genmapitems $capturemaps1 ]
      guilist [ guistrut 15 1; genmapitems $capturemaps2 ]
      showmapshot
    ]
    guitab 2
    guilist [
      guistrut 17 1
      guilist [ guistrut 15 1; genmapitems $capturemaps3 ]
      guilist [ guistrut 15 1; genmapitems $capturemaps4 ]
      showmapshot
    ]
] "capture maps"

newgui ctfmaps [
    guilist [
        guistrut 17 1
        guilist [ guistrut 15 1; genmapitems $ctfmaps1 ]
        guilist [ guistrut 15 1; genmapitems $ctfmaps2 ]
        showmapshot
    ]
    guitab 2
    guilist [
        guistrut 17 1
        guilist [ guistrut 15 1; genmapitems $ctfmaps3 ]
        guilist [ guistrut 15 1 ]
        showmapshot
    ]
] "ctf maps"

custommaps = ""
newgui custommaps [
    looplist curmap $custommaps [
        guibutton $curmap (concat map $curmap) "cube"
    ]
] "custom maps"
showcustommaps = [
    custommaps = ""
    loopfiles curmap "media/map" "ogz" [
        if (< (indexof $allmaps $curmap) 0) [
            custommaps = (concat $custommaps $curmap)
        ]
    ]
    custommaps = (sortlist $custommaps x y [<=s $x $y])
    showgui custommaps
]
macro playermodelbutton [cleargui 1; playermodel %1]

newgui playermodel [
    guilist [
        guilist [
            loop i $playermodelnum [
                guibutton (playermodelname $i) (playermodelbutton $i) (playermodelicon $i)
            ]
        ]
        guibar
        pmidx = (substr $guirolloveraction (strlen (playermodelbutton "")))
        pmidx = (max 0 (min (- $playermodelnum 1) $pmidx))
        guilist [
            guiplayerpreview $pmidx 0 6 $guirolloveraction 4 1
            guitextbox (playermodelstory $pmidx) 40 9
        ]
    ]
]

crosshairs = [
"media/interface/crosshair/default_normal.png media/interface/crosshair/default_hit.png" 
"media/interface/crosshair/x.png media/interface/crosshair/x-hit.png"
"media/interface/crosshair/x_dot.png media/interface/crosshair/x_dot-hit.png"
"media/interface/crosshair/o.png media/interface/crosshair/o-hit.png"
"media/interface/crosshair/o_x.png media/interface/crosshair/o_x-hit.png"
"media/interface/crosshair/o_dot.png media/interface/crosshair/o_dot-hit.png"
"media/interface/crosshair/dot.png media/interface/crosshair/dot-hit.png"
"media/interface/crosshair/dot_wide.png media/interface/crosshair/dot_wide-hit.png"
"media/interface/crosshair/star.png media/interface/crosshair/star-hit.png"
"media/interface/crosshair/wide.png media/interface/crosshair/wide-hit.png"
"media/interface/crosshair/circle_dot.png media/interface/crosshair/circle_dot_hit.png"
"media/interface/crosshair/cross_normal.png media/interface/crosshair/cross_normal_hit.png"
"media/interface/crosshair/cross_whole.png media/interface/crosshair/cross_whole_hit.png"
"media/interface/crosshair/dot_normal.png media/interface/crosshair/dot_normal_hit.png"
"media/interface/crosshair/dot_whole.png media/interface/crosshair/dot_whole_hit.png"
]

newgui crosshair [
    guitext "crosshair size"
    guislider crosshairsize
    loop row (div (+ (listlen $crosshairs) 6) 7) [
        guilist [
            loop col 7 [
                ch = (at $crosshairs (+ (* $row 7) $col))
                if (!=s $ch "") [
                    guiimage (at $ch 0) [ 
                        cleargui 1
                        loadcrosshair @(at $ch 0) 0
                        loadcrosshair @(at $ch (if (> (listlen $ch) 1) 1 0)) 2
                    ] 0.5
                ]
            ]
        ]
    ]
]

newgui master [
  guistayopen [
    if (ismaster (getclientnum)) [
        guibutton "relinquish master" "setmaster 0"
    ] [
        guibutton "claim master" "setmaster 1"
    ]
    guilist [
        guitext "mastermode: "
        mm = (getmastermode)
        guiradio "open (0) " mm 0 "mastermode 0"
        guiradio "veto (1) " mm 1 "mastermode 1"
        guiradio "locked (2) " mm 2 "mastermode 2"
        guiradio "private (3) " mm 3 "mastermode 3"
    ]
    guibar
    looplist cn (listclients 0 0) [
        guilist [
            guitext (concatword (getclientname $cn) ": ") (getclienticon $cn)
            guibutton "kick " [kick @cn]
            guibutton "toggle spectator " [spectator @(= (isspectator $cn) 0) @cn]
            if (m_teammode (getmode)) [
                guibutton "switch team " [setteam @cn @(? (=s (getclientteam $cn) 1) 2 1)]
            ]
            if (ismaster $cn) [
                guibutton "take master" [setmaster 0 @cn]
            ] [
                guibutton "give master" [setmaster 1 @cn]
            ]
        ]
    ]
  ]
]

botmatchcount = 5
botmatchminskill = 50
botmatchmaxskill = 100

startbotmatch = [
    if (isconnected 0 0) [
        echo "You must disconnect from the current multiplayer game before starting a bot match."
    ] [
        loop i $botmatchcount [addbot (rnd (+ $botmatchmaxskill 1) $botmatchminskill)]
    ]
]

newgui botmatch [
    guitext "bot amount"
    guislider botmatchcount 0 32
    guitext "bot minimum skill"
    guislider botmatchminskill 50 101 [if (< $botmatchmaxskill $botmatchminskill) [botmatchmaxskill = $botmatchminskill]]
    guitext "bot maximum skill"
    guislider botmatchmaxskill 50 101 [if (> $botmatchminskill $botmatchmaxskill) [botmatchminskill = $botmatchmaxskill]]
    guibutton "start match.." [guionclear [startbotmatch]; showgui gamemode]
] "bot match"

newgui servers [
    guistayopen [
        guiservers [
            guilist [
                guibutton "update from master server" "updatefrommaster"
                guibar
                guicheckbox "search LAN" searchlan
                guibar
                guicheckbox "auto-update" autoupdateservers
                guibar
                guicheckbox "auto-sort" autosortservers
                if (= $autosortservers 0) [
                    guibar
                    guibutton "sort" "sortservers"
                ]
            ]
            guibar
        ]
    ]
] "" [initservers]

newgui gamemode [
    guibutton "coop-edit"          "mode 0; showgui maps"
    guibutton "ffa"                "mode 1; showgui maps"
    guibutton "teamplay"           "mode 2; showgui maps"
    guibutton "ctf"                "mode 3; showgui ctfmaps"
]

showfileeditor = [
    guinoautotab [
        guieditor $arg1 $arg2 $arg3
        textinit $arg1 $arg1
        guistayopen [
            guilist [
                guibutton "load" [textfocus @arg1; textload @arg1]
                guibar
                guibutton "save" [textfocus @arg1; textsave @arg1]
                guibar
                guibutton "exec" [textfocus @arg1; textexec]
                guibar
                guibutton "copy" [textfocus @arg1; textcopy]
                guibar
                guibutton "paste" [textfocus @arg1; textpaste]
                guibar
                guibutton "select" [textfocus @arg1; textselectall]
                guibar
                guibutton "clear" [textfocus @arg1; textclear]
            ]
        ]
    ]
]

notepadfile = "untitled.txt"

newgui notepad [
    guifield notepadfile -30
    showfileeditor $notepadfile -80 20
]

notepad = [
    if (> $numargs 0) [notepadfile = $arg1]
    showgui notepad
]

newgui pastebuffer [
    guinoautotab [
        guieditor "#pastebuffer" -80 20
        guistayopen [
            guilist [
                guibutton "exec" [textfocus "#pastebuffer"; textexec]
                guibar
                guibutton "clear" [textfocus "#pastebuffer"; textclear]
            ]
        ]
    ]
]

pastebuffer = [showgui pastebuffer]

newgui scratchpad [
    guinoautotab [
        guieditor "#scratchpad" -80 20
        guistayopen [
            guilist [
                guibutton "exec" [textfocus "#scratchpad"; textexec]
                guibar
                guibutton "copy" [textfocus "#scratchpad"; textcopy]
                guibar
                guibutton "paste" [textfocus "#scratchpad"; textpaste]
                guibar
                guibutton "select" [textfocus "#scratchpad"; textselectall]
                guibar
                guibutton "clear" [textfocus "#scratchpad"; textclear]
            ]
        ]
    ]
]

scratchpad = [showgui scratchpad]

newmapsize = 12
savemap_name = temp

newgui editing [
    guibutton "materials.."                           "showgui materials"
    guibutton "toggle edit mode	(E)"              "edittoggle"
    guibutton "toggle heightmap	(H)"              ""
    guibutton "deselect			(SPACE)"                "cancelsel"
    guibutton "passthrough		(hold LCTRL)"               "passthrough"
    guibutton "select			drag left mouse   "	          ""
    guibutton "select corners		drag middle mouse "     ""
    guibutton "extend selection	right mouse"          ""
    guibutton "reorient			right mouse"                ""
    guibutton "move selection		drag right mouse"       ""
    guibutton "copy				(C)"			                  "copy"
    guibutton "paste			(V)"			                "paste"

    guitab ents
    guilist [
      guilist [
        guibutton "drop entities.."              "showgui dropent"
        guibutton "mapmodels.."                  "showgui mapmodels"
        guibutton "light.."                      "resetlight;showgui newlight"

        guibutton "newent playerstart   "
        guibutton "newent shells"
        guibutton "newent bullets"
        guibutton "newent rockets"
        guibutton "newent riflerounds"
        guibutton "newent grenades"
      ]
      guilist [
        guibutton "teleport.."                   "showgui newteleport"

        guibutton "newent cartridges"
        guibutton "newent health"
        guibutton "newent greenarmour"
        guibutton "newent yellowarmour"
      ]
    ]

    guitab keys
    guibutton "flip / mirror cube		(X)"            "flip"
    guibutton "undo					(Z, U)"                     "undo"
    guibutton "redo					(I)"                        "redo"
    guibutton "delete				(BACKSPACE)"                  "editdel"
    guibutton "texture palette     	(F2)" "showtexgui"
    guibutton "edit entity gui			(F3)" "showentgui"
    guibutton "mapmodels list			(F4)" "showgui mapmodels"
    guibutton "select all in box		(keypad ENTER)" "entselect insel"
    guibutton "select all matching		(N)" "selentfindall"
    guibutton "center view on ent		(COMMA + scroll)" "entautoview"
    guibutton "edit entity console		(PERIOD)" "selentedit"
    guitext "toggle heightmap tex	(MMB in heightmapmode)"
    guicheckbox "toggle outline			(7)"        outline
    guicheckbox "toggle wireframe		(8)"    wireframe


    guitab scroll
    guitext "push/pull selection			scroll wheel only"
    guitext "push the face of a cube	hold F + scroll wheel"
    guitext "change gridsize			hold G + scroll wheel"
    guitext "change textures			hold Y + scroll wheel"
    guitext "rotate         			hold R + scroll wheel"
    guitext "push corner with cursor		hold Q + scroll wheel"
    guitext "push/pull heightmap			hold LALT + scroll wheel"
    guitext "change heightmap brush		hold B + scroll wheel"

    guitab lighting
    guibutton "calclight 1 (slow: 8xAA)" "calclight 1"
    guibutton "calclight -1 (quick: no AA, no model shadows)"   "calclight -1"
    guibutton "patchlight"                         "patchlight"

    guicheckbox "fullbright" fullbright

    guitext "lightprecision (default: 32)"
    guilistslider lightprecision "8 16 32 48 64 128 256"

    guitext "lighterror (default: 8)"
    guislider lighterror

    guitext "lightthreads (CPU threads/cores) (default: 1)"
    guislider lightthreads

    //guibutton "lightlod high (6)" "lightlod 6"
    //guibutton "lightlod low  (2)" "lightlod 2"
    //guibutton "lightlod off  (0)" "lightlod 0"


    guitab blend
    guitext "blend paint mode:"
	guiradio "off						(keypad 0)" blendpaintmode 0
	guiradio "replace					(keypad 1)" blendpaintmode 1
	guiradio "dig						(keypad 2)" blendpaintmode 2
	guiradio "fill						(keypad 3)" blendpaintmode 3
	guiradio "inverted dig				(keypad 4)" blendpaintmode 4
	guiradio "inverted fill				(keypad 5)" blendpaintmode 5
    guibar
    guitext "paint blendmap			(MOUSELEFT)"
	guibutton "change blend brush			scroll wheel" "nextblendbrush"
	guibutton "rotate blend brush			(MOUSEMIDDLE)" "rotateblendbrush"
	guibutton "clear entire blendmap 		(^"clearblendmap^" command)" "clearblendmap"
	guibutton "clear blendmap in selection	(^"clearblendmapsel^" command)" "clearblendmapsel"

    guitab map
    guitext   "save map:"
    guifield  savemap_name 20 [ savemap $savemap_name ]
    guibutton [load @savemap_name map]             "map $savemap_name"
    guibutton "newmap (default size: 12)"          "newmap $newmapsize"
    guislider "newmapsize" 10 16
    guibutton "increase mapsize (2x)"              "mapenlarge"
    guitext   "max undo size (default: 5 MB)"
    guislider "undomegs" 1 10
    guibutton "optimize geometry"                  "remip"
    guibutton "set map title/author"               "saycommand [/maptitle ]"
    guibutton "show custom maps"                   "showcustommaps"

    guitab skybox
    guilist [
        guistayopen [
            guilist [ genskyitems $skies1 ]
            //guilist [ genskyitems $skies2 ]
            showskyshot
        ]
    ]

    guitab cfg
    guitext (mapcfgname)
    showfileeditor (mapcfgname) -60 15
]

skies1 = [
black
white
0ptikz/partly-cloudy
0ptikz/sunrise
0ptikz/sunset
cgtextures/clear01
cgtextures/clear-sunset01
cgtextures/dusk01
cgtextures/overcast01
cgtextures/sunset01
dosch/clearsky
]
//skies2 = [...]

genskyitems = [
    looplist cursky $arg1 [
        guibutton $cursky (concat skybox $cursky) //"cube"
    ]
]

alias showskyshot [ guibar; guiimage (concatword "<thumbnail:128>media/skybox/" (if (> $numargs 0) [result $arg1] [result (at $guirollovername 0)]) "_ft.png") $guirolloveraction 4 1 "media/interface/cube.png"]


newgui materials [
    guibutton "air"                        "editmat air"
    guibutton "water"                      "editmat water"
    guibutton "lava"                       "editmat lava"
    guibutton "clip"                       "editmat clip"
    guibutton "glass"                      "editmat glass"
    guibutton "noclip"                     "editmat noclip"
    guibutton "gameclip"                   "editmat gameclip"
    guibutton "death"                      "editmat death"
    guibutton "alpha"                      "editmat alpha"

    guicheckbox "show material volumes"    showmat

    //guibutton "waterlod very high		(3)"  "waterlod 3"
    //guibutton "waterlod high		(2)"          "waterlod 2"
    //guibutton "waterlod default		(1)"  "waterlod 1"
    //guibutton "waterlod low		(0)"          "waterlod 0"

    //guibutton "watersubdiv very fine	(0)"          "watersubdiv 0"
    //guibutton "watersubdiv fine		(1)"  "watersubdiv 1"
    //guibutton "watersubdiv default		(2)"  "watersubdiv 2"
    //guibutton "watersubdiv coarse		(3)"  "watersubdiv 3"

    //guibutton "water waves on"                     "waterwaves 1"
    //guibutton "water waves off"                    "waterwaves 0"
]

setting_entediting = 1

newgui dropent [
    guitext "drop entities:"
    guiradio "to the floor"                 entdrop 1
    guiradio "on selection"                 entdrop 2
    guiradio "to the floor at selection"    entdrop 3
    guiradio "at current position"          entdrop 0
    guibar
    guicheckbox "entity snap-to-grid"       entselsnap
    guicheckbox "entity selection"          setting_entediting 1 0 [ entediting $setting_entediting ]
]

resetlight = [
    lightcolour = 0
    lightbright = 1
    lightset 255 255 255
    lightradius = 128
    lightshadows = 0
]

lightset = [
    lightr = $arg1
    lightg = $arg2
    lightb = $arg3
    if (= $lightbright 0) lightscale
]

lightscale = [
    if (= $lightbright 0) [
        lightr = (div $lightr 2); lightg = (div $lightg 2); lightb = (div $lightb 2)
    ] [
        lightr = (min 255 (* $lightr 2)); lightg = (min 255 (* $lightg 2)); lightb = (min 255 (* $lightb 2))
    ]
]

lightcmd = [
    concat newent light $lightradius $lightr $lightg $lightb $lightshadows
]

newgui newlight [
    guibutton "sunlight"    "newent light 0 255 255 255"
    guibutton (lightcmd)
    guibar
    guitext "color:"
    guicolor (+ (* (+ (* $lightr 256) $lightg) 256) $lightb)
    guislider lightr 0 255
    guislider lightg 0 255
    guislider lightb 0 255
    guilist [
        guicheckbox "bright"    lightbright 1 0 [lightscale]
        guibar
        guiradio "white"        lightcolour 0 [lightset 255 255 255]
        guiradio "blue"         lightcolour 1 [lightset 192 192 255]
        guiradio "red"          lightcolour 2 [lightset 255 192 192]
        guiradio "green"        lightcolour 3 [lightset 192 255 192]
        guiradio "yellow"       lightcolour 4 [lightset 255 255 192]
        guiradio "purple"       lightcolour 5 [lightset 255 192 255]
        guiradio "turquoise"    lightcolour 6 [lightset 192 255 255]
    ]
    guitext "radius:"
    guislider lightradius 0 512
    guilist [
        guitext "shadows"
        guibar
        guiradio "dynamic" lightshadows 0
        guiradio "none" lightshadows 1
        guiradio "static" lightshadows 2
    ]
]

newgui newteleport [
    guibutton "newent teleport 1"
    guibutton "newent teledest 1"
    guibutton "newent teleport 2"
    guibutton "newent teledest 2"
    guibutton "newent teleport 3"
    guibutton "newent teledest 3"
    guibutton "newent teleport 4"
    guibutton "newent teledest 4"
]

newgui mapmodels [
    loop i (div (+ (nummapmodels) (- 17 1)) 17) [
        if (> $i 0) [
            guitab (+ $i 1)
        ]
        guilist [
            guilist [
                loop j (min (- (nummapmodels) (* $i 17)) 17) [
                    guibutton (mapmodelname (+ (* $i 17) $j)) (concat newent mapmodel (+ (* $i 17) $j))
                ]
            ]
            guispring
            guibar
            guimodelpreview $guirollovername "mapmodel" $guirolloveraction 4 1
        ]
    ]
]

newgui postfx [
    guibutton "(effect OFF)"          "clearpostfx"
    guibutton "rotoscope" "rotoscope 1"
    guibutton "rotoscope + blur3" "rotoscope 1 1"
    guibutton "rotoscope + blur5" "rotoscope 1 2"
    guibutton "sobel"  "setpostfx sobel"
    guibutton "invert" "setpostfx invert"
    guibutton "gbr"    "setpostfx gbr"
    guibutton "bw"     "setpostfx bw"
    guibutton "blur3"  "setpostfx hblur3; addpostfx vblur3"
    guibutton "blur5"  "setpostfx hblur5; addpostfx vblur5"
]

macro resbutton [
    guibutton "%1x%2" "screenres %1 %2" (if (&& (= $screenw %1) (= $screenh %2)) [result "radio_on"] [result "radio_off"])
]

newgui options [
    guicheckbox "show FPS" showfps
    guilist [
        guicheckbox "wall clock" wallclock
        if $wallclock [
            guibar
            guicheckbox "24 hour" wallclock24
            guibar
            guicheckbox "seconds" wallclocksecs
        ]
    ]
    guicheckbox "show scoreboard at death" deathscore
    guilist [
        guicheckbox "damage screen"     damagescreen
        guibar
        guicheckbox "damage compass"    damagecompass
    ]
	guilist [
    	guicheckbox "hudguns"       hudgun
		if $hudgun [
			guibar
			guicheckbox "sway"		hudgunsway
            guibar
            guicheckbox "muzzle flash" muzzleflash
            guibar
            guicheckbox "muzzle light" muzzlelight
		]
	]
    guicheckbox "blood"             blood
    guilist [
        guicheckbox "ragdoll deaths"    ragdoll
        if $ragdoll [
            guibar
            guicheckbox "keep after respawn" ragdollmillis 10000
        ]
        guibar
        guicheckbox "hide dead players" hidedead
    ]
    guitext "ragdoll velocity multiplier"
    guislider deadpush
    guilist [
        guicheckbox "fullbright player models" fullbrightmodels 60 0
        if $fullbrightmodels [
            guibar
            guiradio "subtle" fullbrightmodels 60
            guibar
            guiradio "bright" fullbrightmodels 100
            guibar
            guiradio "overbright" fullbrightmodels 150
            guibar
            guiradio "max" fullbrightmodels 200
        ]
    ]
    guilist [
        guicheckbox "force matching player models" forceplayermodels
        guibar
        guicheckbox "always use team skins" teamskins
    ]
    guicheckbox "hit sound" hitsound
    guilist [
        guicheckbox "crosshair color effects" crosshairfx
        if $crosshairfx [
            guibar
            guicheckbox "teammates" teamcrosshair
            guibar
            guicheckbox "hits" hitcrosshair 425
        ]
    ]

    guibar
    guibutton "restore defaults (clears ALL settings)" "exec restore.cfg"

    guitab    "gfx"
    guitext "performance key: ^f0fast^f~, ^f2moderate^f~, ^f3slow and pretty^f~"
    guibar
    guicheckbox "^f0HDR" hdr
    guilist [
        guitext "water			"
        guibar
        guicheckbox "^f3reflection"  waterreflect
        guibar
        guicheckbox "^f0caustics"    caustics
        guibar
        guicheckbox "^f0animation" vertwater
    ]
	guilist [
       	guitext "shadows			"
        guibar
        guiradio "^f0low quality" smfilter 0
        guibar
       	guiradio "^f0medium quality" smfilter 1
        guibar
	    guiradio "^f2high quality" smfilter 2
        guibar
        guiradio "^f3ultra quality" smfilter 3
    ]
    guilist [
        guicheckbox "^f2global illumination	" gi
        if $gi [
            guibar
            guiradio "^f0low quality" rhtaps 12
            guibar
            guiradio "^f2medium quality" rhtaps 20
            guibar
            guiradio "^f3high quality" rhtaps 32
        ]
    ]
    guicheckbox "^f2SSAO" ao
    guicheckbox "^f0temporal AA" tqaa
	guilist [
		guicheckbox "^f2morphological AA	" smaa
		if $smaa [
			guibar
			guiradio "^f2low quality" smaaquality 0 [smaacoloredge 0]
            guibar
            guiradio "^f2medium quality" smaaquality 1 [smaacoloredge 0]
            guibar
            guiradio "^f2high quality" smaaquality 2 [smaacoloredge 0]
            guibar
            guiradio "^f3ultra quality" smaaquality 3 [smaacoloredge 1]
        ]
    ]
    guilist [
        guicheckbox "^f0FXAA				" fxaa
        if $fxaa [
            guibar
            guiradio "^f0low quality" fxaaquality 0
            guibar
            guiradio "^f0medium quality" fxaaquality 1
            guibar
            guiradio "^f2high quality" fxaaquality 2
            guibar
            guiradio "^f3ultra quality" fxaaquality 3
        ]
    ]
    guilist [
        guicheckbox "^f3grass			" grass
		if $grass [
			guibar
			guiradio "^f2quick fade" grassdist 128
			guibar
			guiradio "^f2moderate fade" grassdist 256
			guibar
			guiradio "^f3slow fade" grassdist 512
		]
	]
    guicheckbox "^f0soft particles" softparticles
    guilist [
        guicheckbox "^f0decals			" decals
        if $decals [
            guibar
            guiradio "^f0quick fade" decalfade 10000 [maxdecaltris 1024]
            guibar
            guiradio "^f2slow fade" decalfade 60000 [maxdecaltris 4096]
        ]
    ]
    guilist [
        guitext "models			"
        guibar
        guicheckbox "^f0lighting"    lightmodels
        guibar
        guicheckbox "^f0reflection"  envmapmodels
        guibar
        guicheckbox "^f0glow"        glowmodels
        guibar
        guicheckbox "^f2bumpmap" bumpmodels
    ]
    guilist [
        guitext "animation			"
        guibar
        guiradio "^f0medium quality" matskel 1
        guibar
        guiradio "^f0high quality" matskel 0
    ]

    guitab    "display"
    guilist [
        guicheckbox "v-sync"        vsync 1 0
        if $vsync [
            guibar
            guicheckbox "tear"      vsynctear 1 0
        ]
    ]
    guicheckbox "fullscreen"        fullscreen
    guitext "gamma (default: 100)"
    guislider gamma
    guitext "anisotropic filtering (default: 0)"
    guilistslider aniso "0 2 4 8 16"
    guitext "multisample anti-aliasing (default: 0)"
    guilistslider msaa "0 2 4 8 16"

    guitab   "res"
    guitext "field of view (default: 100)"
    guislider fov
    guistayopen [
      guilist [
        guilist [
            guitext "4:3"
            @@@@(resbutton 320 240)
            @@@@(resbutton 640 480)
            @@@@(resbutton 800 600)
            @@@@(resbutton 1024 768)
            @@@@(resbutton 1152 864)
            @@@@(resbutton 1280 960)
            @@@@(resbutton 1400 1050)
            @@@@(resbutton 1600 1200)
            @@@@(resbutton 1792 1344)
            @@@@(resbutton 1856 1392)
            @@@@(resbutton 1920 1440)
            @@@@(resbutton 2048 1536)
            @@@@(resbutton 2800 2100)
            @@@@(resbutton 3200 2400)
        ]
        guibar
        guilist [
            guitext "16:10"
            @@@@(resbutton 320 200)
            @@@@(resbutton 640 400)
            @@@@(resbutton 1024 640)
            @@@@(resbutton 1280 800)
            @@@@(resbutton 1440 900)
            @@@@(resbutton 1600 1000)
            @@@@(resbutton 1680 1050)
            @@@@(resbutton 1920 1200)
            @@@@(resbutton 2048 1280)
            @@@@(resbutton 2560 1600)
            @@@@(resbutton 2880 1800)
            @@@@(resbutton 3200 2000)
            @@@@(resbutton 3840 2400)
        ]
        guibar
        guilist [
            guitext "16:9"
            @@@@(resbutton 640 360)
            @@@@(resbutton 960 540)
            @@@@(resbutton 1024 600)
            @@@@(resbutton 1280 720)
            @@@@(resbutton 1366 768)
            @@@@(resbutton 1600 900)
            @@@@(resbutton 1920 1080)
            @@@@(resbutton 2048 1152)
            @@@@(resbutton 2560 1440)
            @@@@(resbutton 2880 1620)
            @@@@(resbutton 3200 1800)
            @@@@(resbutton 3840 2160)
        ]
        guibar
        guilist [
            guitext "5:4"
            @@@@(resbutton 600 480)
            @@@@(resbutton 1280 1024)
            @@@@(resbutton 1600 1280)
            @@@@(resbutton 2560 2048)
        ]
        guibar
        guilist [
            guitext "5:3"
            @@@@(resbutton 400 240)
            @@@@(resbutton 800 480)
            @@@@(resbutton 1280 768)

            guibar
            guitext "Custom"
            guilist [
                customw = $screenw
                customh = $screenh
                guifield customw 4 [screenw $customw]
                guifield customh 4 [screenh $customh]
            ]
        ]
      ]
    ]

    guitab   "sound"
    guitext "sound volume"
    guislider soundvol
    guitext "music volume"
    guislider musicvol
    guitext "sound channels"
    guislider soundchans
    guitext "sound frequency"
    guilistslider soundfreq "11025 22050 44100"
    guitext "sound buffer length"
    guislider soundbufferlen
    guicheckbox "Mumble positional audio" mumble

    guitab  "mouse"
    guicheckbox "invert mouse"      invmouse

    guilist [
        guitext "mouse sensitivity: "
        newsens = $sensitivity
        guifield newsens 8 [sensitivity $newsens]
    ]

    guilist [
        guitext "mouse UI sensitivity: "
        newguisens = $guisens
        guifield newguisens 8 [guisens $newguisens]
    ]

    guilist [
        guibutton "crosshair: " [showgui crosshair]
        guiimage (getcrosshair) [showgui crosshair] 0.5
    ]
    guitext "crosshair size"
    guislider crosshairsize

    guitab "keys"
    guitext "(basic keybinds, for anything more use the 'bind' command)"
    guitext "select action to bind and press desired keys (ESC when done):"

    guilistsplit n 2 $bindactions [
        guilist [
            guitext (tabify (concatword $n ": ") 4)
            [newbinds@i] = (searchbinds $n)
            guikeyfield [newbinds@i] -16 [
                oldbinds = (searchbinds [@@n])
                looplist j $oldbinds [bind $j ""]
                looplist j $[newbinds@@i] [bind $j [@@@n]]
            ]
        ]
    ] [guibar]

    guitab "console"
    guicheckbox "chat console" miniconfilter 0x300 0
    guitext "console size (lines)"
    guislider consize
    guitext "console filter:"
    guilist [
        guilist [
            guibitfield "important information" confilter 0x01
            guibitfield "warnings" confilter 0x02
            guibitfield "errors" confilter 0x04
            guibitfield "init messages" confilter 0x10
            guibitfield "script messages" confilter 0x20
	    ]
        guibar
        guilist [
            guibitfield "chat" confilter 0x100
            guibitfield "team chat" confilter 0x200
            guibitfield "game events" confilter 0x400
            guibitfield "self frags" confilter 0x800
            guibitfield "other player frags" confilter 0x1000
        ]
        guibar
        guilist [
            guibitfield "team kills" confilter 0x2000
        ]
    ]
    guitext "full console size (percent of screen)"
    guislider fullconsize
    guitext "full console filter:"
    guilist [
        guilist [
            guibitfield "important information" fullconfilter 0x01
            guibitfield "warnings" fullconfilter 0x02
            guibitfield "errors" fullconfilter 0x04
            guibitfield "init messages" fullconfilter 0x10
            guibitfield "script messages" fullconfilter 0x20
        ]
        guibar
        guilist [
            guibitfield "chat" fullconfilter 0x100
            guibitfield "team chat" fullconfilter 0x200
            guibitfield "game events" fullconfilter 0x400
            guibitfield "self frags" fullconfilter 0x800
            guibitfield "other player frags" fullconfilter 0x1000
        ]
        guibar
        guilist [
            guibitfield "team kills" fullconfilter 0x2000
        ]
    ]

    guitab "autoexec.cfg"
    guitext "autoexec.cfg"
    showfileeditor "autoexec.cfg" -75 15
] "game"

bindactions = [forward backward left right jump attack togglezoom saycommand sayteamcommand showscores toggleconsole screenshot edittoggle "setweapon FI" "setweapon SG" "setweapon CG" "setweapon RL" "setweapon RI" "setweapon GL" "setweapon PI" "weapon" "universaldelta 1" "universaldelta -1" dropflag addbot delbot]

entupdate = [ entset $entguitype $entguiattr0 $entguiattr1 $entguiattr2 $entguiattr3 $entguiattr4 ]

initentgui = [
  entguitype = (enttype)
  @(loopconcat i 5 [result [
    entguiattr@i = (entattr @i)
  ]])
]

genentattributes = [
    entattributes = (loopconcat i (listlen $arg2) [
        entattribname = (at $arg2 $i)
        entattriblimits = (at $arg3 (* 2 $i))
        entattriblimits2 = (at $arg3 (+ 1 (* 2 $i)))
        if (=s (at $entattriblimits 0) $entattriblimits) [
            result [
                guitext @entattribname
                guislider entguiattr@i @entattriblimits @entattriblimits2 entupdate
            ]
        ] [
            result [
                guitext @entattribname
                guinameslider entguiattr@i [@@entattriblimits] [@@entattriblimits2] entupdate
            ]
        ]
    ])
]

guilistsplit = [
  guilist [
    i = 0
    l = (listlen $arg3)
    z = (div (+ $l (- $arg2 1)) $arg2)
    loop a $arg2 [
      guilist [
        t = (min (+ $i $z) $l)
        while [< $i $t] [
          $arg1 = (at $arg3 $i)
          arg4
          i = (+ $i 1)
        ]
      ]
      if (&& (>= $numargs 5) (< (+ $a 1) $arg2)) [arg5]
    ]
  ]
]

quickeditmenu = [
  guitext "Quick Commands:"
  guibar
  guifield  savemap_name 10 [ savemap $savemap_name ]
  guibutton "quick light"       "calclight -1"
  guibutton "optimize map"      "remip"
  guibutton "new entity"        "newent shells"
  guibar
  guibutton newmap
  guibar
  guibutton help "showgui editing"
]

matmenu = [
    guibutton "air"                        "editmat air"
    guibutton "water"                      "editmat water"
    guibutton "lava"                       "editmat lava"
    guibutton "clip"                       "editmat clip"
    guibutton "glass"                      "editmat glass"
    guibutton "noclip"                     "editmat noclip"
    guibutton "gameclip"                   "editmat gameclip"
    guibutton "death"                      "editmat death"
    guibutton "alpha"                      "editmat alpha"
    guicheckbox "show material volumes"    showmat
]

brushmenu = [
  guilist [ // diamonds
  guiimage "media/interface/icon/brush_1c.png" brush_0
  guiimage "media/interface/icon/brush_21c.png" brush_1
  guiimage "media/interface/icon/brush_421c.png" brush_2
  ]
  guilist [ // squares
  guiimage "media/interface/icon/brush_3s.png" brush_3
  guiimage "media/interface/icon/brush_5s.png" brush_4
  guiimage "media/interface/icon/brush_7s.png" brush_5
  ]
  guititle smooth
  guilist [ // smooth
  guiimage "media/interface/icon/brush_3s.png" brush_6
  guiimage "media/interface/icon/brush_5s.png" brush_7
  guiimage "media/interface/icon/brush_7s.png" brush_8
  ]
]

newentgui = [
  genentattributes $arg1 $arg2 $arg3
  newgui $arg1 [
    guitext $entguitype
    guibar
    @entattributes
    guitab type
    guilistsplit n 2 $enttypelist [
        guibutton $n [ entset @n ]
    ]
    guitab misc
    @quickeditmenu
  ] "" [initentgui]
]

looplist i $enttypelist [
  newentgui $i "" ""
]

newgui materials [
  @matmenu
  guitab misc
  @quickeditmenu
]

newgui brushes [
  @brushmenu
  guitab misc
  @quickeditmenu
]

newgui quickedit [
  @quickeditmenu
  guitab materials
  @matmenu
]

newentgui light "radius red green blue shadow" "0 400 0 255 0 255 0 255 [dynamic none static] [0 1 2]"
newentgui spotlight "radius" "0 200"
newentgui playerstart "direction" "0 360"
newentgui teleport "tag" "0 20"
newentgui teledest "direction tag" "0 360 0 20"
newentgui mapmodel "model direction" "0 100 0 360"
newentgui envmap "radius" "0 400"
newentgui jumppad "Z Y X" "0 200 0 200 0 200"
newentgui sound "type radius size" "0 20 0 500 0 500"
newentgui particles "type" "0 3"

contexteditgui = [
    if $hmapedit [showgui brushes] [
        if (enthavesel) [
            showgui (enttype)
        ] [showgui (? (havesel) materials quickedit)]
    ]
]

showentgui = [ contexteditgui ] // legacy bind

newgui seltexinfo [
    guitext (concatword "Slot: " (getseltex))
    guilist [ loop x 5 [
        texname = (gettexname (getseltex) $x)
        texname = (substr $texname (+ (strstr $texname ">") 1)) // chop off any leading commands
        guiimage (concatword "media/" $texname) "" 1 1
    ]]
    loop x 5 [ tex = (gettexname (getseltex) $x); if (strcmp $tex "") [] [guitext $tex]]
]

]

