// standard menu definitions
// don't modify, add personal menus to autoexec.cfg instead

UIMenuNoTitle "changes" [
    uivlist 0.01 [
        uitext "the follow settings have changed:"
        uispace 0 0.01
        loop i (pendingchanges) [
            uitext (pendingchanges $i)
        ]
        uispace 0 0.01
        uitext "apply changes now?"
        uihlist 0 [
            UIButton "yes" [ applychanges ]
            uispace 0.02 0
            UIButton "no" [ hideui changes ]
        ]
    ]
]

UIMenu "main" [
    uihlist 0.01 [
        uivlist 0.01 [
            UIButton "Load Map" [
            ]
            UICondButton "Editing" [ $editing ] [
            ]
            UIButton "Multiplayer" [
                hideui main
                showui servers
            ]
            UIButton "Options" [
            ]
            UIButton "Credits" [
            ]
            uispace 0 0.025
            UIButton "Exit" [ quit ]
        ]
        uifill 0.9 0.8 [
        ]
    ]
] "Main Menu"

bind ESCAPE [toggleui main]

UIEditMenu "textures" [
    UIVScroller 1.3 0.8 0.03 .5 [
        uigrid 12 0.015 [
            looptexmru tex [
                uivslotview $tex 0.09 0.09 [
                    uihold [uioutline 0xC0C0C0; uiclamp- 1 1 1 1] [uihover [uioutline 0xFFFFFF; uiclamp- 1 1 1 1]]
                    uirelease [hideui textures; settex $tex]
                ]
            ]
        ]
    ]
] "Textures"

UIEditMenu "mapmodels" [
    uihlist 0.01 [
        if (mapmodelname $mapmodelpreviewindex) [
            uimodelpreview (mapmodelname $mapmodelpreviewindex 1) "mapmodel" 0.6 0.6 [
                uihold [uioutline 0xC0C0C0; uiclamp- 1 1 1 1] [uihover [uioutline 0xFFFFFF; uiclamp- 1 1 1 1]]
                uirelease [
                    hideui mapmodels
                    newent mapmodel $mapmodelpreviewindex
                ]
            ]
        ] [
            uifill 0.6 0.6
        ] 
        UIVScroller 0.6 0.8 0.03 0.5 [
            uivlist 0.01 [
                loop i (nummapmodels) [
                    UIButton (mapmodelname $i) [hideui mapmodels; newent mapmodel $i] [
                        uihover [mapmodelpreviewindex = $i]
                    ]
                ]
            ]
        ]
    ]
] "Map Models" [mapmodelpreviewindex = -1]

UIMenu "servers" [
    refreshservers
    UIVScroller 1.3 0.8 0.03 0.5 [
        uitable 0.01 [
            uialign 0 -1
            uitableheader [
                uitext ping
                uitext players
                uitext mode
                uitext map
                uitext time
                uitext master
                uitext port
                uitext description
                uialign* -1 0
            ] [
                uiline 0xFFFFFF; uiclamp- 1 1; uialign- 0 1
            ]
            loop i (numservers) [
                if (servinfovalid $i) [
                    uitablerow [
                        uitext (servinfoping $i)
                        uitext (servinfoplayers $i)
                        uitext (servinfomodename $i)
                        uitext (servinfomap $i)
                        uitext (servinfotime $i)
                        uitext (servinfomastermodename $i)
                        uitext (servinfoport $i)
                        uitext (servinfodesc $i)
                        uialign* -1 0
                    ] [
                        uihold [
                            uicolor 0x404040; uiclamp -1 1 1 1
                            uioutline 0xC0C0C0; uiclamp- 1 1 1 1
                        ] [
                            uihover [
                                uicolor 0x585858; uiclamp- 1 1 1 1
                                uioutline 0xC0C0C0; uiclamp- 1 1 1 1
                            ]
                        ]
                        uirelease [connectservinfo $i]
                    ]
                ] [
                    uitablerow [] [
                        uitext (servinfodesc $i)
                    ]
                ]
            ]
        ]
    ]
] "Server Browser" [initservers]

scoreboardtable = [
    uitable 0.01 [
        uiclamp 1 1 0 0
        uialign 0 -1
        if (> $arg1 0) [
            uitableheader [] [
                if (= $arg1 1) [uicolor 0x3333C0] [uicolor 0xC03333]
                    uiclamp- 1 1 1 1
                uispace 0.01 0 [
                    uialign -1 0
                    uitext (getteamname $arg1)
                ]
                uispace 0.01 0 [
                    uialign 1 0
                    uitext (getteamscore $arg1)
                ]
            ]
        ]
        uitableheader [
            if (scoreboardshowfrags) [uitext frags 0.75]
            uitext name 0.75
            if (scoreboardmultiplayer) [
                if $showpj [uitext pj 0.75]
                if $showping [uitext ping 0.75]
            ]
            if (scoreboardshowclientnum) [uitext cn 0.75]
            uialign* -1 0
        ] [
            uiline 0xFFFFFF; uiclamp- 1 1; uialign- 0 1
        ]
        loopscoreboard cn $arg1 [
            uitablerow [
                if (scoreboardshowfrags) [uitext (getclientfrags $cn)]
                uicolortext (getclientcolorname $cn) (scoreboardstatus $cn)
                if (scoreboardmultiplayer) [
                    if $showpj [uitext (scoreboardpj $cn)]
                    if $showping [uitext (scoreboardping $cn)]
                ]
                if (scoreboardshowclientnum) [uitext $cn]
                uialign* -1 0
            ] [
                pushif sbhigh (scoreboardhighlight $cn) [
                    uicolor $sbhigh; uiclamp- 1 1 1 1
                ]
            ]   
        ]
    ]
]

scoreboardspectators = [
    uifill 0 0.01
    if (scoreboardshowclientnum) [
        uitable 0.01 [
            uiclamp 1 1 1 1
            uialign 0 -1
            uitableheader [
                uitext spectator 0.75
                uitext cn 0.75
                uialign* -1 0
            ] [
                uiclamp 1 1 1 1
                uiline 0xFFFFFF; uiclamp- 1 1; uialign- 0 1
            ]
            loopscoreboard cn -1 [
                uitablerow [
                    uicolortext (getclientcolorname $cn) (scoreboardstatus $cn)
                    uitext $cn
                    uialign* -1 0
                ] [
                    pushif sbhigh (scoreboardhighlight $cn) [
                        uicolor $sbhigh; uiclamp- 1 1 1 1
                    ]
                ]
            ]
        ]
    ] [
        uigroup [
            uiclamp 1 1 1 1
            uitext spectators 0.75
            uiline 0xFFFFFF; uiclamp- 1 1; uialign- 0 1
        ]
        uigrid 3 0.01 [
            uiclamp 1 1 1 1
            loopscoreboard cn -1 [
                uicolortext (getclientcolorname $cn) (scoreboardstatus $cn) 0.75 [
                    pushif sbhigh (scoreboardhighlight $cn) [
                        uicolor $sbhigh; uiclamp- 1 1 1 1
                    ]
                ]
            ]
        ]
    ]
]

UIMenuNoTitle scoreboard [if $mainmenu [hideui scoreboard] [
    refreshscoreboard
    uispace 0.01 0.01 [
        uivlist 0.01 [
            pushif sbserv (scoreboardservinfo) [
                uitext $sbserv
            ]
            uihlist 0.01 [
                uitext (scoreboardmode)
                uispace 0.01 0 [uiclamp 0 0 1 1; uiline 0xC0C0C0; uiclamp- 0 0 1 1]
                uitext (scoreboardmap)
                pushif sbtime (scoreboardtime) [
                    uispace 0.01 0 [uiclamp 0 0 1 1; uiline 0xC0C0C0; uiclamp- 0 0 1 1]
                    uitext $sbtime
                ]
                if (!= $gamespeed 100) [
                    uispace 0.01 0 [uiclamp 0 0 1 1; uiline 0xC0C0C0; uiclamp- 0 0 1 1]
                    uitext $prettygamespeed
                ]
                if $paused [
                    uispace 0.01 0 [uiclamp 0 0 1 1; uiline 0xC0C0C0; uiclamp- 0 0 1 1]
                    uitext paused
                ]
            ]
            if (m_teammode (getmode)) [
                uihlist 0.02 [
                    uiclamp 1 1 1 1
                    scoreboardtable 1
                    scoreboardtable 2
                ]
            ] [
                scoreboardtable 0
            ]
            if (numscoreboard -1) [
                scoreboardspectators
            ]
        ]
    ]
]] [] [] [] [] 0

