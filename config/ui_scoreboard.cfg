///////////////////////////////////////////////////////////////////////////////
//  Scoreboard                                                               //
///////////////////////////////////////////////////////////////////////////////
//if (!= $gamespeed 100) [uitext $prettygamespeed]

scoreboardtable = [
   uigrid 2 0.02 0 [
      loopscoreboard cn 0 [
         local ai ; ai = (isai $cn)
         uicolor $c_open1 0 0.032 [
            pushif highlight (scoreboardhighlight $cn) [
               uimodvgradient 0xFFFFFF $highlight
               uiclamp- 1 1 1 1
            ]
            uispace 0.006 0 [
               uialign -1
               uihlist 0.01 [
                  if (= $getfollow $cn) [
                     UItriangle 0 0.01 0.01 270
                  ] [uifill 0.01 0.01]
                  uifill 0.3 0 [
                     uicolortext (getclientcolorname $cn) (scoreboardstatus $cn) 0.6
                     uialign- -1
                  ]
                  if $scoreboardshowfrags [
                     uifill 0.04 0 [
                        uitext (getclientfrags $cn) 0.6
                        uialign- 1
                     ]
                  ]
                  if $scoreboardmultiplayer [
                     if $showpj [
                        uifill 0.04 0 [
                           if (|| $ai [= $cn $getclientnum]) [uitext "^f4-" 0.6] [
                              uitext (scoreboardpj $cn) 0.6
                              uialign- 1
                           ]
                        ]
                     ]
                     if $showping [
                        uifill 0.04 0 [
                           if $ai [uitext "^f4-" 0.6] [
                              uitext (scoreboardping $cn) 0.6
                              uialign- 1
                           ]
                        ]
                     ]
                  ]
                  if (|| $scoreboardshowclientnum $showclientnum) [
                     uifill 0.04 0 [
                        if $ai [uitext "^f4BOT" 0.6] [
                           uitext $cn 0.6
                           uialign- 1
                        ]
                     ]
                  ]
               ]
            ]
            uioutline $c_open2
            uiclamp- 1 1 1 1
         ]
      ]
   ]
]

scoreboardtableteam = [
   if (! (numscoreboard $arg1)) [uifill $sbw] [
      uivlist 0 [
         loopscoreboard cn $arg1 [
            local ai ; ai = (isai $cn)
            uicolor $[c_@(case $arg1 1 [result azul1] 2 [result rojo1])] 0 0.032 [
               pushif highlight (scoreboardhighlight $cn) [
                  uimodvgradient 0xFFFFFF $highlight
                  uiclamp- 1 1 1 1
               ]
               uispace 0.006 0 [
                  uialign -1
                  uihlist 0.01 [
                     if (= $getfollow $cn) [
                        UItriangle 0 0.01 0.01 270
                     ] [uifill 0.01 0.01]
                     uifill 0.3 0 [
                        uicolortext (getclientcolorname $cn) (scoreboardstatus $cn) 0.6
                        uialign- -1
                     ]
                     //if (scoreboardshowflags) [
                     //    uifill 0.03 0 [
                     //        pushif i (getclientfrags $cn) [
                     //            uitext $i 0.6
                     //            uialign- -1
                     //        ]
                     //    ]
                     //]
                     if $scoreboardshowfrags [
                        uifill 0.04 0 [
                           uitext (getclientfrags $cn) 0.6
                           uialign- 1
                        ]
                     ]
                     if $scoreboardmultiplayer [
                        if $showpj [
                           uifill 0.04 0 [
                              if (|| $ai [= $cn $getclientnum]) [uitext "^f4-" 0.6] [
                                 uitext (scoreboardpj $cn) 0.6
                                 uialign- 1
                              ]
                           ]
                        ]
                        if $showping [
                           uifill 0.04 0 [
                              if $ai [uitext "^f4-" 0.6] [
                                 uitext (scoreboardping $cn) 0.6
                                 uialign- 1
                              ]
                           ]
                        ]
                     ]
                     if (|| $scoreboardshowclientnum $showclientnum) [
                        uifill 0.04 0 [
                           if $ai [uitext "^f4BOT" 0.6] [
                              uitext $cn 0.6
                              uialign- 1
                           ]
                        ]
                     ]
                  ]
               ]
               uioutline $[c_@(case $arg1 1 [result azul2] 2 [result rojo2])]
               uiclamp- 1 1 1 1
            ]
         ]
      ]
   ]
]

scoreboardtablespec = [
   uigrid $arg1 0 0 [
      loopscoreboard cn -1 [
         uicolor $c_spec1 0 0.032 [
            pushif highlight (scoreboardhighlight $cn) [
               uimodvgradient 0xFFFFFF $highlight
               uiclamp- 1 1 1 1
            ]
            uispace 0.006 0 [
               uialign -1
               uihlist 0.01 [
                  uifill (+f 0.32 $sbnw) 0 [
                     uicolortext (getclientcolorname $cn) (scoreboardstatus $cn) 0.6
                     uialign- -1
                  ]
                  if (|| $scoreboardshowclientnum $showclientnum) [
                     uifill 0.04 0 [
                        uitext $cn 0.6
                        uialign- 1
                     ]
                  ]
               ]
            ]
            uioutline $c_spec2
            uiclamp- 1 1 1 1
         ]
      ]
   ]
]

scoreboardheader0 = [
   uihlist 0 [
      uiclamp 1 1 1 1
      uihgradient $c_open2 $c_menu 0.3 0 [
         uispace 0.02 0 [
            uivlist 0 [
               uitext (concatword "^f2" $scoreboardmap) 0.6
               if (m_timed $getmode) [
                  uitext (concatword (? (|| $intermission $paused) "^f3" "^f8") $scoreboardtime) 0.6
               ]
               uialign* -1
            ]
            uialign -1
         ]         
         uispace 0.01 0 [
            uicolortext (getteamscore 1) $c_azul2 1.5
            uialign 1
         ]
      ]
      uihgradient $c_menu $c_open2 0.3 0 [
         uispace 0.02 0 [
            uialign 1
            uitext (getmodeprettyname $getmode)
         ]
      ]
      uiclamp* 1 1 1 1
   ]
]

scoreboardheader1 = [
   uihlist 0 [
      uiclamp 1 1 1 1
      uihgradient $c_azul1 $c_menu 0.3 0 [
         uispace 0.02 0 [
            uialign -1
            uivlist 0 [
               uitext (concatword "^f2" $scoreboardmap) 0.6
               if (m_timed $getmode) [
                  uitext (concatword (? (|| $intermission $paused) "^f3" "^f8") $scoreboardtime) 0.6
               ]
               uialign* -1
            ]
         ]         
         uispace 0.01 0 [
            uialign 1
            uicolortext (getteamscore 1) $c_azul2 1.5
         ]
      ]
      uihgradient $c_menu $c_rojo1 0.3 0 [
         uispace 0.01 0 [
            uicolortext (getteamscore 2) $c_rojo2 1.5
            uialign -1
         ]
         uispace 0.02 0 [
            uialign 1
            uitext (getmodeprettyname $getmode)
         ]
      ]
      uiclamp* 1 1 1 1
   ]
   UIbar 0 1
]

// 0.012    BOX PADDING
// 0.02     SPECTATOR ARROW
// 0.30     CLIENT NAME
// 0.04     FLAGS SCORED
// 0.05     FRAGS
// 0.05     PJ
// 0.05     PING
// 0.05     CN

UIwindow "scoreboard" [
   if $mainmenu [hideui "scoreboard"]
   refreshscoreboard
   local m ; m = (m_teammode (getmode))
   // name box width
   sbnw = (+f (? $scoreboardshowfrags 0.05) (
      if $scoreboardmultiplayer [+f (? $showpj 0.05) (? $showping 0.05)]
   ))
   // full box width
   sbw = (+f 0.012 0.02 0.3 $sbnw (
      ? (|| $scoreboardshowclientnum $showclientnum) 0.05
   ))
   uifont "default_outline" [
      uivlist 0.02 [
         uialign 0 -1
         uivlist 0.01 [
            uifill 0 0.04
            uiclip 1 0 [
               uitext (scoreboardservinfo) 0.85
            ]
            uifont "default" [
               uicolor $c_menu (+f 0.02 (*f $sbw 2)) 0.07 [
                  [scoreboardheader@m]
                  uioutline $c_line
                  uiclamp- 1 1 1 1
               ]
            ]
         ]
         if $m [
            uihlist 0.02 [
               scoreboardtableteam 1
               scoreboardtableteam 2
            ]
            scoreboardtablespec 2
         ] [
            scoreboardtable
            scoreboardtablespec 2
         ]
      ]
   ]
] [if $mainmenu [hideui "scoreboard"]] [] []
