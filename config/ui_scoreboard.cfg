///////////////////////////////////////////////////////////////////////////////
//  Scoreboard                                                               //
///////////////////////////////////////////////////////////////////////////////

newui "scoreboard" [
   if $mainmenu [hideui "scoreboard"]
   uiallowinput 0
   refreshscoreboard
   uicolor $c_menu_a 0 0 [
      uivlist 0 [
         UI_sbheader
         uifill 0 0.005
         if (> $getmode 2) [
            UI_sbtopteam
            uifill 0 0.005
            UI_playerlistteam
         ] [
            UI_sbtopsolo
            uifill 0 0.005
            UI_playerlistsolo
         ]
         uifill 0 0.005
         UI_spectatorlist
      ]
      uioutline $c_line_a ; uiclamp- 1 1 1 1
   ]
] [if $mainmenu [hideui "scoreboard"]] []

///////////////////////////////////////////////////////////////////////////////

UI_sbwsolo = 0.75
UI_sbwteam = 1.12
UI_sbheader = [
   uifill 0 0.005
   uifill (? (> $getmode 2) $UI_sbwteam $UI_sbwsolo) 0.05 [
      uifill (-f (? (> $getmode 2) $UI_sbwteam $UI_sbwsolo) (? (= $showip 1) 0.18 0)) 0 [
         uialign -1
         uiclip (-f (? (> $getmode 2) $UI_sbwteam $UI_sbwsolo) (? (= $showip 1) 0.18 0)) 0 [
            uialign -1
            uispace 0.01 0 [
               uivlist 0 [
                  if $scoreboardservinfo [
                     uitext $scoreboardservinfo 0.65
                  ] [
                     uitext "^f4Tesseract" 0.65
                  ]
                  uihlist 0.015 [
                     uifill
                     uihlist 0.003 [
                        uitext "^fs[" 0.52
                        uitext (format "%2%1" (getmastermodename $getmastermode) (at ["^f0" "^f0" "^f2" "^f3"] $getmastermode)) 0.52
                        uitext "^fS]" 0.52
                     ]
                     UItriangle 0x606060 0.01 0.01 270
                     uitext (getmodeprettyname $getmode) 0.52
                     UItriangle 0x606060 0.01 0.01 270
                     uitext $scoreboardmap 0.52
                     if (m_timed $getmode) [
                        UItriangle 0x606060 0.01 0.01 270
                        uitext (concatword (? (|| $intermission $paused) "^f3" "^f8") $scoreboardtime) 0.52
                     ]
                  ]
                  uialign* -1
               ]
            ]
         ]
      ]
      if $showip [
         uifill 0.18 0 [
            uialign 1
            UIbar 0 1; uialign- -1
            uiclip 0.18 0 [
               uialign 1
               uispace 0.01 0 [
                  uivlist 0.004 [
                     uicolortext $servinfoname 0xA0A0A0 0.52
                     uicolortext $servinfoport 0xA0A0A0 0.52
                     uialign* 1
                  ]
               ]
               uialign- 1
            ]
         ]
      ]
   ]
]
//-------------------------------------------------------------------------------------------------
UI_sbtopsolo = [
   uicolor 0x99161616 $UI_sbwsolo 0.022 [
      uihlist 0 [
         uialign 1 1
         if $showkills        [uifill 0.080 0 [uicolortext "K"    0xBBCC8B 0.45 ; uialign- 1]]
         if $showdeaths       [uifill 0.060 0 [uicolortext "D"    0xE57B50 0.45 ; uialign- 1]]
         if $scoreboardmultiplayer [
            if $showpj        [uifill 0.060 0 [uicolortext "PJ"   0x85A2CC 0.45 ; uialign- 1]]
            if $showping      [uifill 0.060 0 [uicolortext "PING" 0x85A2CC 0.45 ; uialign- 1]]
            if $showclientnum [uifill 0.046 0 [uicolortext "#"    0xA0A0A0 0.45 ; uialign- 1]]
         ]
         uifill 0.018
      ]
   ]
]
UI_sbtopteam = [
   uihlist 0 [
      uicolor 0x99202860 (*f $UI_sbwteam 0.5) 0.042 [ // <<-- AZUL
         uihlist 0 [
            uialign -1 1
            uifill 0.018
            if $scoreboardmultiplayer [
               if $showclientnum [uifill 0.046 0 [uicolortext "#"    0xA0A0A0 0.45 ; uialign- -1]]
               if $showping      [uifill 0.060 0 [uicolortext "PING" 0x85A2CC 0.45 ; uialign- -1]]
               if $showpj        [uifill 0.060 0 [uicolortext "PJ"   0x85A2CC 0.45 ; uialign- -1]]
            ]
            if $showdeaths       [uifill 0.060 0 [uicolortext "D"    0xE57B50 0.45 ; uialign- -1]]
            if $showkills        [uifill 0.080 0 [uicolortext "K"    0xBBCC8B 0.45 ; uialign- -1]]
         ]
         uispace 0.01 0 [
            uialign 1
            uicolortext (getteamscore 1) 0x4060D0 1
         ]
      ]
      uicolor 0x99602020 (*f $UI_sbwteam 0.5) 0.042 [ // -->> ROJO
         uispace 0.01 0 [
            uialign -1
            uicolortext (getteamscore 2) 0xD04040 1
         ]
         uihlist 0 [
            uialign 1 1
            if $showkills        [uifill 0.080 0 [uicolortext "K"    0xBBCC8B 0.45 ; uialign- 1]]
            if $showdeaths       [uifill 0.060 0 [uicolortext "D"    0xE57B50 0.45 ; uialign- 1]]
            if $scoreboardmultiplayer [
               if $showpj        [uifill 0.060 0 [uicolortext "PJ"   0x85A2CC 0.45 ; uialign- 1]]
               if $showping      [uifill 0.060 0 [uicolortext "PING" 0x85A2CC 0.45 ; uialign- 1]]
               if $showclientnum [uifill 0.046 0 [uicolortext "#"    0xA0A0A0 0.45 ; uialign- 1]]
            ]
            uifill 0.018
         ]
      ]
   ]
]
//-------------------------------------------------------------------------------------------------
UI_sblc = 1
UI_sblistrowh  = 0.026
UI_sblistwsolo = (-f $UI_sbwsolo 0.02)
UI_sblistwteam = (-f (*f $UI_sbwteam 0.5) 0.02)
UI_playerlistsolo = [
   uivlist 0 [
      uivlist 0 [
         uialign 0 -1
         loopscoreboard cn 0 [
            UI_sblc = (! $UI_sblc)
            uicolor (? $UI_sblc 0x992F2F2F 0x99292929) $UI_sblistwsolo $UI_sblistrowh [
               pushif highlight (scoreboardhighlight $cn) [
                  uifill (-f $UI_sblistwsolo 0.002) (-f $UI_sblistrowh 0.002) [uioutline 0xA0A0A0 ; uiclamp- 1 1 1 1] // outline fix/hack
               ]
               uihlist 0 [
                  uialign -1
                  uifill 0.01
                  uicolortext (getclientcolorname $cn) (scoreboardstatus $cn) 0.6
               ]
               uihlist 0 [
                  uialign 1
                  if $showkills        [uifill 0.080 0 [uicolortext (getclientfrags $cn) 0xBBCC8B 0.52 ; uialign- 1]]
                  if $showdeaths       [uifill 0.060 0 [uicolortext (getclientfrags $cn) 0xE57B50 0.52 ; uialign- 1]]
                  if $scoreboardmultiplayer [
                     if (isai $cn) [
                        if $showpj        [uifill 0.060 0 [uitext "^f4-" 0.52 ; uialign- 1]]
                        if $showping      [uifill 0.060 0 [uitext "^f4-" 0.52 ; uialign- 1]]
                        if $showclientnum [uifill 0.046 0 [uitext "^f4-" 0.52 ; uialign- 1]]
                     ] [
                        if $showpj        [uifill 0.060 0 [uicolortext (scoreboardpj $cn)   0x85A2CC 0.52 ; uialign- 1]]
                        if $showping      [uifill 0.060 0 [uicolortext (scoreboardping $cn) 0x85A2CC 0.52 ; uialign- 1]]
                        if $showclientnum [uifill 0.046 0 [uicolortext $cn                  0xA0A0A0 0.52 ; uialign- 1]]
                     ]
                  ]
                  uifill 0.009
               ]
            ]
         ]
      ]
   ]
]

UI_playerlistteam = [
   uihlist 0 [
      uivlist 0 [ // <<-- AZUL
         uialign 0 -1
         uifill $UI_sblistwteam 0 // empty
         loopscoreboard cn 1 [
            UI_sblc = (! $UI_sblc)
            uicolor (? $UI_sblc 0x9935383B 0x992E3033) $UI_sblistwteam $UI_sblistrowh [
               pushif highlight (scoreboardhighlight $cn) [
                  uifill (-f $UI_sblistwteam 0.002) (-f $UI_sblistrowh 0.002) [uioutline 0xA0A0A0 ; uiclamp- 1 1 1 1] // outline fix/hack
               ]
               uihlist 0 [
                  uialign 1
                  uicolortext (getclientcolorname $cn) (scoreboardstatus $cn) 0.6
                  uifill 0.01
               ]
               uihlist 0 [
                  uialign -1
                  uifill 0.009
                  if $scoreboardmultiplayer [
                     if (isai $cn) [
                        if $showclientnum [uifill 0.046 0 [uitext "^f4-" 0.52 ; uialign- -1]]
                        if $showping      [uifill 0.060 0 [uitext "^f4-" 0.52 ; uialign- -1]]
                        if $showpj        [uifill 0.060 0 [uitext "^f4-" 0.52 ; uialign- -1]]
                     ] [
                        if $showclientnum [uifill 0.046 0 [uicolortext $cn                  0xA0A0A0 0.52 ; uialign- -1]]
                        if $showping      [uifill 0.060 0 [uicolortext (scoreboardping $cn) 0x85A2CC 0.52 ; uialign- -1]]
                        if $showpj        [uifill 0.060 0 [uicolortext (scoreboardpj $cn)   0x85A2CC 0.52 ; uialign- -1]]
                     ]
                  ]
                  if $showdeaths          [uifill 0.060 0 [uicolortext (getclientfrags $cn) 0xE57B50 0.52 ; uialign- -1]]
                  if $showkills           [uifill 0.080 0 [uicolortext (getclientfrags $cn) 0xBBCC8B 0.52 ; uialign- -1]]
               ]
            ]
         ]
      ]
      uifill 0.02 //********************************************************
      uivlist 0 [ // -->> ROJO
         uialign 0 -1
         uifill $UI_sblistwteam 0 // empty
         loopscoreboard cn 2 [
            UI_sblc = (! $UI_sblc)
            uicolor (? $UI_sblc 0x993B3535 0x99332E2E) $UI_sblistwteam $UI_sblistrowh [
               pushif highlight (scoreboardhighlight $cn) [
                  uifill (-f $UI_sblistwteam 0.002) (-f $UI_sblistrowh 0.002) [uioutline 0xA0A0A0 ; uiclamp- 1 1 1 1] // outline fix/hack
               ]
               uihlist 0 [
                  uialign -1
                  uifill 0.01
                  uicolortext (getclientcolorname $cn) (scoreboardstatus $cn) 0.6
               ]
               uihlist 0 [
                  uialign 1
                  if $showkills           [uifill 0.080 0 [uicolortext (getclientfrags $cn) 0xBBCC8B 0.52 ; uialign- 1]]
                  if $showdeaths          [uifill 0.060 0 [uicolortext (getclientfrags $cn) 0xE57B50 0.52 ; uialign- 1]]
                  if $scoreboardmultiplayer [
                     if (isai $cn) [
                        if $showpj        [uifill 0.060 0 [uitext "^f4-" 0.52 ; uialign- 1]]
                        if $showping      [uifill 0.060 0 [uitext "^f4-" 0.52 ; uialign- 1]]
                        if $showclientnum [uifill 0.046 0 [uitext "^f4-" 0.52 ; uialign- 1]]
                     ] [
                        if $showpj        [uifill 0.060 0 [uicolortext (scoreboardpj $cn)   0x85A2CC 0.52 ; uialign- 1]]
                        if $showping      [uifill 0.060 0 [uicolortext (scoreboardping $cn) 0x85A2CC 0.52 ; uialign- 1]]
                        if $showclientnum [uifill 0.046 0 [uicolortext $cn                  0xA0A0A0 0.52 ; uialign- 1]]
                     ]
                  ]
                  uifill 0.009
               ]
            ]
         ]
      ]
   ]
]
//-------------------------------------------------------------------------------------------------
UI_spectatorlist = [
   if $showspectators [
      uicolor 0x99161616 0 0.005 [
         uiclamp 1 1 1 1
         uigrid 2 0.04 0.02 [ // <---------- ????? Wrapping uihlist ?????
            loopscoreboard cn -1 [
               uispace 0.01 0.01 [
                  pushif sbhigh (scoreboardhighlight $cn) [
                     uioutline 0xA0A0A0 ; uiclamp- 1 1 1 1
                  ]
                  uihlist 0.01 [
                     uicolortext (getclientcolorname $cn) (scoreboardstatus $cn) 0.52
                     if $scoreboardmultiplayer [if $showclientnum [uitext $cn 0.52]]
                  ]
               ]
            ]
         ]
      ]
   ]
]
